PRCPRUSP ;WISC/RFJ-usage demand item report (print report)          ;14 Feb 94
V ;;5.1;IFCAP;**1**;Oct 20, 2000
 ;Per VHA Directive 10-93-142, this routine should not be modified.
 Q
 ;
 ;
PRINT ;  print report
 D NOW^%DTC S Y=% D DD^%DT S NOW=Y
 S PAGE=1,SCREEN=$$SCRPAUSE^PRCPUREP U IO D H
 ;
 ;  whse
 I PRCP("DPTYPE")="W" D
 . S NSN=""
 . F  S NSN=$O(^TMP($J,"PRCPRUSE",NSN)) Q:NSN=""  D  Q:$G(PRCPFLAG)
 . . S DESCR=0
 . . F  S DESCR=$O(^TMP($J,"PRCPRUSE",NSN,DESCR)) Q:DESCR']""  D  Q:$G(PRCPFLAG)
 . . . S ITEMDA=0
 . . . F  S ITEMDA=$O(^TMP($J,"PRCPRUSE",NSN,DESCR,ITEMDA)) Q:'ITEMDA  D  Q:$G(PRCPFLAG)
 . . . . S DATA=^TMP($J,"PRCPRUSE",NSN,DESCR,ITEMDA)
 . . . . I $G(ZTQUEUED),$$S^%ZTLOAD S PRCPFLAG=1 W !?10,"<<< TASKMANAGER JOB TERMINATED BY USER >>>" Q
 . . . . I $Y>(IOSL-8) D:SCREEN P^PRCPUREP Q:$D(PRCPFLAG)  D H
 . . . . W !,$TR(NSN,"-"),?15,$E($P(DATA,"^"),1,15)
 . . . . D USAGE
 ;
 ;  primary and secondary
 I PRCP("DPTYPE")'="W" D
 . S GROUP=""
 . F  S GROUP=$O(^TMP($J,"PRCPRUSE",GROUP)) Q:GROUP=""  D  Q:$G(PRCPFLAG)
 . . I $G(ZTQUEUED),$$S^%ZTLOAD S PRCPFLAG=1 W !?10,"<<< TASKMANAGER JOB TERMINATED BY USER >>>" Q
 . . I $Y>(IOSL-8) D:SCREEN P^PRCPUREP Q:$D(PRCPFLAG)  D H
 . . W !!?5,"GROUP: ",$S(GROUP=" ":"<<NONE>>",1:GROUP)
 . . S DESCR=""
 . . F  S DESCR=$O(^TMP($J,"PRCPRUSE",GROUP,DESCR)) Q:DESCR=""  D  Q:$G(PRCPFLAG)
 . . . S ITEMDA=0
 . . . F  S ITEMDA=$O(^TMP($J,"PRCPRUSE",GROUP,DESCR,ITEMDA)) Q:'ITEMDA  D  Q:$G(PRCPFLAG)
 . . . . S DATA=^TMP($J,"PRCPRUSE",GROUP,DESCR,ITEMDA)
 . . . . I $Y>(IOSL-6) D:SCREEN P^PRCPUREP Q:$D(PRCPFLAG)  D H
 . . . . W !,$E($$DESCR^PRCPUX1(PRCP("I"),ITEMDA),1,30)
 . . . . D USAGE
 ;
 I '$G(PRCPFLAG) D END^PRCPUREP
 K ^TMP($J,"PRCPRUSE"),^TMP($J,"PRCPURS1")
 D ^%ZISC
 Q
 ;
 ;
USAGE ;  display usage
 W ?31,ITEMDA
 W ?38,$J($P(DATA,"^",2),8)
 W $J($P(DATA,"^",3),12,3)
 W $J($P(DATA,"^",4),12,3)
 W $J($P(DATA,"^",5),10)
 I $Y>(IOSL-6) D:SCREEN P^PRCPUREP Q:$D(PRCPFLAG)  D H
 S (COLUMN,TOTUSED,TOTCOST,TTOTUSED,TTOTCOST)=0
 S DATE=$E(DATESTRT,1,5)-1
 F  S DATE=DATE+1 S:$E(DATE,4,5)=13 DATE=($E(DATE,1,3)+1)_"01" Q:DATE>$E(DATEEND,1,5)!($G(PRCPFLAG))  D
 .   I $Y>(IOSL-4) D:SCREEN P^PRCPUREP Q:$D(PRCPFLAG)  D H
 .   S DATA=$G(^PRCP(445,PRCP("I"),1,ITEMDA,2,DATE,0))
 .   S MONYR=$P("JAN^FEB^MAR^APR^MAY^JUN^JUL^AUG^SEP^OCT^NOV^DEC","^",+$E(DATE,4,5))_$E(DATE,2,3)
 .   S TOTUSED=TOTUSED+$P(DATA,"^",2),TOTCOST=TOTCOST+$P(DATA,"^",3)
 .   S COLUMN=COLUMN+1
 .   W:COLUMN=1 !?4 W:COLUMN=2 ?31 W:COLUMN=3 ?58
 .   W MONYR,$J(+$P(DATA,"^",2),7),$J(+$P(DATA,"^",3),10,2)
 .   I COLUMN=3 S COLUMN=0
 .   ; S TTOTUSED=TTOTUSED+TOTUSED,TTOTCOST=TTOTCOST+TOTCOST
 .   ; W !?4,"--------------- TOTALS   3-MONTH"
 .   ; W $J(TOTUSED,7),$J(TOTCOST,10,2)
 .   ; W ?57,"CUMLAT",$J(TTOTUSED,7),$J(TTOTCOST,10,2)
 .   ; S (COLUMN,TOTUSED,TOTCOST)=0
 I $G(PRCPFLAG) Q
 ; I COLUMN=0 Q
 S TTOTUSED=TTOTUSED+TOTUSED,TTOTCOST=TTOTCOST+TOTCOST
 ; W !?4,"--------------- TOTALS   3-MONTH",$J(TOTUSED,7),$J(TOTCOST,10,2)
 ; W ?57,"CUMLAT",$J(TTOTUSED,7),$J(TTOTCOST,10,2)
 W !?4,"---------------------------------------- CUMULATIVE TOTAL"
 W ?63,$J(TTOTUSED,7),$J(TTOTCOST,10,2)
 Q
 ;
 ;
H S %=NOW_"  PAGE "_PAGE,PAGE=PAGE+1 I PAGE'=2!(SCREEN) W @IOF
 W $C(13),"USAGE DEMAND ITEM REPORT: ",$E(PRCP("IN"),1,20),?(80-$L(%)),%
 S %="",$P(%,"-",81)=""
 W !?5,"USAGE DATE RANGE FROM ",DATESTRD,"  TO  ",DATEENDD,"  (",TOTALDAY," DAYS)"
 I PRCP("DPTYPE")="W" W !,"NSN",?15,"DESCRIPTION"
 E  W !,"DESCRIPTION"
 W ?31,"MI",?38,$J("UNIT/IS",8),$J("LAST $",12),$J("AVG $",12),$J("ON-HAND",10),!,%
 Q
