ICDDRG0	;ALB/GRR/EG/ADL - DRG GROUPER PROCESSING BEGINS ; 5/16/05 9:05pm
	;;18.0;DRG Grouper;**1,2,7,10,14,17,20**;Oct 20, 2000
	;GROUPING PROCESS BEGINS
	;
GROUP	;
	I $D(ICDSEX(1))&($D(ICDSEX(2))) S ICDRTC=4,ICDDRG=470 G KILL^ICDDRG
	I ICDMDC'=14,ICDMDC'=17,ICDMDC'=18,ICDMDC'=19,ICDMDC'=20,ICDMDC'=23,ICDMDC'=15 D:ICDOPCT<2  I "468^476^477"[ICDRG G END
	. ;I ICDPD["M",ICDOR'["a" S ICDPDRG(344)="",ICDOPCT=0
	. I $D(ICDF) Q
	. I ICDPD["M",ICDOR'["y" S ICDOPCT=0 Q
	.I ICDORNI["O",ICDNOR=ICDONR,ICDNOR>0,'$D(ICDPDRG(377)),ICDORNI'["p" S ICDRG=$S($D(ICDORNI("O")):468,ICDORNI["y":476,ICDORNI["z":477,1:468) Q
	.I ICDOPNR S ICDRG=$S(ICDORNI["y":476,1:468),ICDOPNR=0 Q
	;
	;if number of non-extensive ORs eqs # OR, 477
	;
	I ICDMDC'=14,ICDMDC'=17,ICDMDC'=18,ICDMDC'=19,ICDMDC'=20,ICDMDC'=23,ICDMDC'=15,ICDORNI'["y"&(ICDORNI'="")&(ICDORNI["z") D  I ICDRG=477 G END
	. I $D(ICDF) Q
	. NEW K S K=$$ORNI(ICDORNI) I K=ICDOPCT S ICDRG=477 Q
	;
	;if number of non-extensive ORs+prostatics eqs # OR, 476
	;
	I ICDMDC'=14,ICDMDC'=17,ICDMDC'=18,ICDMDC'=19,ICDMDC'=20,ICDMDC'=23,ICDMDC'=15,ICDORNI["y"&(ICDORNI'="") D  I ICDRG=476 G END
	.N K S K=$$ORNI(ICDORNI) I K=ICDOPCT&(ICDNOR=ICDONR) S ICDRG=476 Q
	I ICDMDC'=14,ICDMDC'=17,ICDMDC'=18,ICDMDC'=19,ICDMDC'=20,ICDMDC'=23,ICDMDC'=15,ICDNOR=ICDONR&(ICDOPCT>0) S ICDRG=468 G END
	I ICDMDC=5,ICDOR'["O" S ICDRTC=$S(ICDEXP="":5,1:"") S:ICDRTC'="" ICDRG=470 D:ICDRTC="" MI G END
	I ICDMDC=18,ICDOR["O"!(ICDORNI["O") S ICDRG=415 G END
	I ICDMDC=19,ICDOCNT>0,ICDOR["O" S (ICDRG,HICDRG)=424 D CKDRG
	I ICDMDC=23,ICDOR["O"!(ICDORNI["O") S ICDRG=461 G END
	I ICDMDC=14 D ^ICDDRG14 I ICDRG]"" G END
	I ICDMDC=20 S ICDRTC=$S(ICDDMS="":7,1:"") I ICDDMS'=0 S ICDRG=$S(ICDDMS="":470,1:433) G END
	I ICDMDC=22 S ICDRTC=$S(ICDTRS="":6,1:"") S:ICDRTC'="" ICDRG=470 D:ICDRTC="" CKBURN G END
	I ICDMDC=15 S ICDRTC=$S(ICDEXP="":5,ICDTRS="":6,1:"") I ICDTRS'=0 S ICDRG=$S(ICDRTC'="":470,1:385) G END
NEONATE	I 'ICDNOR!('$D(ICDODRG)) S ICDRG=$O(ICDPDRG(0)) X "I ICDMDC=15,$D(ICDSDRG),$O(ICDSDRG(0))<ICDRG S ICDRG=$S($D(ICDPDRG(391)):391,$D(ICDPDRG(387)):387,1:$O(ICDSDRG(0)))" D  D DODRG G GETMOR:ICDRG="",END
	. N X,X1,X2,%
	. S X1=$S($G(DGADM):$G(DGADM),1:DT),X2=$G(DOB) I X1,X2 D ^%DTC I X<29 D NBCOMP Q
	. I ICDRG<385!(ICDRG>391) Q
	.; I "^11917^11918^11921^"[("^"_ICDDX(1)_"^") S ICDRG=395 Q
	. I $O(ICDRG(391)) S ICDRG=$O(ICDRG(391)) Q
	. I 'ICDRG S ICDRG=470,ICDRTC=8
	I AGE="",ICDMDC=3 S ICDRTC=3,ICDRG=470 G END
	D ^ICDDRG1:ICDMDC=1,^ICDDRG2:ICDMDC=2,^ICDDRG3:ICDMDC=3,^ICDDRG5:ICDMDC=5,^ICDDRG6:ICDMDC=6,^ICDDRG7:ICDMDC=7,^ICDDRG8:ICDMDC=8,^ICDDRG9:ICDMDC=9,^ICDDRG10:ICDMDC=10,^ICDDRG12:ICDMDC=12,^ICDDRG13:ICDMDC=13,^ICDDRG17:ICDMDC=17
CONT	G:ICDMDC=15 GETMOR S (ICDRG,HICDRG)=$O(ICDODRG(0)) G:ICDRG'>0 ENTER
	D DODRG
	G:ICDRG'>0 LOOK8:ICDMDC=8,AGAIN G END
ENTER	I 'ICDNOR,ICDORNR'=0,ICDMDC'=20,ICDMDC'=15 S ICDRG=468 G END
GETMOR	S (ICDRG,HICDRG)=$O(ICDPDRG(0)) S:ICDRG'>0 (ICDRG,HICDRG)=469 ;I ICDMDC=15,'$D(ICDODRG),$D(ICDSDRG),$O(ICDSDRG(0))<ICDRG S (ICDRG,HICDRG)=$O(ICDSDRG(0))
CKDRG	D DODRG
	I ICDRG="" K ICDPDRG(HICDRG) G GETMOR
DODRG	;Go to DRG file and retrieve table entry to use if defined
	N ICDMCV,ICDMCV1,ICDMCV2
	N DRGFY,ICDREF S (DRGFY,ICDREF)=""
	I ICDRG S DRGFY=$O(^ICD(ICDRG,2,"B",$P(+$G(ICDDATE),".")_.01),-1)
	I 'DRGFY S DRGFY=3051001 ;default to current fiscal year
	S ICDREF=$O(^ICD(+ICDRG,2,"B",+DRGFY,ICDREF))
	I ICDREF'="" D
	. S ICDREF=$P($G(^ICD(+ICDRG,2,ICDREF,0)),U,3)
	. S ICDREF="DRG"_ICDRG_"^"_ICDREF D @ICDREF K ICDREF
	I ICDOR["4" D DRG232^ICDTLB3
	Q
ORNI(X)	;
	N I,K
	S K=0 F I=1:1:$L(ICDORNI) I $E(ICDORNI,I,I)="z"!($E(ICDORNI,I,I)="y") S K=K+1
	Q K
END	;
	D:ICDP24'=""!($D(ICDS24)) CKMST^ICDDRGX S ICDDRG=ICDRG
	D:$G(ICDP25)=1!(($G(ICDP25)>1)&($D(ICDS25(1)))) CKHIV^ICDDRGX S ICDDRG=ICDRG
	; this will effectively make DRG 103 into a pre-MDC (ICD*18*1)
	I $D(ICDOP(" 33.6"))!$D(ICDOP(" 37.5"))!(ICDDATE>3030930.9&($D(ICDOP(" 37.51"))!$D(ICDOP(" 37.66")))) S ICDRG=103,ICDNMDC(1)=""
	I (ICDDATE>3050930.9)&($D(ICDOP(" 37.64")))&($D(ICDOP(" 37.65"))) S ICDRG=103,ICDNMDC(1)=""
	; this will create DRGs 512/513 as pre-MDC
	I $D(ICDOP(" 52.80"))!$D(ICDOP(" 52.82")) S ICDRG=513,ICDNMDC(1)=""
	I ICDRG=513 I $D(ICDOP(" 55.69")) S ICDRG=512
	; this will create DRG 481 as pre-MDC - loops thru 41.00 thru .09
	N X S X=0 F  S X=$O(ICDOP(X)) Q:X=""  I X["41.0" S ICDRG=481,ICDNMDC(1)=""
	I $D(ICDNMDC(1)) I ICDNMDC(1)="" D CKNMDC^ICDDRGX S ICDDRG=ICDRG K ICDNMDC
	I ICDRG=468 D CHKMDC4^ICDDRGX D DODRG S ICDDRG=ICDRG
	S:ICDRTC="" ICDRTC=0
	S ICDTMP=$$DRG^ICDGTDRG(ICDDRG,ICDDATE) I '$P(ICDTMP,U,14) S ICDDRG=470
	G KILL^ICDDRG
MI	;
	; if PTCA and not a bypass
	I ICDOR["1"!($D(ICDOP(" 37.90"))) I ICDOR'["b"&(ICDOR'["6") I ICDDATE>3050930.9 D DRG516^ICDTLB6B Q
	I ICDPD["A" D EN1^ICDDRG5 I ICDCC3 S ICDRG=$O(ICDODRG(0)) D DODRG Q
	I ICDPD["AI"!(ICDSD["AI") D  Q
	. I $D(ICDOP(" 36.07")) I $D(ICDOP(" 37.26"))!($D(ICDOP(" 37.27"))) S ICDRG=526 Q
	. S ICDRG=$S($S($D(ICDEXP):ICDEXP,1:0):123,ICDPD["V"!(ICDSD["V"):121,1:122)
	I $D(ICDOP(" 37.26"))&($D(ICDOP(" 39.61"))) S ICDRG=108 Q
	;I $D(ICDOP(" 37.26")) S ICDRG=112 Q
	I $D(ICDOP(" 36.07")) I $D(ICDOP(" 37.26"))!($D(ICDOP(" 37.27"))) S ICDRG=527 Q
	I $D(ICDOP(" 36.06")) I $D(ICDOP(" 37.26"))!$D(ICDOP(" 37.27")) S ICDRG=517 Q
	I $D(ICDOP(" 37.26"))!$D(ICDOP(" 37.27")) S ICDRG=518 Q 
	I ICDOR["H" S ICDRG=$S(ICDPD["X"!(ICDSD["X"):124,1:125) Q
	K ICDPDRG(124)
	I ICDOR["p" S ICDRG=$O(ICDODRG(0)) D DODRG Q
	I ICDOR["F" S ICDRG=$O(ICDODRG(0)) D DODRG Q
	E  K ICDPDRG(121) S ICDRG=$O(ICDPDRG(0)) D DODRG Q
	;
CKBURN	; MDC22 - Burns (extensive, full thickness, or non-extensive)
	D
	. I ICDPD["*"!(ICDSD["*") S ICDRG=$S(ICDOR["k":504,1:505) Q
	. I ICDPD["b"!(ICDSD["b") D FTBURN Q
	. S ICDRG=$S(ICDCC!(ICDPD["T")!(ICDSD["T"):510,1:511)
	Q
	;
AGAIN	G:'$D(ICDODRG) ENTER
	K ICDODRG(HICDRG) I $O(ICDODRG(HICDRG))'>0 K ICDODRG G GROUP
	S ICDRG=$O(ICDODRG(HICDRG)) G GROUP
	;
	;
LOOK8	G:'$D(ICDJ) GETMOR
	S ICDJ=$O(ICDJ(0)) G:ICDJ'>0 GETMOR
	K ICDJ(ICDJ),ICDODRG D END^ICDDRG8 G GETMOR:'$D(ICDODRG),CONT
	Q
	;
NBCOMP	; check for complication related to NB
	I ICDSD'["J"!'$D(ICDSDRG) Q
	N ICDSDXCK
	S ICDSDXCK=$O(ICDSDRG(0))
	I ICDSDXCK<ICDRG,ICDSDXCK>384,ICDSDXCK<392 D
	. S ICDRG=$S($D(ICDPDRG(391)):391,$D(ICDPDRG(387)):387,1:$O(ICDSDRG(0)))
	Q
	;
FTBURN	; full thickness burn check
	I ICDSD["j"!(ICDOR["k") D
	. I ICDCC!(ICDPD["T")!(ICDSD["T") S ICDRG=506
	. E  S ICDRG=507
	E  D
	. I ICDCC!(ICDPD["T")!(ICDSD["T") S ICDRG=508
	. E  S ICDRG=509
	Q
