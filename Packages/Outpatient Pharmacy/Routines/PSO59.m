PSO59 ;BHM/DB - Outpatient Site File API ;1 JUL 05
 ;;7.0;OUTPATIENT PHARMACY;**213,229**;DEC 1997
PSS(PSOIEN,PSOTXT,LIST) ;
 N DA,DIC,DIQ,DR,X,I
 I $G(PSOIEN)="",$G(PSOTXT)="" Q
 I $G(LIST)="" Q
 K ^TMP($J,LIST) S:$G(PSOIEN)>0 ^TMP($J,LIST,PSOIEN,0)=0
 I $G(PSOIEN)>0,'$D(^PS(59,PSOIEN,0)) S ^TMP($J,LIST,PSOIEN,0)="-1^NO DATA FOUND" Q
 I $G(PSOTXT)'="",'$D(^PS(59,"B",PSOTXT)),$G(PSOIEN)>0 S ^TMP($J,LIST,PSOIEN,0)="-1^NO DATA FOUND" Q
 S DA=$S($G(PSOIEN)]"":PSOIEN,1:$O(^PS(59,"B",PSOTXT,0)))
 I $G(DA)'>0 S ^TMP($J,LIST,0)="-1^NO DATA FOUND" Q
 K ^UTILITY("DIQ1",$J),DIC S DIC=59,DR=".01;.02;.05;.06;.07;.08;1;100;1003",DIQ(0)="IE" D EN^DIQ1
 I '$D(^UTILITY("DIQ1",$J)) S ^TMP($J,LIST,PSOIEN,0)="-1^NO DATA FOUND" Q
 F X=.01,.02,.05,.06,.07,.08,1,100,1003 D
 .I $G(^UTILITY("DIQ1",$J,59,DA,X,"I"))'=$G(^UTILITY("DIQ1",$J,59,DA,X,"E")) S ^TMP($J,LIST,DA,X)=$G(^UTILITY("DIQ1",$J,59,DA,X,"I"))_"^"_$G(^UTILITY("DIQ1",$J,59,DA,X,"E")) Q
 .S ^TMP($J,LIST,DA,X)=$G(^UTILITY("DIQ1",$J,59,DA,X,"I"))
 S PSOTXT=$G(^UTILITY("DIQ1",$J,59,DA,.01,"E")) S ^TMP($J,LIST,"B",PSOTXT,DA)=""
 S ^TMP($J,LIST,DA,0)=$G(^TMP($J,LIST,DA,0))+1
 K DA,DIC,DIQ,DR,PSOIEN,PSOTXT
 Q
