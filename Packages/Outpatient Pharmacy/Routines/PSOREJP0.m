PSOREJP0 ;BIRM/MFR - Third Party Rejects Processing Screen ;04/28/05
 ;;7.0;OUTPATIENT PHARMACY;**148**;DEC 1997
 ;
 N PSOREJST,PSORJSRT,PSORJASC,PSOSTFLT,PSODRFLT,PSOPTFLT,PSORXFLT,PSOINFLT
 ;
 ; - Division/Site selection
 D SEL^PSOREJU1("DIVISION","^PS(59,",.PSOREJST) I $G(PSOREJST)="^" G EXIT
 ;
 S PSORJSRT="PA",PSORJASC=1,PSOSTFLT="U",(PSODRFLT,PSOPTFLT,PSORXFLT,PSOINFLT)="ALL"
 D LST
 G EXIT
 ;
LST ; - Invokes Listmanager
 W !,"Please wait..." D EN^VALM("PSO REJECTS PROCESSING") D FULL^VALM1
 Q
 ;
HDR      ; - Header code
 N LINE1,LINE2
 S LINE1=$$SITES(),LINE2="" I $L(LINE1)>80 S $E(LINE1,78,999)="..."
 ;
 S LINE2="Selection: ALL "_$S(PSOSTFLT="U":"UNRESOLVED ",PSOSTFLT="R":"RESOLVED ",1:"")_"REJECTS"
 I $G(PSOPTFLT)'="ALL" S LINE2=LINE2_" FOR "_$$NAME("P")
 I $G(PSODRFLT)'="ALL" S LINE2=LINE2_" FOR "_$$NAME("D")
 I $G(PSORXFLT)'="ALL" S LINE2=LINE2_" FOR PRESCRIPTION "_$$NAME("R")
 I $G(PSOINFLT)'="ALL" S LINE2=LINE2_" FOR "_$$NAME("I")
 S VALMHDR(1)=LINE1,VALMHDR(2)=LINE2
 ;
 D SETHDR()
 Q
 ;
SETHDR() ; Displays the Header Line
 N HDR,ORD
 ;
 S HDR="  #",$E(HDR,5)="Rx#",$E(HDR,18)="PATIENT(ID)",$E(HDR,43)="DRUG",$E(HDR,64)="REASON"
 S ORD=$S(PSORJASC=1:"[^]",1:"[v]")
 S:PSORJSRT="RX" $E(HDR,8,10)=ORD S:PSORJSRT="PA" $E(HDR,29,31)=ORD
 S:PSORJSRT="DR" $E(HDR,47,49)=ORD S:PSORJSRT="RE" $E(HDR,70,72)=ORD
 S $E(HDR,81)="" D INSTR^VALM1(IORVON_HDR_IOINORM,1,4)
 Q
 ;
INIT ; Populates the Body section for ListMan
 K ^TMP("PSOREJP0",$J)
 D SETSORT(PSORJSRT),SETLINE
 S VALMSG="Select the entry # to view or ?? for more actions"
 Q
 ;
SETLINE ; Sets the line to be displayed in ListMan
 N SUB,SEQ,LINE,Z,X,TOTAL,LUCRO,QTDE,TOT,I,X1,X2
 I '$D(^TMP("PSOREJSR",$J)) D  Q
 . F I=1:1:7 S ^TMP("PSOREJP0",$J,I,0)=""
 . S ^TMP("PSOREJP0",$J,8,0)="               No Clinical Third Party Payer Rejects found."
 . S VALMCNT=1
 S SUB="",LINE=0 K ^TMP("PSOREJP0",$J)
 F  S SUB=$O(^TMP("PSOREJSR",$J,SUB),PSORJASC) Q:SUB=""  D
 . S Z=$G(^TMP("PSOREJSR",$J,SUB))
 . S X1="",SEQ=$G(SEQ)+1,X1=$J(SEQ,3)
 . S $E(X1,5)=$P(Z,"^",3),$E(X1,18)=$P(Z,"^",4),$E(X1,43)=$P(Z,"^",5),$E(X1,64)=$P(Z,"^",6)
 . S LINE=LINE+1,^TMP("PSOREJP0",$J,LINE,0)=X1
 . S X2="",$E(X2,5)="Payer Message: "_$P(Z,"^",7)
 . S LINE=LINE+1,^TMP("PSOREJP0",$J,LINE,0)=X2
 . S ^TMP("PSOREJP0",$J,SEQ,"RX")=$P(Z,"^",1,2)
 F I=1:1:LINE D
 . D:(I#2) CNTRL^VALM10(I,1,80,IOINHI,IOINORM),CNTRL^VALM10(I,64,3,IOUON,IOINORM),CNTRL^VALM10(I,67,80,IOINHI,IOINORM)
 S VALMCNT=+$G(LINE)
 Q
 ;
SETSORT(FIELD) ; - Sets the data sorted by the FIELD specified
 N RX,STS
 K ^TMP("PSOREJSR",$J)
 S RX=0
 F STS=0,1 D
 . I PSOSTFLT="U",STS=1 Q
 . I PSOSTFLT="R",STS=0 Q
 . I $G(PSORXFLT)'="ALL" D SETTMP(+PSORXFLT,STS) Q
 . F  S RX=$O(^PSRX("REJSTS",STS,RX)) Q:'RX  D
 . . D SETTMP(RX,STS)
 Q
 ;
SETTMP(RX,STS) ; - Sets ^TMP global that will be displayed in the body section
 N REJ,REJLST,FILL,CODE,RXNUM,PTNAME,DRNAME,MSG,REASON,MSG,X,Z,SORT,I
 I $$FILTER(RX) Q
 S REJ=0
 F  S REJ=$O(^PSRX("REJSTS",STS,RX,REJ)) Q:'REJ  D
 . I $G(PSORXFLT)="ALL",$$CLOSED^PSOREJP1(RX,REJ),$$REOPN^PSOREJP1(RX,REJ) Q
 . S FILL=+$$GET1^DIQ(52.25,REJ_","_RX,5)
 . I '$$DIV(RX,FILL) Q
 . K REJLST D GET^PSOREJU2(RX,FILL,.REJLST,,1) I '$D(REJLST) Q
 . I $$FILTER(,REJLST(REJ,"INSURANCE NAME")) Q
 . S PTNAME=$$PTNAME(RX)
 . S DRNAME=$$GET1^DIQ(52,RX,6)
 . S RXNUM=$$GET1^DIQ(52,RX,.01)
 . S CODE=$G(REJLST(REJ,"CODE"))
 . S MSG=$G(REJLST(REJ,"PAYER MESSAGE")) I $L(MSG)>60 S MSG=$E(MSG,1,58)_"..."
 . S REASON=$S(CODE=88:"DUR:"_$G(REJLST(REJ,"REASON")),1:"79 :REFILL TOO SOON")
 . S Z="",$P(Z,"^")=RX,$P(Z,"^",2)=REJ,$P(Z,"^",3)=RXNUM,$P(Z,"^",4)=PTNAME
 . S $P(Z,"^",5)=$E(DRNAME,1,20),$P(Z,"^",6)=$E(REASON,1,17),$P(Z,"^",7)=MSG
 . S SORT=$S(FIELD="PA":PTNAME,FIELD="DR":DRNAME,FIELD="RX":RXNUM_" ",1:REASON)_RX_REJ
 . S ^TMP("PSOREJSR",$J,SORT)=Z
 Q
 ;
PAT ; Sort by Patient
 D SORT("PA")
 Q
DRG ; Sort by Drug
 D SORT("DR")
 Q
RX ; Sort by Rx
 D SORT("RX")
 Q
REA ; Sort by Reason
 D SORT("RE")
 Q
SORT(FIELD) ; Sort entries by FIELD
 I PSORJSRT=FIELD S PSORJASC=$S(PSORJASC=1:-1,1:1)
 E  S PSORJSRT=FIELD,PSORJASC=1
 D REF
 Q
 ;
REF ; Screen Refresh
 W ?52,"Please wait..." D INIT S VALMBCK="R"
 Q
 ;
SEL ; Process selection of one entry
 N PSOSEL,XQORM,Z,RX,REJ,PSOCHNG
 S PSOSEL=+$P($P(Y(1),"^",4),"=",2) I 'PSOSEL S VALMSG="Invalid selection!",VALMBCK="R" Q
 S Z=$G(^TMP("PSOREJP0",$J,PSOSEL,"RX"))
 S RX=$P(Z,"^"),REJ=$P(Z,"^",2) I 'RX!'REJ S VALMSG="Invalid selection!",VALMBCK="R" Q
 S PSOCHNG=0 D EN^PSOREJP1(RX,REJ,.PSOCHNG) I $G(PSOCHNG) D REF
 Q
 ;
EXIT ;
 K ^TMP("PSOREJP0",$J),^TMP("PSOREJSR",$J)
 Q
 ;
HELP Q
 ;
SITES() ; Returns the list of sites along with their NCPDP #s
 N CNT,SITE,SITES,NAME
 I '$D(PSOREJST) Q ""
 I $G(PSOREJST)="ALL" Q "Divisions: ALL"
 S SITE=0 F  S SITE=$O(PSOREJST(SITE)) Q:'SITE  D
 . S NAME=$$GET1^DIQ(59,SITE,.01)
 . S SITES=$G(SITES)_", "_NAME
 S $E(SITES,1,2)="",SITES="Division"_$S($L(SITES,",")>1:"s",1:" ")_": "_SITES
 Q SITES
 ;
DIV(RX,FILL) ; Check if the Division for the Prescription/Fill was selected by the user
 ;
 I $G(PSOREJST)="ALL" Q 1
 I $D(PSOREJST($$RXSITE^PSOBPSUT(RX,FILL))) Q 1
 Q 0
 ;
PTNAME(RX) ; Returns header displayable Patient Name (Last 4 SSN)
 N DFN,VADM,PTNAME
 S DFN=$$GET1^DIQ(52,RX,2,"I") D DEM^VADPT
 S PTNAME=$E($G(VADM(1)),1,18)_"("_$P($P($G(VADM(2)),"^",2),"-",3)_")"
 Q PTNAME
 ;
FILTER(RX,INS) ; - Filter entries based on user's selection
 N FILTER,NAME
 S FILTER=1
 I $G(PSOPTFLT)'="ALL",$D(RX),'$D(PSOPTFLT($$GET1^DIQ(52,RX,2,"I"))) Q FILTER
 I $G(PSODRFLT)'="ALL",$D(RX),'$D(PSODRFLT($$GET1^DIQ(52,RX,6,"I"))) Q FILTER
 I $G(PSOINFLT)'="ALL",$D(INS) D  Q FILTER
 . S NAME="" F  S NAME=$O(PSOINFLT(NAME)) Q:NAME=""  I $$UP^XLFSTR(INS)[$$UP^XLFSTR(NAME) S FILTER=0 Q
 Q 0
 ;
NAME(TYPE) ; - Returns the name if ONE was selected or "MULTIPLE ..."
 N I,CNT
 ;
 I TYPE="P",$O(PSOPTFLT($O(PSOPTFLT(""))))="" Q $$GET1^DIQ(2,$O(PSOPTFLT("")),.01)
 I TYPE="D",$O(PSODRFLT($O(PSODRFLT(""))))="" Q $$GET1^DIQ(50,$O(PSODRFLT("")),.01)
 I TYPE="I",$O(PSOINFLT($O(PSOINFLT(""))))="" Q $O(PSOINFLT(""))
 I TYPE="R" Q $$GET1^DIQ(52,PSORXFLT,.01)
 Q "MULTIPLE "_$S(TYPE="P":"PATIENTS",TYPE="D":"DRUGS",1:"INSURANCE COMPANIES")
