FHOMTK1 ;Hines OIFO/RTK OUTPATIENT MEALS TRAY TICKETS  ;7/02/03  14:05
 ;;5.5;DIETETICS;;Jan 28, 2005
 ;
START S RM="",FHBY=$E($G(FHBY),1) I FHBY="" Q
 S FHDTE=D1
 S X1=D1,X2=-1 D C^%DTC S FHRMDTE=X
 S X1=D1,X2=1 D C^%DTC S FHDTQ=X
RM ; Recurring Meals
 S FHOMTYP="R"
 F FHOMDT=FHRMDTE:0 S FHOMDT=$O(^FHPT("RM",FHOMDT)) Q:FHOMDT=""!(FHOMDT'<FHDTQ)  D
 .F FHDFN=0:0 S FHDFN=$O(^FHPT("RM",FHOMDT,FHDFN)) Q:FHDFN=""  D
 ..F FHRNUM=0:0 S FHRNUM=$O(^FHPT("RM",FHOMDT,FHDFN,FHRNUM)) Q:FHRNUM=""  D
 ...S FHZN=$G(^FHPT(FHDFN,"OP",FHRNUM,0)),FHLOC=$P(FHZN,U,3) Q:FHLOC=""
 ...S FHSTAT=$P(FHZN,U,15) Q:FHSTAT="C"
 ...S FHMEAL=$P(FHZN,U,4)
 ...I FHOMEAL'="A",FHOMEAL'[FHMEAL Q
 ...I FHBY="P",FHDFN'=FHTTDFN Q
 ...I FHBY="L",W1'=FHLOC Q
 ...I FHBY="C" S FHCOMM=$P($G(^FH(119.6,FHLOC,0)),U,8) I FHP'=FHCOMM Q
 ...I UPD,$P(FHZN,U,16)<$P(FHZN,U,13) Q
 ...I FHOMEAL'="A" D BLD^FHOMTK2 Q
 ...S MEAL=FHMEAL D BLD^FHOMTK2
 ...Q
 ..Q
 .Q
SM ; Special Meals
 S FHOMTYP="S"
 F FHOMDT=FHDTE:0 S FHOMDT=$O(^FHPT("SM",FHOMDT)) Q:FHOMDT=""!(FHOMDT>FHDTQ)  D
 .F FHDFN=0:0 S FHDFN=$O(^FHPT("SM",FHOMDT,FHDFN)) Q:FHDFN=""  D
 ..S FHZN=$G(^FHPT(FHDFN,"SM",FHOMDT,0)),FHLOC=$P(FHZN,U,3) Q:FHLOC=""
 ..S FHMEAL=$P(FHZN,U,9)
 ..I FHOMEAL'="A",FHOMEAL'[FHMEAL Q
 ..I FHBY="P",FHDFN'=FHTTDFN Q
 ..I FHBY="L",W1'=FHLOC Q
 ..I FHBY="C" S FHCOMM=$P($G(^FH(119.6,FHLOC,0)),U,8) I FHP'=FHCOMM Q
 ..I FHOMEAL'="A" D BLD^FHOMTK2 Q
 ..S MEAL=FHMEAL D BLD^FHOMTK2
 ..Q
 .Q
GM ; Guest Meals
 S FHOMTYP="G"
 F FHOMDT=FHDTE:0 S FHOMDT=$O(^FHPT("GM",FHOMDT)) Q:FHOMDT=""!(FHOMDT>FHDTQ)  D
 .F FHDFN=0:0 S FHDFN=$O(^FHPT("GM",FHOMDT,FHDFN)) Q:FHDFN=""  D
 ..S FHZN=$G(^FHPT(FHDFN,"GM",FHOMDT,0)),FHLOC=$P(FHZN,U,5) Q:FHLOC=""
 ..S FHMEAL=$P(FHZN,U,3)
 ..I FHOMEAL'="A",FHOMEAL'[FHMEAL Q
 ..I FHBY="P",FHDFN'=FHTTDFN Q
 ..I FHBY="L",W1'=FHLOC Q
 ..I FHBY="C" S FHCOMM=$P($G(^FH(119.6,FHLOC,0)),U,8) I FHP'=FHCOMM Q
 ..I FHOMEAL'="A" D BLD^FHOMTK2 Q
 ..S MEAL=FHMEAL D BLD^FHOMTK2
 ..Q
 .Q
 I $G(NBR) D PRT^FHMTK1C Q
 K FHTTDFN Q
 ;
