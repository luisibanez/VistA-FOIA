FHOMGP1 ;Hines OIFO/RTK PRINT GUEST MEALS LIST  ;6/30/03  15:45
 ;;5.5;DIETETICS;;Jan 28, 2005
 ;
EN S FHSRTLOC="" D STDATE^FHOMUTL I STDT="" Q
 D ENDATE^FHOMUTL I ENDT="" Q
 ;D NOW^%DTC I STDT>X W !!,"No future dates allowed.",! D EN Q
 K DIR S DIR("A")="Sort by Location or Print All? "
 S DIR(0)="SAO^L:Location;A:All",DIR("B")="A" D ^DIR Q:$D(DIRUT)
 Q:$D(DIRUT)  S FHSORT=Y
 I FHSORT="L" D OUTLOC^FHOMUTL S FHSRTLOC=FHLOC I FHSRTLOC="" Q
 D DEV Q
DEV ;get device and set up queue
 W ! K %ZIS,IOP S %ZIS="Q" D ^%ZIS Q:POP
 I '$D(IO("Q")) U IO D LIST,^%ZISC,END Q
 S ZTRTN="LIST^FHOMGP1"
 S ZTSAVE("STDT")="",ZTSAVE("ENDT")="",ZTSAVE("FHDFN")=""
 S ZTSAVE("FHSORT")="",ZTSAVE("FHSRTLOC")=""
 S ZTDESC="Guest Meals Display" D ^%ZTLOAD
 D ^%ZISC K %ZIS,IOP
 D END Q
LIST ;
 S EX="",NUM=0,ENDT=ENDT_.99 D HDR
 F FHGMDT=STDT:0 S FHGMDT=$O(^FHPT("GM",FHGMDT)) Q:FHGMDT'>0!(FHGMDT>ENDT)!(EX=U)  D
 .F FHDFN=0:0 S FHDFN=$O(^FHPT("GM",FHGMDT,FHDFN)) Q:FHDFN'>0!(EX=U)  D
 ..S NUM=NUM+1,FHNODE=$G(^FHPT(FHDFN,"GM",FHGMDT,0))
 ..S FHCL=$P(FHNODE,U,2),FHML=$P(FHNODE,U,3),FHCH=$P(FHNODE,U,4)
 ..S FHLPT=$P(FHNODE,U,5) I FHLPT="" Q
 ..S FHLOC=$E($P($G(^FH(119.6,FHLPT,0)),U,1),1,10)
 ..I FHSORT="L",FHLPT'=FHSRTLOC Q
 ..S FHCL=$S(FHCL="E":" EMPLOYEE",FHCL="G":"GRATUITOUS",FHCL="O":"   OOD",FHCL="P":"   PAID",1:"VOLUNTEER")
 ..D PATNAME^FHOMUTL W !?2,$E(FHPTNM,1,26)
 ..S FHD=$$FMTE^XLFDT(FHGMDT,"P") W ?30,$E(FHD,1,12)
 ..W ?44,FHLOC,?57,FHML,?62,FHCL,?74,FHCH
 ..I $Y>(IOSL-4) D PG I EX=U Q
 .Q
 I NUM=0 W !!,"NO GUEST MEALS FOR THIS DATE RANGE" Q
 Q
END ;
 K FHGMDT,FHML,FHCL,FHCH
 Q
PG ;
 Q:$O(^FHPT(FHDFN,"GM",FHGMDT))'>0
 I IOST?1"C".E W ! K DIR S DIR(0)="E" D ^DIR I 'Y S EX=U Q
 D HDR Q
HDR ;
 W:$Y @IOF
 W !?5,"G U E S T   M E A L   L I S T"
 W !!?9,"Name",?34,"Date",?45,"Location",?56,"Meal"
 W ?64,"Class",?73,"Charge"
 W !?2,"==========================",?30,"============",?44,"=========="
 W ?56,"====",?62,"=========",?73,"======"
 Q
