FHOMSS1 ;Hines OIFO/RTK SPECIAL MEALS STATUS LIST  ;4/11/03  10:05
 ;;5.5;DIETETICS;;Jan 28, 2005
 ;
 D STDATE^FHOMUTL I STDT="" Q
 D ENDATE^FHOMUTL I ENDT="" Q
 S FHS="ACDP"
 D DEV Q
DEV ;get device and set up queue
 W ! K %ZIS,IOP S %ZIS="Q" D ^%ZIS Q:POP
 I '$D(IO("Q")) U IO D LIST,^%ZISC,END Q
 S ZTSAVE("STDT")="",ZTSAVE("ENDT")=""
 S ZTSAVE("FHDFN")="",ZTSAVE("FHS")=""
 S ZTRTN="LIST^FHOMSS1",ZTDESC="Special Meals Display" D ^%ZTLOAD
 D ^%ZISC K %ZIS,IOP
 D END Q
LIST ;
 K FHLIST S NUM=0,EX=""
 I $G(ENDT)="" S ENDT=9999999
 D HDR S ENDT=ENDT_.99
 F FHSMDT=STDT:0 S FHSMDT=$O(^FHPT("SM",FHSMDT)) Q:FHSMDT'>0!(FHSMDT>ENDT)!(EX=U)  D
 .S FHDFN=$O(^FHPT("SM",FHSMDT,""))
 .S FHNODE=$G(^FHPT(FHDFN,"SM",FHSMDT,0)),FHSTAT=$P(FHNODE,U,2)
 .I FHS'[FHSTAT Q
 .S FHSTAT=$S(FHSTAT="P":"PENDING",FHSTAT="A":"AUTHORIZED",FHSTAT="D":"DENIED",1:"CANCELLED")
 .S NUM=NUM+1,PAD=$S($L(NUM)=1:" ",1:"") W !,PAD,NUM
 .D PATNAME^FHOMUTL W ?5,$E(FHPTNM,1,18)
 .S FHD=$$FMTE^XLFDT(FHSMDT,"P") W ?25,$E(FHD,1,12)
 .S FHLPT=$P(FHNODE,U,3),FHLOC=$E($P($G(^FH(119.6,FHLPT,0)),U,1),1,10)
 .W ?39,FHLOC
 .S FHDPT=$P(FHNODE,U,4),FHDIET=$E($P($G(^FH(111,FHDPT,0)),U,1),1,12)
 .W ?51,FHDIET
 .S FHMEAL=$P(FHNODE,U,9) W ?66,FHMEAL,?70,FHSTAT
 .S FHLIST(NUM)=FHDFN_"^"_FHSMDT
 .I $E(FHSTAT,1)="D" D
 ..S FHDENY=$P(FHNODE,U,6) W !?6,"Denied by: "
 ..I FHDENY'="" W $P($G(^VA(200,FHDENY,0)),U,1)
 .S FHCOMM=$P(FHNODE,U,8) I FHCOMM'="" W !?6,"Comment: ",FHCOMM
 .I $D(^FHPT(FHDFN,"SM",FHSMDT,1)) D
 ..S FHEL=$G(^FHPT(FHDFN,"SM",FHSMDT,1))
 ..W !?6,"Early/Late Tray Time: ",$P(FHEL,U,1)
 ..W "  Bagged Meal: ",$P(FHEL,U,2)
 ..Q
 .I $Y>(IOSL-4) D PG I EX=U Q
 .Q
 I NUM=0 W !!,"NO SPECIAL MEALS FOR THIS DATE RANGE" Q
 Q
PG ;
 Q:$O(^FHPT(FHDFN,"SM",FHSMDT))'>0
 I IOST?1"C".E W ! K DIR S DIR(0)="E" D ^DIR I 'Y S EX=U Q
 D HDR Q
HDR ;
 W:$Y @IOF
 W !?5,"S P E C I A L    M E A L S    S T A T U S    R E P O R T"
 W !!!?1,"#",?8,"Name",?26,"Date/Time",?40,"Location"
 W ?51,"Diet Ordered",?65,"Meal",?71,"Status"
 W !,"===",?5,"==================",?25,"============",?39,"=========="
 W ?51,"============",?65,"====",?70,"========="
 Q
END ;
 K EX,FHNODE,FHSTAT Q
