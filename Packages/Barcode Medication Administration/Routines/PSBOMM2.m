PSBOMM2	;BIRMINGHAM/EFC-MISSED MEDS ;Mar 2004
	;;3.0;BAR CODE MED ADMIN;**26**;Mar 2004
	;
	;
MISSED(PSBADMN,PSBEDIT)	;
	;
	N PSBMISD,PSBAUDT
	F Y=1:1:$L(PSBADMN,"-") S PSBDT=+("."_$P(PSBADMN,"-",Y))+(PSBSTRT\1) D
	.S PSBMISD=$$CHECK(PSBDT)
	.;CHECK AUDITED ADMIN TIMES FOR MISSED MED
	.I PSBMISD F I=1:1:$P(PSBOACTL(0),U,4) I $P($G(PSBOACTL(I,1)),U,3)["ADMIN TIMES" D  Q:'PSBMISD
	..Q:$P(PSBOACTL(I,1),U)<PSBSTRT
	..Q:$P(PSBOACTL(I,1),U)>PSBSTOP
	..S PSBAUDT=+("."_$P(PSBOACTL(I,2),"-",Y))+(PSBSTRT\1)
	..S PSBMISD=$$CHECK(PSBAUDT),PSBEDIT=1
	.I PSBMISD D
	..S ^TMP("PSB",$J,DFN,PSBDT,PSBOITX,PSBONX)=""
	..D UDCONT
	Q
	;
CHECK(PSBDT)	;
	I PSBDT<PSBOST Q 0 ; Order Start Date
	I PSBDT'<PSBOSP Q 0 ; Order Stop Date
	I PSBDT<PSBSTRT Q 0 ; Report Window
	I PSBDT>PSBSTOP Q 0  ; Report Window
	I $D(^PSB(53.79,"AORD",DFN,PSBONX,PSBDT)) D  Q:PSBSTUS'="N"&(PSBSTUS'="M") 0
	.S PSBINDX=$O(^PSB(53.79,"AORD",DFN,PSBONX,PSBDT,"")),PSBSTUS=$P(^PSB(53.79,PSBINDX,0),U,9)
	Q 1
UDCONT	;
	S PSBFLAG=0,J=1
	K ^TMP("PSB1",$J)
	F I=1:1:$P(PSBOACTL(0),U,4) D
	. I $P($G(PSBOACTL(I,1)),U,4)["ON HOLD"!($P($G(PSBOACTL(I,1)),U,4)="HOLD") S ^TMP("PSB1",$J,DFN,J)="HOLD"_U_$E($P($G(PSBOACTL(I,1)),U,1),1,12)
	. I $P($G(PSBOACTL(I,1)),U,4)["TAKEN OFF HOLD"!($P($G(PSBOACTL(I,1)),U,4)["UNHOLD") S $P(^TMP("PSB1",$J,DFN,J),U,3)="OFF HOLD"_U_$E($P($G(PSBOACTL(I,1)),U,1),1,12),J=J+1
	D:$D(^TMP("PSB1",$J,DFN))&($P(PSBOACTL(0),U,4)'=1)
	.S J=0 F  S J=$O(^TMP("PSB1",$J,DFN,J)) Q:'J  Q:PSBFLAG  D
	..S PSBHDDT=$P(^TMP("PSB1",$J,DFN,J),U,2)
	..S PSBHDST=$P(^TMP("PSB1",$J,DFN,J),U)
	..S PSBOFDT=$P(^TMP("PSB1",$J,DFN,J),U,4)
	..S PSBOFST=$P(^TMP("PSB1",$J,DFN,J),U,3)
	..I PSBDT>PSBHDDT,PSBHDST="HOLD",PSBOFST'="" I PSBDT<PSBOFDT,PSBOFST="OFF HOLD" S PSBFLAG=2,PSBUNHD=PSBOFDT
	..I PSBDT>PSBHDDT,PSBHDST="HOLD",PSBOFST="" S PSBFLAG=1
	K PSBCNT,TMP("PSB1",$J)
	I PSBFLAG=1 S ^TMP("PSB",$J,DFN,PSBDT,PSBOITX,PSBONX)="(On Hold) "_$$FMTE^XLFDT(PSBHDDT)
	I PSBFLAG=2 S ^TMP("PSB",$J,DFN,PSBDT,PSBOITX,PSBONX)="(On Hold) "_$$FMTE^XLFDT(PSBHDDT)_"  "_"(Off Hold) "_$$FMTE^XLFDT(PSBUNHD) Q
	Q
	;
UDONE	;
	S PSBFLAG=0,J=1
	F I=1:1:$P(PSBOACTL(0),U,4) D
	.I $P($G(PSBOACTL(I,1)),U,4)["ON HOLD"!($P($G(PSBOACTL(I,1)),U,4)="HOLD") S ^TMP("PSB1",$J,DFN,J)="HOLD"_U_$E($P($G(PSBOACTL(I,1)),U,1),1,12)
	.I $P($G(PSBOACTL(I,1)),U,4)["TAKEN OFF HOLD"!($P($G(PSBOACTL(I,1)),U,4)["UNHOLD") S $P(^TMP("PSB1",$J,DFN,J),U,3)="OFF HOLD"_U_$E($P($G(PSBOACTL(I,1)),U,1),1,12),J=J+1
	D:$D(^TMP("PSB1",$J,DFN))&($P(PSBOACTL(0),U,4)'=1)
	.S J="" F  S J=$O(^TMP("PSB1",$J,DFN,J)) Q:'J  Q:PSBFLAG  D
	..S PSBHDDT=$P(^TMP("PSB1",$J,DFN,J),U,2)
	..S PSBHDST=$P(^TMP("PSB1",$J,DFN,J),U)
	..S PSBOFDT=$P(^TMP("PSB1",$J,DFN,J),U,4)
	..S PSBOFST=$P(^TMP("PSB1",$J,DFN,J),U,3)
	..I PSBOSTS="A",PSBHDST="HOLD",PSBOFST'="",'$D(^TMP("PSB1",$J,DFN,J+1)) I PSBSTOP>PSBOFDT,PSBOFST="OFF HOLD" S PSBFLAG=2,PSBUNHD=PSBOFDT
	..I PSBOSTS="A",PSBHDST="HOLD",PSBOFST'="",PSBOFDT'<PSBSTOP S PSBFLAG=1
	..I PSBOSTS="H",PSBHDST="HOLD",'$D(^TMP("PSB1",$J,DFN,J+1)) S PSBFLAG=1
	K PSBCNT,^TMP("PSB1",$J)
	Q
	;
