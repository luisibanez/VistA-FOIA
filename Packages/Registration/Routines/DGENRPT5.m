DGENRPT5 ;ALB/DW,LBD - EGT Impact Report Utility; 02/17/2004
 ;;5.3;Registration;**568**;Aug 13,1993
 ;
 ;
 Q
GETAPPT(TYPE) ; Set up array of Patient IENs for SD API to process
 N VETARRAY,PIEN,PNAME,RCNT,ACNT,DGARRAY,SDCNT,I
 S ACNT=1,RCNT=0
 S PNAME="" F  S PNAME=$O(^TMP($J,TYPE,PNAME)) Q:PNAME=""  D
 .S PIEN=0 F  S PIEN=$O(^TMP($J,TYPE,PNAME,PIEN)) Q:'PIEN  D
 ..S RCNT=RCNT+1,VETARRAY(ACNT)=$G(VETARRAY(ACNT))_PIEN_";"
 ..; Group DFNs by no more than twenty records
 ..I RCNT>19 S ACNT=ACNT+1,RCNT=0
 ;
 ; Call SD API by array of Patient DFNs
 F I=1:1 Q:'$D(VETARRAY(I))  D
 .S DGARRAY("FLDS")="1;2;3;10",DGARRAY(4)=VETARRAY(I)
 .S SDCNT=$$SDAPI^SDAMA301(.DGARRAY)
 .M ^TMP($J,"SDAMA")=^TMP($J,"SDAMA301")
 .K DGARRAY
 Q
 ;
BLDUTL(DFN) ; Build Utility Global Entries for records processed
 Q:'$D(^TMP($J,"SDAMA301",DFN))
 N CLIEN,APPTDT,NODE,APPTNUM S APPTNUM=1
 S CLIEN=0  F  S CLIEN=$O(^TMP($J,"SDAMA301",DFN,CLIEN)) Q:'CLIEN  D
 .S APPTDT=0 F  S APPTDT=$O(^TMP($J,"SDAMA301",DFN,CLIEN,APPTDT)) Q:'APPTDT  D
 ..Q:APPTDT'>DT
 ..S NODE=^TMP($J,"SDAMA301",DFN,CLIEN,APPTDT)
 ..S ^UTILITY("VASD",$J,APPTNUM,"E")=$$FMTE^DILIBF($P(NODE,U),"5U")_U_$P($P(NODE,U,2),";",2)_U_U_$P($P(NODE,U,10),";",2)
 ..S ^UTILITY("VASD",$J,APPTNUM,"I")=NODE,APPTNUM=APPTNUM+1
 K ^TMP($J,"SDAMA301")
 Q
