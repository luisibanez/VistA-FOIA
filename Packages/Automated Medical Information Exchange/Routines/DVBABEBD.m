DVBABEBD ;ALB - CAPRI EXAM BY DATE REPORT;09/13/04
 ;;2.7;AMIE;**57**;Apr 10, 1995
 Q
 ;
EXAMBYDT(MSG,BEGDT,ENDDT) ; CALLED BY REMOTE PROCEDURE DVBAB EXAMS BY DATE
 N DVBABCNT,DVBABIEN,DVBABPAT,DVBABT,DVBAB0,DVBABT1,DVBABT2,DVBABT3,DVBABT4,DVBABT5,DVBABT6
 K ^TMP("DVBABEBD",DUZ)
 D SETSTAT
 S ^TMP("DVBABEBD",DUZ,1)="PATIENT NAME^REQUEST DATE^DATE RELEASED^DATE PRINTED BY RO^REQUEST STATUS"_$C(13)
 S DVBABCNT=2,DVBABIEN=0,MSG=$NA(^TMP("DVBABEBD",DUZ))
 ;
 F  S DVBABIEN=$O(^DVB(396.3,DVBABIEN)) Q:'DVBABIEN  D
 .;REQUEST DATE
 .S DVBAB0=^DVB(396.3,DVBABIEN,0),DVBABT2=$P(DVBAB0,"^",2)
 .I $P(DVBABT2,".",1)<(ENDDT+1) I $P(DVBABT2,".",1)>(BEGDT-1) D
 ..S DVBABPAT=^DPT($P(DVBAB0,"^",1),0)
 ..;PATIENT NAME
 ..S DVBABT1=$P(DVBABPAT,"^",1)
 ..;SOCIAL SECURITY NUMBER
 ..S DVBABT6=$P(DVBABPAT,"^",9)
 ..;DATE RELEASED
 ..S DVBABT3=$P(DVBAB0,"^",14)
 ..;DATE PRINTED BY RO
 ..S DVBABT4=$P(DVBAB0,"^",16)
 ..;REQUEST STATUS
 ..S DVBABT5=$P(DVBAB0,"^",18)
 ..S:$D(DVBABT(DVBABT5)) DVBABT5=DVBABT(DVBABT5)
 ..S ^TMP("DVBABEBD",DUZ,DVBABCNT)=DVBABT1_U_DVBABT2_U_DVBABT3_U_DVBABT4_U_DVBABT5_U_DVBABT6_$C(13)
 ..S DVBABCNT=DVBABCNT+1
 Q
 ;
SETSTAT S DVBABT("N")="NEW"
 S DVBABT("P")="PENDING, REPORTED"
 S DVBABT("S")="PENDING, SCHEDULED"
 S DVBABT("R")="RELEASED TO RO, NOT PRINTED"
 S DVBABT("C")="COMPLETED, PRINTED BY RO"
 S DVBABT("X")="CANCELLED BY MAS"
 S DVBABT("RX")="CANCELLED BY RO"
 S DVBABT("T")="TRANSCRIBED"
 S DVBABT("NT")="NEW, TRANSFERRED IN"
 S DVBABT("CT")="COMPLETE, TRANSFERRED OUT"
 Q
