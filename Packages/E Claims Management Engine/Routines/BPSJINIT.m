BPSJINIT ;BHAM ISC/LJF - HL7 Application Registration ;21-NOV-2003
 ;;1.0;E CLAIMS MGMT ENGINE;**1**;JUN 2004
 ;;Per VHA Directive 10-93-142, this routine should not be modified.
 ;
 N DA,DIC,DIE,DINUM,DIR,DIRUT,DIROUT,DLAYGO,DR,DTOUT,DUOUT,X,Y
 N BPVALFN,BPSJAPPR,BPSJVALR,PHIX,DT,DUZ
 ;
 ;  This program will allow user to enter site data.
 ;
 ; Programmer Note: D BPSJVAL^BPSJAREG(X) will validate with following.
 ;   where X is: 0 = HL7 trigger, no validation display
 ;               1 = HL7 trigger, display validation
 ;               2 = no HL7 trigger, display validation
 ;               3 = no validation display, no HL7 trigger
 ;
 D DT^DICRW S DUZ(0)="@",DT=$$NOW^XLFDT
 D HOME^%ZIS,CLEAR^VALM1
 W !!!,"ENTER/VERIFY SITE REGISTRATION DATA.",!!
 ;
 S BPVALFN=9002313.99
 ; Set Version number to 2 if not set or not an integer > 1
 S DA=$O(^BPS(BPVALFN,0)),DIE=$$ROOT^DILFD(BPVALFN)
 I 'DA S DA=1 D
 . S DR=".01////BPS SETUP #1" D ^DIE
 . S DR="6003////2" D ^DIE
 ;
 K DIC,DIE,DINUM,DIR,DIRUT,DIROUT,DLAYGO,DR,DTOUT,DUOUT,X,Y
 S DR=$P($G(^BPS(BPVALFN,DA,"VITRIA")),U,3)
 I DR>1,DR=+DR
 E  S DIE=$$ROOT^DILFD(BPVALFN),DR="6003////2" D ^DIE
 ;
 W !!,"PRIMARY SITE CONTACT DATA."
 K DIC,DIE,DINUM,DIR,DIRUT,DIROUT,DLAYGO,DR,DTOUT,DUOUT,X,Y
 S DIE=$$ROOT^DILFD(BPVALFN)
 S DR="[BPSJ CONTACT ENTER/EDIT]" D ^DIE
 ;
 W !!,"ALTERNATE SITE CONTACT DATA."
 K DIC,DIE,DINUM,DIR,DIRUT,DIROUT,DLAYGO,DR,DTOUT,DUOUT,X,Y
 S DIE=$$ROOT^DILFD(BPVALFN)
 S DR="[BPSJ ALT CONTACT ENTER/EDIT]" D ^DIE
 ;
 D HOME^%ZIS,CLEAR^VALM1
 W !!!,"-- APPLICATION REGISTRATION VALIDATION RESULTS. --",!!
 S BPSJVALR=-1
 D BPSJVAL^BPSJAREG(2)
 S BPSJAPPR=BPSJVALR
 ;
 I 'BPSJAPPR W !!,"-- APPLICATION REGISTRATION DATA VALID. --",!
 E  D
 . W !!,"** APPLICATION REGISTRATION DATA INVALID!!!  **"
 . W !,"**  APPLICATION REGISTRATION AND PHARMACY    **"
 . W !,"**    REGISTRATIONS WILL NOT BE SENT!        **",!
 ;
 K DA,DIC,DIE,DINUM,DIR,DIRUT,DIROUT,DLAYGO,DR,DTOUT,DUOUT,X,Y
 S DIR(0)="EO" D ^DIR I X=U Q
 ;
 D PHARM
 I BPSJAPPR D  Q
 . W !!,"REGISTRATION ABORTED DUE TO INVALID SITE REGISTRATION DATA.",!!
 ;
 D HOME^%ZIS,CLEAR^VALM1
 W !!!,"APPLICATION REGISTRATION DATA IS VALID."
 W !!,"PHARMACY REGISTRATION DATA IS:"
 S PHIX=$O(^BPS(9002313.56,0))
 F  Q:'PHIX  D  S PHIX=$O(^BPS(9002313.56,PHIX))
 . S BPSJVALR=-1 D REG^BPSJPREG(PHIX,3)
 . I BPSJVALR>0 S DIR=" *INVALID",DIE=" and will NOT be transmitted."
 . E  S DIR="    VALID",DIE=" and will be transmitted."
 . W !,DIR_" for "_$P($G(^BPS(9002313.56,PHIX,0)),U)_DIE
 W !
 K DA,DIC,DIE,DINUM,DIR,DIRUT,DIROUT,DLAYGO,DR,DTOUT,DUOUT,X,Y
 S DIR(0)="YEO",DIR("A")="SEND APPLICATION REGISTRATION: Y/N " D ^DIR
 I $TR($E(X),"y","Y")'="Y" Q
 ;
 K DA,DIC,DIE,DINUM,DIR,DIRUT,DIROUT,DLAYGO,DR,DTOUT,DUOUT,X,Y
 D BPSJVAL^BPSJAREG(0)
 W !!,"APPLICATION REGISTRATION SUBMITTED."
 Q
 ;
PHARM ;CYCLE THROUGH PHARMACIES
 ;
 N DA,DIC,DIE,DINUM,DIR,DIRUT,DIROUT,DLAYGO,DR,DTOUT,DUOUT,X,Y
 N BPVALFN,BPSJVALR,DT,DUZ,BPSJPHPR
 ;
 D DT^DICRW S DUZ(0)="@",DT=$$NOW^XLFDT
 ;
 S BPVALFN=9002313.56,PHIX=0
 ;
 F  D  Q:PHIX=""
 . D HOME^%ZIS,CLEAR^VALM1
 . W !!!,"ENTER/VERIFY PHARMACY REGISTRATION DATA."
 . W !!,"PHARMACY SPECIFIC DATA."
 . K DA,DIC,DIE,DINUM,DIR,DIRUT,DIROUT,DLAYGO,DR,DTOUT,DUOUT,X,Y
 . S DIC(0)="QAELM",DIC=BPVALFN,DLAYGO=DIC D ^DIC
 . ;
 . I X'=U,0<+Y S PHIX=+Y
 . E  S PHIX="" Q
 . D MOD I 'PHIX Q
 . D HOME^%ZIS,CLEAR^VALM1
 . W !!!,"-- PHARMACY REGISTRATION VALIDATION RESULTS. --",!
 . ;
 . S BPSJVALR=-1
 . D REG^BPSJPREG(PHIX,2)
 . S BPSJPHPR=BPSJVALR
 . ;
 . I 'BPSJPHPR W !!,"-- PHARMACY REGISTRATION DATA VALID. --",!
 . E  D
 .. W !!,"**     PHARMACY REGISTRATION DATA INVALID!!!      **"
 .. W !,"** THIS PHARMACY'S REGISTRATION WILL NOT BE SENT! **",!
 . ;
 . K DA,DIC,DIE,DINUM,DIR,DIRUT,DIROUT,DLAYGO,DR,DTOUT,DUOUT,X,Y
 . S DIR(0)="EO",DIR("A")="Enter RETURN to continue" D ^DIR
 ;
 Q
 ;
MOD ;
 N DA,DIC,DIE,DINUM,DIR,DIRUT,DIROUT,DLAYGO,DR,DTOUT,DUOUT,X,Y
 ;
 ; Set hours to default if not set.
 S DA=$$OPHOURS^BPSJZRP(PHIX),DR=$G(^BPS(9002313.56,PHIX,"HOURS"))
 I $P(DR,U,2,5)'=DA S ^BPS(9002313.56,PHIX,"HOURS")="24"_U_DA
 ;
 W !!,"SITE DATA."
 K DA,DIC,DIE,DINUM,DIR,DIRUT,DIROUT,DLAYGO,DR,DTOUT,DUOUT,X,Y
 S DA=PHIX,DIE=$$ROOT^DILFD(BPVALFN)
 S DR="[BPSJ PHARMACY SITE ENTER/EDIT]" D ^DIE
 ;
 I '$G(DA) S PHIX=0 Q   ; Pharmacy killed by user
 ;
 ; Pharmacy open hours
 I '$D(Y) D EN^BPSJINI1(PHIX)
 ;
 K DA,DIC,DIE,DINUM,DIR,DIRUT,DIROUT,DLAYGO,DR,DTOUT,DUOUT,X,Y
 S DIR(0)="EO" D ^DIR
 ;
 I X=U Q
 ;
 W !!,"PRIMARY CONTACT DATA."
 K DA,DIC,DIE,DINUM,DIR,DIRUT,DIROUT,DLAYGO,DR,DTOUT,DUOUT,X,Y
 S DA=PHIX,DIE=$$ROOT^DILFD(BPVALFN)
 S DR="[BPSJ PHARM CONTACT ENTER/EDIT]" D ^DIE
 ;
 K DA,DIC,DIE,DINUM,DIR,DIRUT,DIROUT,DLAYGO,DR,DTOUT,DUOUT,X,Y
 S DIR(0)="EO" D ^DIR
 ;
 I X=U Q
 ;
 W !!,"ALTERNATE CONTACT DATA."
 K DA,DIC,DIE,DINUM,DIR,DIRUT,DIROUT,DLAYGO,DR,DTOUT,DUOUT,X,Y
 S DA=PHIX,DIE=$$ROOT^DILFD(BPVALFN)
 S DR="[BPSJ PHARM ALT CONT ENTER/EDIT]" D ^DIE
 ;
 K DA,DIC,DIE,DINUM,DIR,DIRUT,DIROUT,DLAYGO,DR,DTOUT,DUOUT,X,Y
 S DIR(0)="EO" D ^DIR
 ;
 I X=U Q
 ;
 W !!,"PHARMACIST DATA."  ; VA LEAD PHARMACIST
 K DA,DIC,DIE,DINUM,DIR,DIRUT,DIROUT,DLAYGO,DR,DTOUT,DUOUT,X,Y
 S DA=PHIX,DIE=$$ROOT^DILFD(BPVALFN)
 S DR="[BPSJ PHARMACIST ENTER/EDIT]" D ^DIE
 ;
 I $D(Y) Q
 ;
 ; VA LEAD PHARMACIST LICENSE
 K DA,DIC,DIE,DINUM,DIR,DIRUT,DIROUT,DLAYGO,DR,DTOUT,DUOUT,X,Y
 S DA=PHIX,DIE=$$ROOT^DILFD(BPVALFN)
 S DR="1900.04//" D ^DIE
 ;
 Q
 ;
VALIDATE ;  this will only validate the Application Registration and
 ;          the Pharmacy registrations
 Q
 N AREG
 ;
 D BPSJVAL^BPSJAREG(2)
 ;
 S DIR(0)="EO"
 D ^DIR
 I X=U Q
 ;
 S AREG="" F  S AREG=$O(^BPS(9002313.56,AREG)) Q:'AREG  D  I X=U Q
 . D REG^BPSJPREG(AREG,2)
 . S DIR(0)="EO"
 . D ^DIR
 ;
 Q
