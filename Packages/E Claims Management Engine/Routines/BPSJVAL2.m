BPSJVAL2 ;BHAM ISC/LJF - Validate Pharmacy data ;2004-03-01
 ;;1.0;E CLAIMS MGMT ENGINE;**1**;JUN 2004
 ;;Per VHA Directive 10-93-142, this routine should not be modified.
 ;
 N PHARMIX,RET,DIR,X
 ;
 S PHARMIX=0,X=""
 F  S PHARMIX=$O(^BPS(9002313.56,PHARMIX)) Q:'PHARMIX  D  Q:X=U
 . D DT^DICRW,HOME^%ZIS,CLEAR^VALM1
 . W !!,"VERIFY PHARMACY REGISTRATIONS DATA.",!
 . D REG^BPSJPREG(PHARMIX,2)
 . W !
 . S DIR(0)="EO" D ^DIR
 ;
 Q
 ;
VALIDATE ;
 N SEG,SEGIX,ZRP,RAY,RIX,PIX,PIXL,SEGDAT,ZNOTE,ZMAX,CPS,FS,REP
 N VALDATA
 S ZMAX=16
 ;
 S RETCODE=$G(RETCODE)
 S ZRP="",RIX=0
 ;
 ; Set HL7 Delimiters - use standard defaults if none provided
 S FS=$G(HL("FS")) I FS="" S FS="|"
 S CPS=$E($G(HL("ECH"))) I CPS="" S CPS="^"
 S REP=$E($G(HL("ECH")),2) I REP="" S REP="~"
 ;
 F SEGIX=3:1 S SEG=$G(^TMP("HLS",$J,SEGIX)),PIX=0 Q:SEG=""  D  I ZRP]"" Q
 . I $E(SEG,1,3)="ZRP" S ZRP=$E(SEG,4) S $E(SEG,1,4)=""
 I ZRP="" Q
 F  S RIX=$O(^TMP("HLS",$J,SEGIX,RIX)) Q:'RIX  I RIX<(ZMAX+1) D
 . S RETCODE(RIX)=$P($G(^TMP("HLS",$J,SEGIX,RIX)),ZRP) D @RIX
 . I +$G(VERBOSE),$L($G(RETCODE(RIX))) W !,RETCODE(RIX) Q
 ;
 Q
 ;
 ; NS=Not Supported, R=Required, RE=Required or empty, C=Conditional
 ; CE=Conditional or empty, O=Optional,
 ;
1 ; Set ID - NS
 Q
2 ; NABP Number - R
 S ZNOTE="   NABP NUMBER - Required - VALID"
 I RETCODE(RIX)="" S ZNOTE="** NABP NUMBER - Required - INVALID",RETCODE=2
 I RETCODE(RIX)]"" S RETCODE(RIX)=": "_RETCODE(RIX)
 S RETCODE(RIX)=ZNOTE_RETCODE(RIX)
 Q
3 ; PHARMACY NAME - R
 S ZNOTE="   PHARMACY NAME - Required - VALID"
 I RETCODE(RIX)="" S ZNOTE="** PHARMACY NAME - Required - INVALID",RETCODE=3
 I RETCODE(RIX)]"" S RETCODE(RIX)=": "_RETCODE(RIX)
 S RETCODE(RIX)=ZNOTE_RETCODE(RIX)
 Q
4 ; DEA Number - R
 S ZNOTE="   DEA NUMBER - Required - VALID"
 I RETCODE(RIX)="" S ZNOTE="** DEA NUMBER - Required - INVALID",RETCODE=4
 I RETCODE(RIX)]"" S RETCODE(RIX)=": "_RETCODE(RIX)
 S RETCODE(RIX)=ZNOTE_RETCODE(RIX)
 Q
5 ; Hour of Operation
 S ZNOTE="   HOURS OF OPERATION - VALID"
 I RETCODE(RIX)]"" S RETCODE(RIX)=": "_RETCODE(RIX)
 S RETCODE(RIX)=ZNOTE_RETCODE(RIX)
 Q
6 ; Mailing Address - R
 S ZNOTE=$$TRIMTAIL(RETCODE(RIX))
 S VALDATA=($L($P(ZNOTE,CPS,1))<1)          ; Street address
 S VALDATA=($L($P(ZNOTE,CPS,3))<1)+VALDATA  ; City
 S VALDATA=($L($P(ZNOTE,CPS,4))<1)+VALDATA  ; State
 S VALDATA=($L($P(ZNOTE,CPS,5))<1)+VALDATA  ; Zip
 S ZNOTE="   MAILING ADDRESS - Required - VALID"
 I VALDATA S ZNOTE="** MAILING ADDRESS - Required - INVALID",RETCODE=6
 I RETCODE(RIX)]"" S RETCODE(RIX)=": "_RETCODE(RIX)
 S RETCODE(RIX)=ZNOTE_RETCODE(RIX)
 Q
7 ; Remittance Address - R
 S ZNOTE=$$TRIMTAIL(RETCODE(RIX))
 S VALDATA=($L($P(ZNOTE,CPS,1))<1)          ; Street Address
 S VALDATA=($L($P(ZNOTE,CPS,3))<1)+VALDATA  ; City
 S VALDATA=($L($P(ZNOTE,CPS,4))<1)+VALDATA  ; State
 S VALDATA=($L($P(ZNOTE,CPS,5))<1)+VALDATA  ; Zip
 S ZNOTE="   REMITTANCE ADDRESS - Required - VALID"
 I VALDATA S ZNOTE="** REMITTANCE ADDRESS - Required - INVALID",RETCODE=7
 I RETCODE(RIX)]"" S RETCODE(RIX)=": "_RETCODE(RIX)
 S RETCODE(RIX)=ZNOTE_RETCODE(RIX)
 Q
8 ; Contact Name
 S ZNOTE=$$TRIMTAIL(RETCODE(RIX))
 S VALDATA=($L($P(ZNOTE,CPS,1))<1)  ; Surname
 S ZNOTE="   CONTACT NAME - Required - VALID"
 I VALDATA S ZNOTE="** CONTACT NAME - Required - INVALID",RETCODE=8
 I RETCODE(RIX)]"" S RETCODE(RIX)=": "_RETCODE(RIX)
 S RETCODE(RIX)=ZNOTE_RETCODE(RIX)
 Q
9 ; Contact Title
 S ZNOTE="   CONTACT TITLE - VALID"
 I RETCODE(RIX)]"" S RETCODE(RIX)=": "_RETCODE(RIX)
 S RETCODE(RIX)=ZNOTE_RETCODE(RIX)
 Q
10 ; Contact means
 S ZNOTE="   CONTACT MEANS - VALID"
 I RETCODE(RIX)]"" S RETCODE(RIX)=": "_RETCODE(RIX)
 S RETCODE(RIX)=ZNOTE_RETCODE(RIX)
 Q
11 ; Alternate Contact Name
 S ZNOTE=$$TRIMTAIL(RETCODE(RIX))
 S VALDATA=($L($P(ZNOTE,CPS,1))<1)  ; Surname
 S ZNOTE="   ALTERNATE CONTACT NAME - Required - VALID"
 I VALDATA S ZNOTE="** ALTERNATE CONTACT NAME - Required - INVALID",RETCODE=11
 I RETCODE(RIX)]"" S RETCODE(RIX)=": "_RETCODE(RIX)
 S RETCODE(RIX)=ZNOTE_RETCODE(RIX)
 Q
12 ; Alternate Contact Title
 S ZNOTE="   ALTERNATE CONTACT TITLE - VALID"
 I RETCODE(RIX)]"" S RETCODE(RIX)=": "_RETCODE(RIX)
 S RETCODE(RIX)=ZNOTE_RETCODE(RIX)
 Q
13 ; Alternate Contact means
 S ZNOTE="   ALTERNATE CONTACT MEANS - VALID"
 I RETCODE(RIX)]"" S RETCODE(RIX)=": "_RETCODE(RIX)
 S RETCODE(RIX)=ZNOTE_RETCODE(RIX)
 Q
14 ; Lead Pharmacist Name - R
 S ZNOTE=$$TRIMTAIL(RETCODE(RIX))
 S VALDATA=($L($P(ZNOTE,CPS,1))<1)  ; Surname
 S ZNOTE="   LEAD PHARMACIST NAME - Required - VALID"
 I VALDATA S ZNOTE="** LEAD PHARMACIST NAME - Required - INVALID",RETCODE=14
 I RETCODE(RIX)]"" S RETCODE(RIX)=": "_RETCODE(RIX)
 S RETCODE(RIX)=ZNOTE_RETCODE(RIX)
 Q
15 ; Lead Pharmacist Title
 S ZNOTE="   LEAD PHARMACIST TITLE - VALID"
 I RETCODE(RIX)]"" S RETCODE(RIX)=": "_RETCODE(RIX)
 S RETCODE(RIX)=ZNOTE_RETCODE(RIX)
 Q
16 ; Lead Pharmacist License Number
 S ZNOTE="   LEAD PHARMACIST LICENSE NUMBER - VALID"
 I RETCODE(RIX)]"" S RETCODE(RIX)=": "_RETCODE(RIX)
 S RETCODE(RIX)=ZNOTE_RETCODE(RIX)
 Q
 ;
TRIMTAIL(INSTR) ;
 N OUTSTR,CHR
 ;
 I $G(INSTR)="" Q ""   ; quit if nothing there
 ;
 S INSTR=$RE(INSTR)
 S CHR=$E($TR(INSTR,CPS_REP))
 I CHR]"" Q $RE($P(INSTR,CHR,2,200))_CHR
 Q ""
