BPSNCPD2 ;BHAM ISC/LJE - Continuation of BPSNCPDP CLAIMS SUBMISSION ANALYSIS ;08/01/03
 ;;1.0;E CLAIMS MGMT ENGINE;**1**;JUN 2004
 ;External reference $$RX^IBNCPDP supported by DBIA 4299
 ;
 ; EN - Call IB Billing Determination.  If good to go, update MOREDATA array
 ; Notes about variables
 ;   ARRAY    - Created by STARRAY^BPSNCPD1 and used for IB Determination 
 ;   MOREDATA - Initialized by BPSNCPDP and more data is added here
 ;   IB       - Returned to BPSNCPDP
 ;   All three of these variables are newed in BPSNCPDP
EN ;
 I ^BPS(9002313.99,1,"CERTIFIER")'=DUZ D  I IB=2 Q
 . ;For NCPDP IB call to see if we need to 3rd Party Bill and if so, get insurance/payer sheet info
 . S MOREDATA("BILL")=$$RX^IBNCPDP(DFN,.ARRAY)  ;IB CALL
 . Q:'$D(MOREDATA("BILL"))
 . ; If calling program is the ECME user screen and we can't bill because of NEEDS SC DETERMINATION,
 . ;   then prompt the user to see if they want to bill 
 . I BWHERE="ERES",$P(MOREDATA("BILL"),"^",1)=0,$P(MOREDATA("BILL"),"^",2)="NEEDS SC DETERMINATION" D
 .. N DIR,X,Y,DTOUT,DUOUT,DIRUT,DIROUT,I
 .. W !!,"The prescription is potentially SC-related and needs SC determination."
 .. W !,"Prescriptions related to SC cannot be billed to Third Party Insurance.",!
 .. S DIR(0)="Y",DIR("A")="Are you sure you want to bill this prescription"
 .. S DIR("B")="NO"
 .. S DIR("?")="If you want to bill this prescription, enter 'Yes' - otherwise, enter 'No'"
 .. W ! D ^DIR K DIR
 .. I '+Y Q
 .. F I="AO","SC","MST","EC","IR","HNC","CV" I $D(ARRAY(I))=1 S ARRAY(I)=+$G(ARRAY(I))
 .. S MOREDATA("BILL")=$$RX^IBNCPDP(DFN,.ARRAY)  ;Call IB again
 . Q:'$D(MOREDATA("BILL"))
 . I $P(MOREDATA("BILL"),"^",1)=0 S IB=2 Q  ;ib says not to bill
 . S IB=1
 . M MOREDATA("IBDATA")=ARRAY("INS")
 . N IBSEQ
 . S IBSEQ=""
 . F  S IBSEQ=$O(MOREDATA("IBDATA",IBSEQ)) Q:IBSEQ=""  D
 . . S $P(MOREDATA("IBDATA",IBSEQ,2),"^",7)=ARRAY("QTY")
 . . S $P(MOREDATA("IBDATA",IBSEQ,2),"^",8)=ARRAY("COST")
 . . S $P(MOREDATA("IBDATA",IBSEQ,2),"^",9)=ARRAY("NDC")
 . . S $P(MOREDATA("IBDATA",IBSEQ,2),"^",10)=BFILL
 ;
 ; -- The following is for certification testing only
 I ^BPS(9002313.99,1,"CERTIFIER")=DUZ,'IB D
 . N NODE,FLD,NFLD
 . S MOREDATA("BILL")=1
 . S MOREDATA("IBDATA",1,1)="",MOREDATA("IBDATA",1,2)=""
 . S $P(MOREDATA("IBDATA",1,1),"^",1)=1  ;PLAN IEN
 . S $P(MOREDATA("IBDATA",1,1),"^",4)=PAYERSH  ;PAYER SHEET
 . S $P(MOREDATA("IBDATA",1,1),"^",10)="01"  ;HOME STATE PLAN
 . S $P(MOREDATA("IBDATA",1,2),"^",5)=0  ;ADMIN FEE
 . S $P(MOREDATA("IBDATA",1,1),"^",14)=CERTIFY
 . S $P(MOREDATA("IBDATA",1,1),"^",15)=CERTIEN
 . K CERTARY D GETS^DIQ(9002313.31,CERTIEN_",","1*","","CERTARY")
 . S (NODE,FLD)="" F  S NODE=$O(CERTARY(9002313.311,NODE)) Q:NODE=""  F  S FLD=$O(CERTARY(9002313.311,NODE,FLD)) Q:FLD=""  D
 . . I FLD=.01 S NFLD="",NFLD=CERTARY(9002313.311,NODE,FLD) D
 . . . I NFLD=101 S $P(MOREDATA("IBDATA",1,1),"^",2)=CERTARY(9002313.311,NODE,.02) ;BIN
 . . . I NFLD=104 S $P(MOREDATA("IBDATA",1,1),"^",3)=CERTARY(9002313.311,NODE,.02)  ;PCN
 . . . I NFLD=110 S $P(MOREDATA("IBDATA",1,2),"^",6)=CERTARY(9002313.311,NODE,.02)  ;CERT ID
 . K CERTARY D GETS^DIQ(9002313.31,CERTIEN_",","2*","","CERTARY")
 . S (NODE,FLD)="" F  S NODE=$O(CERTARY(9002313.3121,NODE)) Q:NODE=""  F  S FLD=$O(CERTARY(9002313.3121,NODE,FLD)) Q:FLD=""  D
 . . I FLD=.01 S NFLD="",NFLD=CERTARY(9002313.3121,NODE,FLD) D
 . . . I NFLD=301 S $P(MOREDATA("IBDATA",1,1),"^",5)=CERTARY(9002313.3121,NODE,.02)  ;GROUP ID
 . . . I NFLD=301 S $P(MOREDATA("IBDATA",1,1),"^",6)=CERTARY(9002313.3121,NODE,.02)  ;CARD ID
 . . . I NFLD=306 S $P(MOREDATA("IBDATA",1,1),"^",7)=CERTARY(9002313.3121,NODE,.02)  ;PATIENT RELATIONSHIP CODE
 . . . I NFLD=310 S $P(MOREDATA("IBDATA",1,1),"^",8)=CERTARY(9002313.3121,NODE,.02)  ;CARDHOLDER 1ST NAME
 . . . I NFLD=311 S $P(MOREDATA("IBDATA",1,1),"^",9)=CERTARY(9002313.3121,NODE,.02)  ;CARDHOLDER LAST NAME
 . . . I NFLD=412 S $P(MOREDATA("IBDATA",1,2),"^",1)=CERTARY(9002313.3121,NODE,.02)  ;DISPENSING FEE
 . . . I NFLD=423 S $P(MOREDATA("IBDATA",1,2),"^",2)=CERTARY(9002313.3121,NODE,.02)  ;BASIS OF COST DETERMINATION
 . . . I NFLD=426 S $P(MOREDATA("IBDATA",1,2),"^",3)=CERTARY(9002313.3121,NODE,.02)  ;USUAL & CUSTOMARY - BASE PRICE
 . . . I NFLD=430 S $P(MOREDATA("IBDATA",1,2),"^",4)=CERTARY(9002313.3121,NODE,.02)  ;GROSS AMT DUE
 . . . I NFLD=442 S $P(MOREDATA("IBDATA",1,2),"^",7)=CERTARY(9002313.3121,NODE,.02)  ;QTY
 . . . I NFLD=409 S $P(MOREDATA("IBDATA",1,2),"^",8)=CERTARY(9002313.3121,NODE,.02)  ;UNIT COST
 . . . I NFLD=407 S $P(MOREDATA("IBDATA",1,2),"^",9)=CERTARY(9002313.3121,NODE,.02)  ;NDC
 . . . I NFLD=403 S $P(MOREDATA("IBDATA",1,2),"^",10)=CERTARY(9002313.3121,NODE,.02)  ;FILL#
 . I $P(MOREDATA("IBDATA",1,2),"^",4)="" S $P(MOREDATA("IBDATA",1,2),"^",4)=$P(MOREDATA("IBDATA",1,2),"^",3)
 . K CERTARY
 . Q:CERTIFY
 . ;
 ;
 I '$D(MOREDATA("IBDATA",1)) D
 . N WW
 . S WW=$O(MOREDATA("IBDATA",""))
 . I WW'="" M MOREDATA("IBDATA",1)=MOREDATA("IBDATA",WW) K MOREDATA("IBDATA",WW)
 ;
 S MOREDATA("IBDATA",1,1)=$TR(MOREDATA("IBDATA",1,1),"abcdefghijklmnopqrstuvwxyz","ABCDEFGHIJKLMNOPQRSTUVWXYZ")
 S MOREDATA("IBDATA",1,2)=$TR(MOREDATA("IBDATA",1,2),"abcdefghijklmnopqrstuvwxyz","ABCDEFGHIJKLMNOPQRSTUVWXYZ")
 ;
 Q
