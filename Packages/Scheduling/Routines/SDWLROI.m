SDWLROI ;;IOFO BAY PINES/TEH/WAIT LIST STAT REPORT - ENROLLEE ; 01 Oct 2002  4:42 PM
 ;;5.3;scheduling;**263,412,415**;AUG 13 1993
 ;
 ; Removed Sort logic as routine exceeded SACC maximum size of 10000
 ; New routine SDWLROIS was created to perform the Sort functionality
 ;
 ;
EN ;
 N ZCODE,ZTDESC,ZTDTH,ZTIO,ZTQUEDED,ZTREQ,ZTRTN,ZTSK
 D INIT G END:$D(DUOUT)
 D SORT^SDWLROIS(SDWLBD,SDWLED,SDWLINS,.SDWL)  ; SD*5.3*412 new routine to perform sort
 I $$S^ZTLOAD G END
 D PRT
 G END
INIT ;
 I $D(CT) S SDWLCT=CT
 I $D(DATE) S SDWLDATE=DATE
 I $D(INS) S SDWLINS=INS
 I $D(ZTSAVE) D
 .S SDWLCT=$G(ZTSAVE("CT")),SDWLDATE=$G(ZTSAVE("DATE")),SDWLINS=$G(ZTSAVE("INS"))
 I SDWLINS'="ALL" F I=1:1 S SDWL=$P(SDWLINS,";",I) Q:SDWL=""  S SDWL("INS",+SDWL)=""
 S SDWLBD=$P(SDWLDATE,U,1),SDWLED=$P(SDWLDATE,U,2),SDWLPG=0
 D NOW^%DTC S Y=% D DD^%DT S SDWLDTP=$P(Y,":",1,2)
 K ZTSAVE
 N POP S POP=0  ;SD*5.3*412
 Q
 ;
PRT ;PRINT REPORT  SD*5.3*412 Q:POP added where needed to allow early exit
 G END:$$S^ZTLOAD
 S (T1,T2,T3,T4,T5,T6,T7,T8,T9,T10,T11,T12)=0  ;SD*5.3*415
 S (TT1,TT2,TT3,TT4,TT5,TT6,TT7,TT8,TT9,TT10,TT11,TT12,SDWLPG)=0 D HD,HD1  ;SD*5.3*415
 I '$D(^TMP("SDWLROI1",$J)) W !!,"No Wait List Data to Report" Q
 S SDWLINS="" F  S SDWLINS=$O(^TMP("SDWLROI1",$J,SDWLINS)) Q:SDWLINS=""  D  Q:POP  D T2 Q:POP  W !,"________________"  I $Y>(IOSL-7) D:$D(SDWLIOSR) SCR Q:POP  D HD,HD1  ;SD*5.3*412 added Quit for early exit
 .W !!,"INSTITUTION: ",SDWLINS
 .S W1="" F  S W1=$O(^TMP("SDWLROI1",$J,SDWLINS,W1)) Q:W1=""  D  Q:POP  D T1 Q:POP  I $Y>(IOSL-7) D:$D(SDWLIOSR) SCR Q:POP  D HD,HD1
 ..W !,"ENROLLMENT PRIORITY: ",$S(W1="P":"PRIOR",W1="N":"NEW",W1="E":"ESTABLISHED",1:"UNDETERMINED")
 ..S SDWLTY="" F  S SDWLTY=$O(^TMP("SDWLROI1",$J,SDWLINS,W1,SDWLTY)) Q:SDWLTY=""  D  Q:POP
 ...S SDWLTNM=$$EXTERNAL^DILFD(409.3,4,,SDWLTY) W !,$E(SDWLTNM,1,15) I $Y>(IOSL-7) D:$D(SDWLIOSR) SCR Q:POP  D HD,HD1
 ...S SDWLSCN="" F  S SDWLSCN=$O(^TMP("SDWLROI1",$J,SDWLINS,W1,SDWLTY,SDWLSCN)) Q:SDWLSCN=""  D  Q:POP
 ....S (T1,T2,T3,T4,T5,T6,T7,T8,T9,T10,T11)=0
 ....S SDWLSCNM="" F  S SDWLSCNM=$O(^TMP("SDWLROI1",$J,SDWLINS,W1,SDWLTY,SDWLSCN,SDWLSCNM)) Q:SDWLSCNM=""  D  Q:POP
 .....S SDWLPRI="" F  S SDWLPRI=$O(^TMP("SDWLROI1",$J,SDWLINS,W1,SDWLTY,SDWLSCN,SDWLSCNM,SDWLPRI)) Q:SDWLPRI=""  D  Q:POP
 ......W !,?2,$E(SDWLSCNM,1,10)," ",$S(SDWLPRI="A":"ASAP",SDWLPRI="F":"FUTURE",1:"")
 ......S SDWLPR=+$G(^TMP("SDWLROI1",$J,SDWLINS,W1,SDWLTY,SDWLSCN,SDWLSCNM,SDWLPRI,"PR")) W ?20,SDWLPR
 ......S SDWLCL=+$G(^TMP("SDWLROI1",$J,SDWLINS,W1,SDWLTY,SDWLSCN,SDWLSCNM,SDWLPRI,"CL")) W ?25,SDWLCL  ;SD*5.3*415
 ......S SDWLD=+$G(^TMP("SDWLROI1",$J,SDWLINS,W1,SDWLTY,SDWLSCN,SDWLSCNM,SDWLPRI,"SDWLD")) W ?30,SDWLD  ;SD*5.3*415
 ......S SDWLNC=+$G(^TMP("SDWLROI1",$J,SDWLINS,W1,SDWLTY,SDWLSCN,SDWLSCNM,SDWLPRI,"SDWLNC")) W ?35,SDWLNC  ;SD*5.3*415
 ......S SDWLSA=+$G(^TMP("SDWLROI1",$J,SDWLINS,W1,SDWLTY,SDWLSCN,SDWLSCNM,SDWLPRI,"SDWLSA")) W ?40,SDWLSA  ;SD*5.3*415
 ......S SDWLCC=+$G(^TMP("SDWLROI1",$J,SDWLINS,W1,SDWLTY,SDWLSCN,SDWLSCNM,SDWLPRI,"SDWLCC")) W ?45,SDWLCC  ;SD*5.3*415
 ......S SDWLNN=+$G(^TMP("SDWLROI1",$J,SDWLINS,W1,SDWLTY,SDWLSCN,SDWLSCNM,SDWLPRI,"SDWLNN")) W ?50,SDWLNN  ;SD*5.3*415
 ......S SDWLER=+$G(^TMP("SDWLROI1",$J,SDWLINS,W1,SDWLTY,SDWLSCN,SDWLSCNM,SDWLPRI,"SDWLER")) W ?55,SDWLER  ;SD*5.3*415
 ......S SDWLTR=+$G(^TMP("SDWLROI1",$J,SDWLINS,W1,SDWLTY,SDWLSCN,SDWLSCNM,SDWLPRI,"SDWLTR")) W ?60,SDWLTR  ;SD*5.3*415
 ......S SDWLAD=+$G(^TMP("SDWLROI1",$J,SDWLINS,W1,SDWLTY,SDWLSCN,SDWLSCNM,SDWLPRI,"AD")) W ?65,SDWLAD  ;SD*5.3*415
 ......S SDWLRR=+$G(^TMP("SDWLROI1",$J,SDWLINS,W1,SDWLTY,SDWLSCN,SDWLSCNM,SDWLPRI,"RR")) W ?70,SDWLRR  ;SD*5.3*415
 ......S SDWLNR=+$G(^TMP("SDWLROI1",$J,SDWLINS,W1,SDWLTY,SDWLSCN,SDWLSCNM,SDWLPRI,"NR")) W ?75,SDWLNR
 ......S T1=T1+SDWLPR,TT1=TT1+SDWLPR
 ......S T2=T2+SDWLCL,TT2=TT2+SDWLCL
 ......S T3=T3+SDWLD,TT3=TT3+SDWLD
 ......S T4=T4+SDWLNC,TT4=TT4+SDWLNC
 ......S T5=T5+SDWLSA,TT4=TT4+SDWLSA
 ......S T6=T6+SDWLCC,TT6=TT6+SDWLCC
 ......S T7=T7+SDWLNN,TT7=TT7+SDWLNN
 ......S T8=T8+SDWLER,TT8=TT8+SDWLER
 ......S T9=T9+SDWLTR,TT9=TT9+SDWLTR  ;SD*5.3*415
 ......S T10=T10+SDWLAD,TT10=TT10+SDWLAD  ;SD*5.3*415
 ......S T11=T11+SDWLRR,TT11=TT11+SDWLRR  ;SD*5.3*415
 ......S T12=T12+SDWLNR,TT12=TT12+SDWLNR  ;SD*5.3*415
 ......I $Y>(IOSL-7) D:$D(SDWLIOSR) SCR Q:POP  D HD,HD1
 Q
T1 ;
 W !?20,"---- ---- ---- ---- ---- ---- ---- ---- ---- ---- ---- ----"  ;SD*5.3*415
 W !,"Sub-Totals:"
 ;write sub-totals
 W ?20,T1,?25,T2,?30,T3,?35,T4,?40,T5,?45,T6,?50,T7,?55,T8,?60,T9,?65,T10,?70,T11,?75,T12,!  ;SD*5.3*415
 S (T1,T2,T3,T4,T5,T6,T7,T8,T9,T10,T11,T12)=0  ;SD*5.3*415
 I $Y>(IOSL-7) D:$D(SDWLIOSR) SCR Q:POP  D HD,HD1
 Q
T2 W !,"Institution Totals:"
 W ?20,TT1,?25,TT2,?30,TT3,?35,TT4,?40,TT5,?45,TT6,?50,TT7,?55,TT8,?60,TT9,?65,TT10,?70,TT11,?75,TT11,!  ;SD*5.3*415
 S (TT1,TT2,TT3,TT4,TT5,TT6,TT7,TT8,TT9,TT10,TT11,TT12)=0  ;SD*5.3*415
 I $Y>(IOSL-7) D:$D(SDWLIOSR) SCR Q:POP  D HD,HD1
 Q
HD W:$D(IOF) @IOF S SDWLPG=SDWLPG+1 W !!,SDWLDTP,?80-$L("Wait List (Sch/PCMM) Enroll Status Report")\2,"Wait List (Sch/PCMM) Enroll Status Report",?65,"Page: ",SDWLPG
 W !,?80-$L("STARTED Date: ")\2,"STARTED Date: " S Y=$P(SDWLDATE,U,1) D DD^%DT W Y
 W !,?80-$L("FINISHED Date: ")\2,"FINISHED Date: " S Y=$P(SDWLDATE,U,2) D DD^%DT W Y
 Q
HD1 ;
 W !,?20,"PREV",?65,"#",?70,"#",?75,"# NOT"  ;SD*5.3*415
 W !,"WAIT LIST TYPE",?20,"REMN",?25,"CLSD",?30,"DTH",?35,"NC",?40,"SA",?45,"CC",?50,"NN",?55,"ER",?60,"TR",?65,"ADD",?70,"REMN",?75,"REMVD",!  ;SD*5.3*415
 Q
SCR S DIR(0)="E" D ^DIR S:X="^" POP=1
 Q
END D EN^SDWLKIL
 K ^TMP("SDWLROI1",$J),^TMP("SDWLROI2",$J),CT,DATE,I,INS
 K T1,T2,T3,T4,T5,T6,T7,T8,T9,T10,T11,T12,X1,X2,X3,X0,Y1,W1,X4,X1,X2  ;SD*5.3*415
 K TT1,TT2,TT3,TT4,TT5,TT6,TT7,TT8,TT9,TT10,TT11,TT12,SDWLXEN,SDWLRDT,SDWLIOSR  ;SD*5.3*415
 Q
