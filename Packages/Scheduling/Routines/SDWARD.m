SDWARD ;ALB/GRR - LIST INPATIENTS WITH PENDING APPTS ; 14 NOV 84
 ;;5.3;Scheduling;;Aug 13, 1993
 S %DT(0)=-DT,%DT="AXE",%DT("A")="LIST PATIENTS WITH PENDING APPOINTMENTS ADMITTED ON DATE: " D ^%DT K %DT Q:Y<0  S SDY=Y D:'$D(DT) DT^SDUTL
 S VAR="SDY",VAL=SDY,PGM="START^SDWARD" D ZIS^DGUTQ G:POP END
START K ^UTILITY("SD",$J) U IO S Y=SDY D D^DIQ S SDPY=Y,Y=DT D D^DIQ S HY=Y
 F SDJ=SDY-.0001:0 S SDJ=$N(^DGPM("AMV1",SDJ)) Q:SDJ<0!(SDJ\1>SDY)  F DFN=0:0 S DFN=$N(^DGPM("AMV1",SDJ,DFN)) Q:DFN<0  D CHECK
 I '$D(^UTILITY("SD",$J)) W !,"NO PATIENTS FOUND" G END
 D HED
 S SDD=0 F SD=0:0 S SDD=$N(^UTILITY("SD",$J,SDD)) Q:SDD<0  S DFN=$N(^UTILITY("SD",$J,SDD,0)) D PN F SDI=0:0 S SDI=$N(^UTILITY("SD",$J,SDD,DFN,SDI)) Q:SDI<0  F SC=0:0 S SC=$N(^UTILITY("SD",$J,SDD,DFN,SDI,SC)) Q:SC<0  D PRT
 G END
CHECK Q:'$D(^DPT(DFN,"S"))
 F SDI=DT-.1:0 S SDI=$N(^DPT(DFN,"S",SDI)) Q:SDI<0  I $P(^(SDI,0),"^",2)'["C" S ^UTILITY("SD",$J,$P(^DPT(DFN,0),"^",1),DFN,SDI,+^DPT(DFN,"S",SDI,0))=""
 Q
PRT D:$Y+2>IOSL HED
 W !,?3,$S($D(^SC(SC,0)):$P(^(0),"^",1),1:"DELETED CLINIC")
 S Y=SDI\1 D D^DIQ W ?50,Y," " S X=SDI D TM^SDROUT0 W ?61,$J(X,8)
 Q
PN D:$Y+2>IOSL HED
 D PID^VADPT6 W !,$E($P(^DPT(DFN,0),"^",1),1,25),?29,VA("PID") K VA("BID"),VA("PID") I $D(^DPT(DFN,.1)) W ?43,$P(^(.1),"^",1)
 Q
HED W @IOF,!,"PATIENTS ADMITTED ",SDPY," WHO HAVE PENDING APPOINTMENTS",?66,HY,!,"PATIENT NAME",?32,"PT ID",?43,"WARD"
 W !,?3,"CLINIC",?50,"APPNT DATE",?64,"TIME",! F I=1:1:79 W "-"
 Q
END W !,@IOF K %DT,DFN,I,HY,SC,SD,SDD,SDI,SDJ,SDPY,X,Y,^UTILITY("SD") D CLOSE^DGUTQ,SDWARD^SDKILL Q
