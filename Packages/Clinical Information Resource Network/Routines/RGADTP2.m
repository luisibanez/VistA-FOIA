RGADTP2 ;BIR/DLR-ADT PROCESSOR TO RETRIGGER A08 or A04 MESSAGES WITH AL/AL (COMMIT/APPLICATION) ACKNOWLEDGEMENTS - CONTINUED ;10/30/02  10:04
 ;;1.0;CLINICAL INFO RESOURCE NETWORK;**27,20**;30 Apr 99
DBIA ;
 ;Reference to $$ADD^VAFCEHU1 supported by IA #2753
 ;Reference to $$SEND2^VAFCUTL1 supported by IA #2779
 ;Reference to EDIT^VAFCPTED supported by IA #2784
 Q
PROCIN(ARRAY,RGLOCAL,RGER,DFN,HL) ;
 N RGRSDFN,OTHSITE,NODE,ICN,CMORIEN,CMOR,CMORDISP,SENSTVTY,RMTDOD,LOCDOD,VAFCA,VAFCA08,HERE,BOGUS,ARAY,REP
 S REP=$E(HL("ECH"),2)
 S HERE=$P($$SITE^VASITE,"^",3)
 ;if sending site is your site quit
 Q:$G(ARRAY("MPISSITE"))=$G(HERE)
 S ARRAY(.097)=$P($$NOW^XLFDT,".")
 I $G(ARRAY("ICN"))'="" D
 . S RGRSDFN=$$GETDFN^MPIF001(+ARRAY("ICN")) I +RGRSDFN<1 S RGER=RGRSDFN_" ICN#"_$G(ARRAY("ICN")) Q  ;quit and return error msg
 . S OTHSITE=+ARRAY("MPISSITE")
 I $G(RGRSDFN)="" S RGRSDFN=$G(DFN)
 I $G(RGRSDFN)="" S RGER="-1^DFN not defined"
 Q:$G(RGER)
 I $G(OTHSITE)="" S OTHSITE=""
 I +$$SEND2^VAFCUTL1(RGRSDFN,"T")  ;quit and return error msg
 S NODE=$$MPINODE^MPIFAPI(RGRSDFN)
 S ICN=$P(NODE,"^")
 S CMORIEN=$P(NODE,"^",3)
 S CMOR=$$NS^XUAF4(CMORIEN)
 S CMORDISP=$P(CMOR,"^",1)
 S CMOR=$P(CMOR,"^",2)
 ;
 ;If patient is Sensitive at other site but not here send bulletin
 I $G(ARRAY("SENSITIVITY"))'="" S SENSTVTY=$G(ARRAY("SENSITIVITY")) D
 . N NAME S NAME=ARRAY("NAME")
 . I '$$SENSTIVE^RGRSENS(RGRSDFN),SENSTVTY D
 .. S ARAY("SSN")=ARRAY("SSN")
 .. S ARAY("SENDING SITE")=ARRAY("SENDING SITE")
 .. S ARAY("SENSITIVITY USER")=ARRAY("SENSITIVITY USER")
 .. S ARAY("SENSITIVITY DATE")=ARRAY("SENSITIVITY DATE")
 .. D SENSTIVE^RGRSBUL1(RGRSDFN,"ARAY",NAME)
 ;
 ;If patient has DATE OF DEATH (DOD) at remote site send bulletin
 ;Ignore time if present with date.
 S RMTDOD=$G(ARRAY("MPIDOD")),RMTDOD=$P(RMTDOD,".")
 S DFN=RGRSDFN D DEM^VADPT
 S LOCDOD=$P($P(VADM(6),"^"),".")
 ;If there is a remote DOD but no local DOD  OR
 ;if remote DOD is different from local DOD, send bulletin
 I RMTDOD D
 . N NAME S NAME=ARRAY("NAME")
 . S ARAY("SSN")=ARRAY("SSN")
 . S ARAY("SENDING SITE")=ARRAY("SENDING SITE")
 . D RMTDOD^RGRSBUL1(RGRSDFN,"ARAY",NAME,RMTDOD,LOCDOD)
 K VADM
 ;
NOTLOC I 'RGLOCAL D
 . ;if sending site is not the CMOR - log update into PDR if differences exist
 . I (OTHSITE)'=(CMOR) D  Q
 .. S VAFCA=$P($$NOW^XLFDT,".")_"^"_$$NOW^XLFDT_"^"_$G(ARRAY("SENDING SITE"))_"^"_RGRSDFN
 .. S ARRAY(.01)=$$FREE^RGRSPARS(ARRAY("NAME"))
 .. S ARRAY(.03)=$$FREE^RGRSPARS($G(ARRAY("MPIDOB")))
 .. S ARRAY(.09)=$$FREE^RGRSPARS($G(ARRAY("SSN")))
 .. S ARRAY(.02)=$$SEX^RGRSPARS($G(ARRAY("SEX")))
 .. S ARRAY(.2403)=$$FREE^RGRSPARS($G(ARRAY("MMN")))
 .. S ARRAY(991.01)=$P($G(ARRAY("ICN")),"V")
 .. N ARAY M ARAY(2)=ARRAY
 .. S VAFCA08=1 S BOGUS=$$ADD^VAFCEHU1(VAFCA,"ARAY")
 . ;if sending site is the CMOR - synchronize data
 . I (OTHSITE)=(CMOR) D
 .. S VAFCA08=1
 .. S ARAY(2,.01)=ARRAY("NAME")
 .. S ARAY(2,.03)=$G(ARRAY("MPIDOB"))
 .. S ARAY(2,.09)=$G(ARRAY("SSN"))
 .. S ARAY(2,.02)=$G(ARRAY("SEX"))
 .. S ARAY(2,.2403)=$G(ARRAY("MMN"))
 .. D EDIT^VAFCPTED(RGRSDFN,"ARAY(2)",".01;.03;.09;.02;.2403")
 .. ;check to see if edits were successful, if not set RGER="why it failed"
 .. N NAME,SSN,PDOB,SEX,MMN,OLDNAME,OLDHLNAM,OLDMMN,OLDHLMMN,HLNAME,HLMMN
 .. S NAME=$$GET1^DIQ(2,+RGRSDFN_",",.01,"I")
 .. S PDOB=$$GET1^DIQ(2,+RGRSDFN_",",.03,"I")
 .. S SSN=$$GET1^DIQ(2,+RGRSDFN_",",.09,"I")
 .. S SEX=$$GET1^DIQ(2,+RGRSDFN_",",.02,"I")
 .. S MMN=$$GET1^DIQ(2,+RGRSDFN_",",.2403,"I")
 .. D STDNAME^XLFNAME(.NAME,"F",.OLDNAME) S HLNAME=ARRAY("NAME") D STDNAME^XLFNAME(.HLNAME,"F",.OLDHLNAM)
 .. I NAME'=$G(HLNAME) S RGER=$S($G(RGER)'="":$G(RGER)_REP,1:"-1^")_"Name field failure"
 .. I PDOB'=$G(ARRAY("MPIDOB")) S RGER=$S($G(RGER)'="":$G(RGER)_REP,1:"-1^")_"DOB field failure"
 .. I SSN'=$G(ARRAY("SSN")) S RGER=$S($G(RGER)'="":$G(RGER)_REP,1:"-1^")_"SSN field failure"
 .. I SEX'=$G(ARRAY("SEX")) S RGER=$S($G(RGER)'="":$G(RGER)_REP,1:"-1^")_"SEX field failure"
 .. D STDNAME^XLFNAME(.MMN,"F",.OLDMMN) S HLMMN=ARRAY("MMN") D STDNAME^XLFNAME(.HLMMN,"F",.OLDHLMMN)
 .. I MMN'=$G(HLMMN) S RGER=$S($G(RGER)'="":$G(RGER)_REP,1:"-1^")_"MOTHER'S MAIDEN NAME field failure"
 .. ;send the updated fields to the MPI to synch site with MPI
 .. S ZTSAVE("DFN")="",ZTRTN="MPISYN^RGADTPC",ZTDESC="Sending Synchronized Patient Data to MPI...",ZTIO="RG QUEUE",ZTDTH=$H D ^%ZTLOAD
 Q
