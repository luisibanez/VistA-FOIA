RGADTP ;BIR/DLR-ADT PROCESSOR TO RETRIGGER A08 or A04 MESSAGES WITH AL/AL (COMMIT/APPLICATION) ACKNOWLEDGEMENTS ;5/28/02
 ;;1.0;CLINICAL INFO RESOURCE NETWORK;**26,27,20,34,35,40**;30 Apr 99
 ;
 ;Reference to BLDEVN^VAFCQRY and BLDPID^VAFCQRY supported by IA #3630
 ;Reference to EN1^VAFHLZEL is supported by IA #752
 ;
INIT ;
 N RGER,RGSITE,ARRAY,MSH,RGLOCAL,RGEVNT,REP,DIC,DR,DIE,DA,DLAYGO
 S RGER=""
 D IN
 D PROCIN
 D GENACK
 Q
PROC ;processing entry point
 N HLA,RGADT,PV1,DIC,ARRAY,RGEVNT,RGLOCAL,REP,ICN,RGSITE
 S RGEVNT=HL("ETN")
 I $G(HL("MID"))'="" S RGADT=HL("MID")
 I $G(HL("MID"))="" S RGADT=999
 D IN
 ;**35 removed reference to stop process if patient is "test" pt
 S ICN=$G(ARRAY("ICN"))
 ;S ICN=$$GETICN^MPIF001(DFN)
 I +$G(ICN)<1 Q  ;quit if no ICN
 I $E($G(ICN),1,3)=$P($$SITE^VASITE,"^",3) Q  ;quit if ICN is a local
 S ZTSAVE("DFN")="",ZTSAVE("RGEVNT")="",ZTSAVE("HLA(""HLS"",")="",ZTRTN="SEND^RGADTPC",ZTDESC="Sending HL7 Patient Update...",ZTIO="RG QUEUE",ZTDTH=$H D ^%ZTLOAD
 K ZTSAVE,ZTRTN,ZTDESC,ZTIO,ZTDTH
 Q
IN ;Process in the ADT A04/A08 (routing logic)
 N RGI,MSG,RG,SG,DFN,EVN,SITE,RGC,RGJ,DIC,PV1,PID,COMP
 ;set local flag to indicate the processing of an outbound for reformatting
 S REP=$E(HL("ECH"),2)
 S COMP=$E(HL("ECH"),1)
 I $P($G(HL("SAF")),COMP)=$P($$SITE^VASITE,"^",3) S RGLOCAL=1
 I $P($G(HL("SAF")),COMP)'=$P($$SITE^VASITE,"^",3) S RGLOCAL=0
 S RGC=$E($G(HL("ECH")),1)
 F RGI=1:1 X HLNEXT Q:HLQUIT'>0  S MSG=HLNODE,SG=$E(HLNODE,1,3) D
 . S RGJ=0 F  S RGJ=$O(HLNODE(RGJ)) Q:'RGJ  S MSG(RGJ)=HLNODE(RGJ)
 . D:SG?2A1(1A,1N) PICK
 ;if message MSH sending facility matches the PID assigning authority update
QUIT Q
ROUTE ;
 N RGERR
 I $G(RGEVNT)="" S RGEVNT=$G(HL("ETN"))
 N MPI S MPI=$$MPILINK^MPIFAPI() D
 . I $P($G(MPI),U)'=-1 S HLL("LINKS",1)="RG ADT-"_HL("ETN")_" 2.4 CLIENT^"_MPI
 . I $P($G(MPI),U)=-1 D
 .. N RGLOG,RGMTXT
 .. D START^RGHLLOG(HLMTIEN,"","")
 .. S RGMTXT="for DFN#"_$G(DFN)
 .. D EXC^RGHLLOG(224,"No MPI link identified"_RGMTXT,$G(DFN)) S RGERR=1
 I $G(RGERR)'=1 S ^XTMP("RG"_HL("ETN")_"%"_DFN,0)=$$FMADD^XLFDT(DT,5)_"^"_DT_"^"_"RG"_HL("ETN")_" msg to MPI for DFN "_DFN S ^XTMP("RG"_HL("ETN")_"%"_DFN,"MPI",0)="A"
 Q
RESP ;
 N RGER,RGSITE,ARRAY,MSH,RGLOCAL,RGEVNT
 N RGI,MSG,RG,SG,DFN,EVN,SITE,RGC,RGJ,DIC,PV1,PID
 D IN
 Q
PICK ;check routine for segment entry point
 I $T(@SG)]"" D @SG
 I $T(@SG)="" Q
 Q
MSA ;;
 ;process the MSA segment
 N ARRAY,CNT,DFN,EXIT,HLCOMP,RGAA,RGERR,RGEVNT,RGMSG,RETURN,RGX,RGY
 I RGLOCAL S HLA("HLS",RGI)=MSG
 S RGAA=MSG
 S EXIT=0
 S RGMSG=$P(RGAA,HL("FS"),3)
 S RGERR=$P(RGAA,HL("FS"),4)
 S RGMSG=$$MSG^HLCSUTL(RGMSG,"RETURN(1)")
 K RGMSG
 S CNT=1,RGX=0 F  S RGX=$O(RETURN(1,RGX)) Q:'RGX!(EXIT=1)  D
 . I RETURN(1,RGX)'="" D
 .. I $D(RGMSG) S RGMSG(CNT)=RETURN(1,RGX),CNT=CNT+1
 .. I '$D(RGMSG) S RGMSG=RETURN(1,RGX),RGY=RGX
 . I RETURN(1,RGX)="" D  S CNT=1 K RGMSG
 .. I $E(RETURN(1,RGY),1,3)="MSH" D MSH
 .. I $E(RETURN(1,RGY),1,3)="PID" D PIDP^RGADTP1(.RGMSG,.ARRAY,.HL) S EXIT=1
 S DFN=$G(ARRAY("DFN"))
 I $D(^XTMP("RG"_HL("ETN")_"%"_DFN,0)) K ^XTMP("RG"_HL("ETN")_"%"_DFN)
 Q
MSH ;;
 S MSH=1
 I RGLOCAL S HLA("HLS",RGI)=MSG
 I 'RGLOCAL S RGC=$E(HL("ECH"),1)
 S RGSITE=$P($P(MSG,HL("FS"),4),RGC)
 S RGEVNT=$P($P(MSG,HL("FS"),9),RGC,2)
 Q
EVN ;;
 N CNT,ERR
 S EVN=RGI
 I RGLOCAL S (EVN(1),HLA("HLS",RGI))=MSG
 I 'RGLOCAL D
 .S ARRAY("EVR")=$P(MSG,HL("FS"),2)
 .S ARRAY("DLT")=$$FMDATE^HLFNC($P(MSG,HL("FS"),3))
 .S ARRAY("EVNAME")=$$FMNAME^XLFNAME($P(MSG,HL("FS"),2),"",$E(HL("ECH"),1))
 .S ARRAY("SENDING SITE")=$P(MSG,HL("FS"),8)
 Q
EVNP ;
 N EVNX
 I $G(DFN)'="" D BLDEVN^VAFCQRY(DFN,"1,2,4,5,6,7",.EVN,.HL,$G(HL("ETN")),.ERR) S CNT=0,EVNX=0 F  S EVNX=$O(EVN(EVNX)) Q:'EVNX  D
 . I CNT>0 S HLA("HLS",EVN,CNT)=EVN(EVNX),CNT=CNT+1
 . I CNT'>0 S HLA("HLS",EVN)=EVN(EVNX),CNT=CNT+1
 Q
PID ;;
 N CNT,PIDX
 I RGLOCAL D
 .N HLCOMP S HLCOMP=$E(HL("ECH"),1)
 .S HLA("HLS",RGI)=MSG
 .S DFN=+$P(MSG,HL("FS"),4)
 .D EVNP
 .D BLDPID^VAFCQRY(DFN,1,"ALL",.PID,.HL)
 .;get Primary ICN value in the PID segment
 .S ARRAY("ICN")=+$P($P(PID(1),HL("FS"),4),HLCOMP)
 .S CNT=0,PIDX=0 F  S PIDX=$O(PID(PIDX)) Q:'PIDX  D
 .. I CNT>0 S HLA("HLS",RGI,CNT)=PID(PIDX),CNT=CNT+1
 .. I CNT'>0 S HLA("HLS",RGI)=PID(PIDX),CNT=CNT+1
 I 'RGLOCAL D PIDP^RGADTP1(.MSG,.ARRAY,.HL)
 Q
PD1 ;;
 I RGLOCAL S HLA("HLS",RGI)=MSG
 I 'RGLOCAL S (ARRAY(991.03),ARRAY("CMOR"))=$P($P(MSG,HL("FS"),4),RGC)
 Q
PV1 ;;
 I RGLOCAL S HLA("HLS",RGI)=MSG
 Q
OBX ;;
 N COMP S COMP=$E(HL("ECH"),1)
 I RGLOCAL S HLA("HLS",RGI)=MSG
 I 'RGLOCAL D:$$FREE^RGRSPARS($P($P(MSG,HL("FS"),4),RGC,2))="SECURITY LEVEL"
 . S ARRAY("SENSITIVITY")=$$SENSTIVE^RGRSPARS($P(MSG,HL("FS"),6),COMP)
 . S ARRAY("SENSITIVITY USER")=$$FREE^RGRSPARS($P($P(MSG,HL("FS"),17),RGC,2))_","_$$FREE^RGRSPARS($P($P(MSG,HL("FS"),17),RGC,3))
 . S ARRAY("SENSITIVITY DATE")=$$FREE^RGRSPARS($$FMDATE^HLFNC($P(MSG,HL("FS"),15)))
 Q
ZPD ;;
 I RGLOCAL S HLA("HLS",RGI)=MSG
 ;I 'RGLOCAL S ARRAY(.351)=$$FREE^RGRSPARS($P(MSG,HL("FS"),5))
 Q
ZSP ;;
 I RGLOCAL S HLA("HLS",RGI)=MSG
 I 'RGLOCAL D
 . S ARRAY(.301)=$$YESNO^RGRSPARS($P(MSG,HL("FS"),3))
 . S ARRAY(.302)=$$FREE^RGRSPARS($P(MSG,HL("FS"),4))
 . S ARRAY(.323)=$$POS^RGRSPARS($P(MSG,HL("FS"),5))
 Q
ZEL ;;
 I RGLOCAL D
 .;**40 to rebuild ZEL segment
 .I '$D(DFN) S HLA("HLS",RGI)=MSG Q
 .; ^ if don't know DFN pass back original ZEL segment
 .N VAFZEL
 .D EN1^VAFHLZEL(DFN,"1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22",2,.VAFZEL) ;build a complete ZEL segment
 .;need to take into account may be more than 1 array entry and that each entry could go over 245 so there would be another subscript
 .N CNT,ZELX
 .S (CNT,ZELX)=0 F  S ZELX=$O(VAFZEL(ZELX)) Q:'ZELX  D
 ..I CNT>0 S HLA("HLS",RGI,CNT)=VAFZEL(ZELX),CNT=CNT+1
 ..I CNT'>0 S HLA("HLS",RGI)=VAFZEL(ZELX),RGI=RGI+2
 .S RGI=RGI-2 ;reset to not end up wih 2 "" entries before next segment
 I 'RGLOCAL D
 . S ARRAY(.361)=$$ELIG^RGRSPARS($P(MSG,HL("FS"),3))
 . S ARRAY(.3612)=$$FREE^RGRSPARS($P(MSG,HL("FS"),12))
 . S ARRAY(.3615)=$$FREE^RGRSPARS($P(MSG,HL("FS"),14))
 . S ARRAY(391)=$$TYPE^RGRSPARS($P(MSG,HL("FS"),10))
 . S ARRAY(1901)=$$VETERAN^RGRSPARS($P(MSG,HL("FS"),9))
 Q
ZCT ;;
 I RGLOCAL S HLA("HLS",RGI)=MSG
 I 'RGLOCAL D
 . S ARRAY(.211)=$$FREE^RGRSPARS($P(MSG,HL("FS"),4))
 . S ARRAY(.219)=$$FREE^RGRSPARS($P(MSG,HL("FS"),7))
 Q
ZEM ;;
 I RGLOCAL S HLA("HLS",RGI)=MSG
 I 'RGLOCAL D
 . S ARRAY(.31115)=$$EMP^RGRSPARS($P(MSG,HL("FS"),4))
 Q
ZFF ;;
 I RGLOCAL S HLA("HLS",RGI)=MSG
 I 'RGLOCAL S ARRAY("FLD")=$P(MSG,HL("FS"),3)
 Q
PROCIN ;
 D PROCIN^RGADTP2(.ARRAY,.RGLOCAL,.RGER,.DFN,.HL)
 Q
GENACK ;
 N RGCNT,IEN,RG
 I $G(ARRAY("DFN"))'>0 S RGER="-1^Unknown ICN#"_$G(ARRAY("ICN"))_" and SSN#"_$G(ARRAY(.09))
 S RGCNT=1
 S HLA("HLA",RGCNT)="MSA"_HL("FS")_"AA"_HL("FS")_HL("MID")_HL("FS")_$S(+$G(RGER)<0:$P(RGER,"^",2,3),1:""),RGCNT=RGCNT+1
 S RGSITE=$$LKUP^XUAF4(RGSITE)
 D LINK^HLUTIL3(RGSITE,.RG) S IEN=$O(RG(0)) S HLL("LINKS",1)="^"_RG(IEN)
 D GENACK^HLMA1(HL("EID"),HLMTIENS,HL("EIDS"),"LM",1,.HLRESLTA,"",.HL)
 K HLA
 Q
RSP ;
 Q
