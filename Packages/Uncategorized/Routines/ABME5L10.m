ABME5L10 ; IHS/ASDST/DMJ - Header 
 ;;2.6;IHS Third Party Billing System;**6**;NOV 12, 2009
 ;Header Segments
 ;
EP ;START HERE
 S ABMLXCNT=0
 K ABM
 D FRATE^ABMDF11
 D ^ABMERGRV
 S ABMREV=""
 F  S ABMREV=$O(ABMRV(ABMREV)) Q:ABMREV=""  D
 .Q:ABMREV=9999
 .S ABMCODE=-1
 .F  S ABMCODE=$O(ABMRV(ABMREV,ABMCODE)) Q:ABMCODE=""  D
 ..S ABMCNTR=0
 ..F  S ABMCNTR=$O(ABMRV(ABMREV,ABMCODE,ABMCNTR)) Q:ABMCNTR=""  D
 ...D LOOP
 K ABMREV,ABMCODE,ABMCNTR
 Q
 ;
LOOP ;
 S ABMLXCNT=ABMLXCNT+1
 D EP^ABME5LX
 D WR^ABMUTL8("LX")
 D EP^ABME5SV2
 D WR^ABMUTL8("SV2")
 I $P(ABMRV(ABMREV,ABMCODE,ABMCNTR),U,10) D
 .D EP^ABME5DTP("472","D8",$P(ABMRV(ABMREV,ABMCODE,ABMCNTR),U,10))
 I '$P(ABMRV(ABMREV,ABMCODE,ABMCNTR),U,10) D
 .D EP^ABME5DTP(472,"D8",$P(ABMB7,U))
 D WR^ABMUTL8("DTP")
 ;
 ; Loop 2420A - Attending Physician
 I $P($G(ABMRV(ABMREV,ABMCODE,ABMCNTR)),U,15) D
 .S ABM("PRV")=$P(ABMRV(ABMREV,ABMCODE,ABMCNTR),U,15)
 .Q:ABM("PRV")=$O(ABMP("PRV","A",0))
 .D EP^ABME5NM1("71")
 .D WR^ABMUTL8("NM1")
 .I ABMNPIU="N" D
 ..D EP^ABME5REF("EI",9999999.06,DUZ(2))
 ..D WR^ABMUTL8("REF")
 .I ABMNPIU'="N" D
 ..D EP^ABME5REF(ABMP("RTYPE"),200,ABM("PRV"))
 ..D WR^ABMUTL8("REF")
 ;
 ; Loop 2420B - Operating Physician Name
 I $P($G(ABMRV(ABMREV,ABMCODE,ABMCNTR)),U,16) D
 .S ABM("PRV")=$P(ABMRV(ABMREV,ABMCODE,ABMCNTR),U,16)
 .Q:ABM("PRV")=$O(ABMP("PRV","O",0))
 .D EP^ABME5NM1("72")
 .D WR^ABMUTL8("NM1")
 .I ABMNPIU="N" D
 ..D EP^ABME5REF("EI",9999999.06,DUZ(2))
 ..D WR^ABMUTL8("REF")
 .I ABMNPIU'="N" D
 ..D EP^ABME5REF(ABMP("RTYPE"),200,ABM("PRV"))
 ..D WR^ABMUTL8("REF")
 ;
 ; Loop 2420C - Other Physician Name
 I $P($G(ABMRV(ABMREV,ABMCODE,ABMCNTR)),U,18) D
 .S ABM("PRV")=$P(ABMRV(ABMREV,ABMCODE,ABMCNTR),U,18)
 .Q:ABM("PRV")=$O(ABMP("PRV","T",0))
 .D EP^ABME5NM1("73")
 .D WR^ABMUTL8("NM1")
 .I ABMNPIU="N" D
 ..D EP^ABME5REF("EI",9999999.06,DUZ(2))
 ..D WR^ABMUTL8("REF")
 .I ABMNPIU'="N" D
 ..D EP^ABME5REF(ABMP("RTYPE"),200,ABM("PRV"))
 ..D WR^ABMUTL8("REF")
 ;
 ; Loop 2420D - Referring Physician Name
 I $P($G(ABMRV(ABMREV,ABMCODE,ABMCNTR)),U,17) D
 .S ABM("PRV")=$P(ABMRV(ABMREV,ABMCODE,ABMCNTR),U,17)
 .Q:ABM("PRV")=$O(ABMP("PRV","F",0))
 .D EP^ABME5NM1("DN")
 .D WR^ABMUTL8("NM1")
 .D EP^ABME5PRV("RF",ABM("PRV"))
 .D WR^ABMUTL8("PRV")
 .I ABMNPIU="N" D
 ..D EP^ABME5REF("EI",9999999.06,DUZ(2))
 ..D WR^ABMUTL8("REF")
 .I ABMNPIU'="N" D
 ..D EP^ABME5REF(ABMP("RTYPE"),200,ABM("PRV"))
 ..D WR^ABMUTL8("REF")
 Q
