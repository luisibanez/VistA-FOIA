BLRLINK3 ; IHS/HQT/MJL - CONT.OF BLR - IHS LABORATORY TO PCC ,PCC DATA TRANSFER ; DEC 09, 2008 8:30 AM ; [ Aug 18, 2010 ]
 ;;5.2;IHS LABORATORY;**1010,1011,1014,1015,1018,1021,1022,1024,1025,1027,1028,1029**;NOV 01, 1997
 ;
 ; EP
 D:$G(SNAPSHOT) ENTRYAUD^BLRUTIL("^BLRLINK3 0.0")
 S BLRVFILE="9000010"_$S(BLRSS="CH":".09",BLRSS="MI":".25",BLRSS="BB":".31",1:"??")
 I BLRVFILE["??" S BLRERR=1,BLRBUL=2,BLRPCC="Unknown Test Subscript - link not yet implemented" Q
 S BLRVGL=$$GETGREF(BLRVFILE)
 D VBUILD Q:BLRERR
 D VEDIT
 D:$G(SNAPSHOT) ENTRYAUD^BLRUTIL("^BLRLINK3 9.0")
 Q
 ;
VBUILD ; EP create APCDALVR array which is the array containing the elements to be passed to PCC
 D:$G(SNAPSHOT) ENTRYAUD^BLRUTIL("VBUILD^BLRLINK3 0.0")
 ;--- BEGIN cmi/anch/maw REF LAB -- LR*5.2*1021
 I $G(^BLRTXLOG("BVER",BLRIEN))]"" D  Q  ;cmi/maw don't move to pcc if held for verification, ref lab
 . S BLRERR=1,BLRBUL=0
 . S BLRPCC="Result being held for verification"
 ;--- END cmi/anch/maw end REF LAB -- LR*5.2*1021
 ;
 ;----- BEGIN IHS/OIT/MKK MODIFICATIONS LR*5.2*1022
 ; Make sure CLINIC STOP CODE of LABORATORY SERVICES is used, if it exists
 NEW LABCLIN,OUT
 D FIND^DIC(40.7,"","","","LABORATORY SERVICES","","","","","OUT")
 S LABCLIN=+$G(OUT("DILIST",2,1))
 I LABCLIN>0 D
 . S APCDALVR("APCDTCLN")="`"_LABCLIN
 . S APCDALVR("APCDCLN")="`"_LABCLIN
 ;----- END IHS/OIT/MKK MODIFICATIONS LR*5.2*1022
 ;
 D:$G(SNAPSHOT) ENTRYAUD^BLRUTIL("VBUILD^BLRLINK3 1.0","APCDALVR")
 ;
 S APCDALVR("APCDTCSF")=BLRSTAT
 S APCDALVR("APCDTLAB")=$S(BLRTLAB="":BLRTNAM,1:"`"_BLRTLAB)
 S APCDALVR("APCDTORD")=BLRORD
 S APCDALVR("APCDTCDT")=BLRCDT
 S APCDALVR("APCDTODT")=BLRODT
 S APCDALVR("APCDTACC")=BLRACC
 ;
 ; send parent ien of V file entry for children of panel
 S:BLRPAREN'="" APCDALVR("APCDTPNT")="`"_$P($G(^BLRTXLOG(BLRPAREN,1)),U,5)
 ;ADDED FOR LOINC PROJECT
 S:$G(APCDALVR("APCDTPNT"))="`" APCDALVR("APCDTPNT")=""  ;ITSC/IHS/TPF 03/04/02 ADDED FOR LOINC
 S APCDALVR("APCDTLNC")="`"_$G(BLRLOINC)
 ;
 ;----- BEGIN LR*5.2*1028;08/18/10;IHS/OIT/MPW - IHS LOINC/UCUM MODIFICATIONS 
 N XXX,LRIEN
 S XXX=$G(BLRVAL(0)),LRIEN=$P(XXX,U,6)
 I $P(^LAB(60,LRIEN,0),U,5)="" S APCDALVR("APCDTLNC")="`"_$P($G(^LAB(60,LRIEN,9999999)),U,2)
 D:$G(SNAPSHOT) ENTRYAUD^BLRUTIL("VBUILD^BLRLINK3 3.0","APCDALVR")
 K XXX,LRIEN
 ;----- END LR*5.2*1028 - IHS LOINC/UCUM MODIFICATIONS - IHS/OIT/MPW
 ;
 S APCDALVR("APCDTRDT")=$G(BLRCOMDT)
 S APCDALVR("APCDTCLS")="`"_$G(BLRCOLSA)
 S:$G(APCDALVR("APCDTLNC"))="`" APCDALVR("APCDTLNC")=""
 S:APCDALVR("APCDTCLS")="`" APCDALVR("APCDTCLS")=""
 ;END LOINC STUFF
 ;
 S APCDALVR("APCDTCOS")=BLRCOST
 S APCDALVR("APCDTBIL")=BLRBILL
 S APCDALVR("APCDTCPS")=BLRCPTST
 S APCDALVR("APCDTSDI")=BLRSDI
 ;
 S:$G(BLRUNIT)'="" APCDALVR("APCDTUNI")=BLRUNIT
 ;
 S APCDALVR("APCDTRFL")=BLRRFL
 S APCDALVR("APCDTRFH")=BLRRFH
 ;
 ;S APCDALVR("APCDTRES")=$E(BLRRES,1,10)
 D:$G(BLRRES)'="" CHSETCOD   ; LR*5.2*1027 -- IHS/OIT/MKK - Check SET OF CODES
 S APCDALVR("APCDTRES")=$E(BLRRES,1,30)  ;cmi/maw 3/25/03 changed for ref lab
 ;S APCDALVR("APCDTRES")=BLRRES   ;IHS/ITSC/TPF 03/25/02 WHY A LIMIT OF 10?
 ;THIS IS OLD COMMENT STUFF
 ;I $D(BLRCOM(1)) S APCDALVR("APCDTLC1")=BLRCOM(1)
 ;I $D(BLRCOM(2)) S APCDALVR("APCDTLC2")=BLRCOM(2)
 ;I $D(BLRCOM(3)) S APCDALVR("APCDTLC3")=BLRCOM(3)
 ;
 ;create micro or Blood Bank specific elements in the APCDALVR array
 I BLRSS="MI" D  ;IHS/DIR TUC/FJE 12/08/98
 . S APCDALVR("APCDTORG")=$S(BLRORG="":BLRORGN,1:"`"_BLRORG),APCDALVR("APCDTANT")=$S(BLRANT="":BLRANTN,1:"`"_BLRANT) ;IHS/DIR TUC/FJE 12/08/98
 . ;S APCDALVR("APCDTCOL")=$S($G(BLCOLSP)="":$G(BLCOLSP),1:"`"_BLRCOLSP) S:+BLRCOMPD APCDALVR("APCDTCMD")=BLRCOMPD  ;IHS/DIR TUC/FJE 12/08/98
 . S APCDALVR("APCDTCOL")=$S($G(BLCOLSP)'="":$G(BLCOLSP),1:"`"_BLRCOLSP) S:+BLRCOMPD APCDALVR("APCDTCMD")=BLRCOMPD  ;IHS/ITSC/TPF CHINLE FIX 08/16/02
 S:BLRSS="BB" APCDALVR("APCDTBTN")=BLRBTN,APCDALVR("APCDTANT")=$S(BLRANT="":BLRANTN,1:"`"_BLRANT)
 S:BLREPRV'="" APCDALVR("APCDTEPR")="`"_BLREPRV
 S:BLROPRV'="" APCDALVR("APCDTPRV")="`"_BLROPRV
 S:BLROPRV="" APCDALVR("APCDTOPR")=$S(BLROPNM="":BLREPNM,1:BLROPNM)
 S:BLRSITE'="" APCDALVR("APCDTSTE")="`"_BLRSITE
 I BLRCPT,$D(^BLRCPT(BLRCPT,0)) S:BLRCPT'="" APCDALVR("APCDTCPT")="`"_BLRCPT ;FJE 1/25/2000
 I BLRVIEN'="" S BLRMOD=1,APCDALVR("APCDLOOK")=BLRVIEN ; do mod
 D:$G(SNAPSHOT) ENTRYAUD^BLRUTIL("VBUILD^BLRLINK3 9.0","APCDALVR")
 Q
 ;
VEDIT ; EP update V file entries
 D:$G(SNAPSHOT) ENTRYAUD^BLRUTIL("VEDIT^BLRLINK3 0.0","APCDALVR")
 I BLRSTAT="D" S DIK=BLRVGL,DA=BLRVIEN D CALLDIK^BLRNLINK Q  ;;results null - do delete
 S BLRTRAN=1
 ;M ^BLRAPCD=APCDALVR
 S APCDALVR("APCDTABN")=$S('$D(BLRMOD):BLRABNL,BLRABNL'="":BLRABNL,BLRVIEN="":"",$P($G(@(BLRVGL_"BLRVIEN,0)")),U,5)'="":"@",1:"") ;must be deleted if result value changes
 ; S APCDALVR("APCDATMP")=$S($D(BLRMOD):"[APCDALVR "_BLRVFILE_" (MOD)]",1:"[APCDALVR "_BLRVFILE_" (ADD)]") D ^APCDALVR
 ; ----- BEGIN IHS/OIT/MKK LR*5.2*1024 MODIFICATIONS
 ;   Several entries in the APCDALVR array were being cleared, so they are now being
 ;   double-checked BEFORE the call to APCDALVR.
 S APCDALVR("APCDATMP")=$S($D(BLRMOD):"[APCDALVR "_BLRVFILE_" (MOD)]",1:"[APCDALVR "_BLRVFILE_" (ADD)]")
 I $G(APCDALVR("APCDDATE"))="" S APCDALVR("APCDDATE")=$G(BLRCD)
 I $G(APCDALVR("APCDLOC"))="" S APCDALVR("APCDLOC")=$G(BLR("SITE"))
 I $G(APCDALVR("APCDPAT"))="" S APCDALVR("APCDPAT")=$G(BLRVADFN)
 I $G(APCDALVR("APCDCAT"))="" S APCDALVR("APCDCAT")=$G(BLRVCAT)
 I $G(APCDALVR("APCDTYPE"))="" S APCDALVR("APCDTYPE")=$S($P($G(^APCCCTRL(BLR("SITE"),0)),U,4)'="":$P($G(^(0)),U,4),1:"I")
 ;
 ; ----- BEGIN IHS/OIT/MKK - LR*5.2*1027
 D:$G(SNAPSHOT) ENTRYAUD^BLRUTIL("^BLRLINK3 5.0","APCDALVR")
 I $G(APCDALVR("APCDTLPV"))=""&($G(BLRLPOV)'="") D
 . NEW IHSLABPOV
 . I $G(BLRLPOV)["^" S IHSLABPOV=$P(BLRLPOV,"^",2)
 . S:$G(IHSLABPOV)="" IHSLABPOV=$P(BLRLPOV,"^")
 . S APCDALVR("APCDTLPV")=IHSLABPOV
 ; ----- END IHS/OIT/MKK - LR*5.2*1027
 ;
 ;----- BEGIN IHS/OIT/MKK MODIFICATIONS LR*5.2*1025
 ; USER LAST UPDATE field in Visit file is populated by the DUZ.
 ; Temporarily change the DUZ, if possible
 NEW OLDDUZ,USER,TSTR
 M OLDDUZ=DUZ
 ; I +$G(BLRLOGDA)>0 S USER=$P($G(^BLRTXLOG(BLRLOGDA,20)),"^",6)
 ; I +$G(USER)>0 S TSTR="DUZ"_"=USER"  S @TSTR
 ;----- END IHS/OIT/MKK MODIFICATIONS LR*5.2*1025
 ;
 ;----- BEGIN IHS/OIT/MKK MODIFICATIONS LR*5.2*1027
 D RESETDUZ^BLRPCCVC
 ;S APCDALVR("APCDTEPR")="`"_DUZ       ; IHS/OIT/MKK - LR*5.2*1029
 ;----- END IHS/OIT/MKK MODIFICATIONS LR*5.2*1027
 ;
 I $G(APCDALVR("BLRLINK"))="" S APCDALVR("BLRLINK")=1
 ;
 D:$G(SNAPSHOT) ENTRYAUD^BLRUTIL("VEDIT^BLRLINK3 5.0","APCDALVR")
 ;
 ; ----- BEGIN IHS/OIT/MKK - LR*5.2*1027 -- Make sure Ref Lab Reference Ranges used, if they exist
 D HL7REFLR
 ; ----- END IHS/OIT/MKK - LR*5.2*1027
 ;
 D ^APCDALVR
 ; 
 D:$G(SNAPSHOT) ENTRYAUD^BLRUTIL("VEDIT^BLRLINK3 6.0","APCDALVR")
 ;
 ; ----- END IHS/OIT/MKK LR*5.2*1024 MODIFICATIONS
 ;----- BEGIN IHS/OIT/MKK MODIFICATIONS LR*5.2*1025
 S TSTR="DUZ"_"=OLDDUZ"
 ; S @TSTR                          ; Change DUZ back
 M @TSTR                          ; Change DUZ back -- LR*5.2*1027
 ;----- END IHS/OIT/MKK MODIFICATIONS LR*5.2*1025
 ;
 I '$D(APCDALVR("APCDAFLG")) D
 .S:'BLRVIEN BLRVIEN=$G(APCDALVR("APCDADFN"))
 .I 'BLRVIEN,$D(@(BLRVGL_"""ALR"",APCDALVR(""APCDVSIT""),BLRACC,BLRTLAB)")) S BLRVIEN=$O(@(BLRVGL_"""ALR"",APCDALVR(""APCDVSIT""),BLRACC,BLRTLAB,0)"))
 .K DIE,DA,DR
 .S DIE="^BLRTXLOG(",DA=BLRIEN,DR="104///^S X="_BLRVFILE_";105///^S X=BLRVIEN"
 .D ^DIE
 .K BLRCMSG  ;IHS/ITSC/TPF 02/17/2004 ADDED TO CLEAR ERRO MESSAGE FOR COMMENTS LR*5.2*1018 RESULT OF "No V Lab Entry" message showing up in Tuba City EHR alpha testing
 .I $O(BLRCOM("")) D
 ..S BLRCMSG=$$LABC^APCDALVR(BLRVIEN,.BLRCOM)  ;maw comments
 .I $G(BLRCMSG)]"" S BLRBUL=2,BLRERR=1,BLRPCC=$P($G(BLRCMSG),U,2)  ;maw 
 .K BLRCMSG  ;IHS/ITSC/TPF 02/17/2004 ADDED TO CLEAR ERRO MESSAGE FOR COMMENTS
 .Q
 .S BLRBUL=2,BLRPCC="Another user is editing this TX file entry..."_BLRIEN S BLRERR=1
 .W:'BLRQUIET !,"Another user is editing this TX file entry..."_BLRIEN,! S BLRERR=1
 ;
 I $G(APCDALVR("APCDAFLG"))=1 S BLRBUL=2,BLRPCC="Invalid template for result display"_" FLG = 1" W:'BLRQUIET !,BLRPCC,!
 D ^BLRLINKP:$G(APCDALVR("APCDAFLG"))=2
 D:$G(SNAPSHOT) ENTRYAUD^BLRUTIL("VEDIT^BLRLINK3 9.0","APCDALVR")
 Q
 ;
GETGREF(BLRX)      ; EP
 D:$G(SNAPSHOT) ENTRYAUD^BLRUTIL("GETGREF^BLRLINK3 0.0")
 Q $G(^DIC(BLRX,0,"GL"))
 ;
 ; ----- BEGIN IHS/OIT/MKK - LR*5.2*1027
 ;       IF and ONLY IF the transaction is tied to an incoming HL7 message
 ;       get Reference Ranges & Units from HL7 message
HL7REFLR ; EP 
 NEW ABNFLAG,REFHIGH,REFLOW,UNITS,WOT
 S WOT=$$CHKINHL7()
 Q:WOT<1
 ;
 S:$G(UNITS)'="" APCDALVR("APCDTUNI")=UNITS
 S:$G(REFLOW)'="" APCDALVR("APCDTRFL")=REFLOW
 S:$G(REFHIGH)'="" APCDALVR("APCDTRFH")=REFHIGH
 Q
 ;
CHKINHL7() ; EP
 NEW DNIEN,DNDESC,F60IEN,HL7TEST,LRAA,LRAD,LRAN,LRAS,STR,UID
 ;
 Q:+$G(BLRLOGDA)<1 0
 ;
 S F60IEN=+$P($G(^BLRTXLOG(BLRLOGDA,0)),"^",6)     ; File 60 IEN
 ;
 S LRAS=$P($G(^BLRTXLOG(BLRLOGDA,12)),"^",2)       ; Accession Number
 D GETACCCP^BLRUTIL3(LRAS,.LRAA,.LRAD,.LRAN)
 Q:LRAA<1!(LRAD<1)!(LRAN<1) 0
 ;
 S UID=+$P($G(^LRO(68,LRAA,1,LRAD,1,LRAN,.3)),"^")
 Q:UID<1 0
 ;
 Q:$$GETINTHU^BLRLINK1(UID)<1 0
 ;
 S STR=$G(^TMP("BLR",$J,UID,F60IEN))
 Q:$L(STR)<1 0
 ;
 S ABNFLAG=$P(STR,"^",2)
 S REFLOW=$P(STR,"^",3)
 S REFHIGH=$P(STR,"^",4)
 S UNITS=$P(STR,"^",5)
 ;
 S:$L(ABNFLAG) APCDALVR("APCDTABN")=$G(ABNFLAG)
 S:$L(UNITS) APCDALVR("APCDTUNI")=$G(UNITS)
 S:$L(REFLOW) APCDALVR("APCDTRFL")=$G(REFLOW)
 S:$L(REFHIGH) APCDALVR("APCDTRFH")=$G(REFHIGH)
 ;
 S:$G(SNAPSHOT) ^TMP("BLRLINK3",$J,UID,F60IEN)=$G(ABNFLAG)_"^"_$G(UNITS)_"^"_$G(REFLOW)_"^"_$G(REFHIGH)
 Q 1
 ;
 ;
CHSETCOD ; EP - Check to see if SET OF CODES & "Change" Result, if Necessary
 NEW CHANGED,DATANAME,F60PTR,LRPIECE,LRSET,Q2
 ;
 S F60PTR=+$TR($G(APCDALVR("APCDTLAB")),"`")
 S DATANAME=$G(^LAB(60,F60PTR,.2))
 Q:DATANAME<1
 ;
 Q:$P($G(^DD(63.04,DATANAME,0)),"^",2)'="S"   ; Quit if NOT Set of Codes
 ;
 S Q2=$P(^DD(63.04,DATANAME,0),U,3)
 S CHANGED=0
 F LRPIECE=1:1 S LRSET=$P(Q2,";",LRPIECE)  Q:LRSET'[":"!(CHANGED)  D
 . Q:$P(LRSET,":")'=BLRRES     ; Quit if NOT code
 . ;
 . S BLRRES=$P(LRSET,":",2)
 . S CHANGED=1
 ;
 Q
 ; ----- END IHS/OIT/MKK - LR*5.2*1027
