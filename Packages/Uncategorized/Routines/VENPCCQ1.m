VENPCCQ1 ; IHS/OIT/GIS - KNOWLEDGEBASE UTILITIES FOR ASQ GUI DATA CAPTURE ; 30 Dec 2009  5:43 PM
 ;;2.6;PCC+;**1,3**;MAR 23, 2011
 ;
 ;
 ;
VISIT(OUT,IN) ; EP - RPC: VEN ASQ GET VISITS
 ; GIVEN DFN, RETURN PATIENT IDENTIFIERS AND CANDIDATE VISIT IDENTIFIERS
 I '$D(^DPT(+$G(IN),0)) Q
 N NAME,DOB,MOM,X,Y,%,AUPNPAT,AUPNSEX,AUPNDOB,AUPNDAYS,AUPNDOD,B,GA,DFN,TS,VSTG,CSIEN,CS
 S OUT="",B="|",DFN=+IN
 S DIC="^AUPNPAT(",X="`"_DFN,DIC(0)="M"
 D PLK^VENPCCQ I OUT="" Q
 S IDT=9999999-(DT\1),BDT=9999999-$$FMADD^XLFDT(DT,-4)
 S IDT=IDT-.0000001,VSTG=""
 F  S IDT=$O(^AUPNVSIT("AA",DFN,IDT)) Q:'IDT  Q:IDT>BDT  S VIEN=0 F  S VIEN=$O(^AUPNVSIT("AA",DFN,IDT,VIEN)) Q:'VIEN  D
 . S X=$G(^AUPNVSIT(VIEN,0)) I '$L(X) Q
 . S TS=$$FMTE^XLFDT(+X) I '$L(TS) Q
 . S CSIEN=$P(X,U,8) I 'CSIEN Q
 . S CS=$P($G(^DIC(40.7,CSIEN,0)),U) I '$L(CS) Q
 . S VSTG=VSTG_"\"_VIEN_B_TS_B_CS
 . Q
 I $L($G(VSTG)) S OUT=OUT_VSTG
 Q
 ;
TX(OUT,IN) ; EP - RPC: VEN ASQ START TX
 ; GIVEN DFN|VIEN POPULATE THE TX FILE AND RETURN THE TABLE GEN STRING
 S OUT="|"
 N B,DFN,VIEN,M,MOS1,MOS2,ASQAGE,CAGE,ASQ1,ASQ2,DATE1,DATE2,DOB,ADOB,GA,F1,FC1,F2,FC2,G1,GC1,G2,GC2,C1,CC1,C2,CC2
 N S1,SC1,S2,SC2,P1,PC1,P2,PC2,DIC,DIE,DA,DR,LSTG,PCE,V,C,TODAY,LAST,DIK,QIEN,FMDT,TABLE,EDOB,FLD
 N AUPNPAT,AUPNSEX,AUPNDOB,AUPNDAYS,AUPNDOD,MULT,LAST,TVIEN,TODAY,LOCKED,START,FIN,VDT,X,Y,Z,%
 S B="|",LAST="",TABLE=3
 S DFN=+$G(IN) I '$D(^DPT(DFN,0)) Q
 S TODAY=$P(IN,B,4)
CLEANUP S DIK="^VEN(7.15,",DA=0
 F  S DA=$O(^VEN(7.15,"B",DFN,DA)) Q:'DA  D ^DIK ; DELETE ALL ASQ TX FILE ENTRIES FOR THIS PATIENT
 S TVIEN=+$P(IN,B,2) ; TODAYS VISIT
 S VDT=+$G(^AUPNVSIT($G(TVIEN),0))\1 I 'VDT S VDT=DT
 S %=$O(^AUTTMSR("C",66,0)),LOCKED=""
 I TVIEN,% S Z=999999999 F  S Z=$O(^AUPNVMSR("AD",TVIEN,Z),-1) Q:'Z  I +$G(^AUPNVMSR(Z,0))=% S LOCKED=1 Q  ; LOCKED = 1 (NO NEW ENTRY ROW IN THE TABLE)
 S MULT=$P(IN,B,3)
POP ; POPULATE THE TX FILE
 S DOB=$P($G(^DPT(DFN,0)),U,3)
 S EDOB=$$FMTE^XLFDT(DOB,"2D") ; PRINTED DOB
 S MOS2=$$DOBAGE^VENPCCQ(DFN) ; CHRONOLICAL AGE IN MONTHS
 S ADOB=$$ASQDOB^VENPCCQ(DFN) ; ADJUSTED (ASQ) DOB
 I ADOB S ADOB=$$FMTE^XLFDT(ADOB,"2D")
 S GA=$P($G(^AUPNVMSR(DFN,0)),U,6) ; GESTATIONAL AGE FROM BIRTH MEASUREMENT FILE
 ; I 'MULT G LAST ; SHOW ONLY ONE PAST ROW + TODAY
 N LSTG0,LSTG1
 D MLSTG(DFN,.LSTG0,.LSTG1) ; GET ALL THE PREVIOUS ASQ RESULTS. IF THERE ARE ALREADY RESULTS ENTERED TODAY
 I $L(LSTG0),$L(LSTG1)
 E  G TODAY:TVIEN,TGS
 D MPOP(DFN,LSTG0,LSTG1,.LAST) ; POPULATE THE TX TABLE WITH PREVIOUS RESULTS
 I 'LAST G TODAY:TVIEN,TGS
ENDPOP ; EP - POPULATE
 I 'TVIEN G TGS
 I $G(LOCKED) G TGS
TODAY ; GET TODAY'S VALUES
 S M=$$ASQAGE^VENPCCQ(DFN) I 'M Q  ; ADJUSTED (ASQ) AGE IN MOS
 S QIEN=+$$ASQIEN^VENPCCQ(M) I 'QIEN Q  ; QUESTIONNAIRE IEN
 S %=$G(^VEN(7.14,QIEN,0)) I '$L(%) Q
 S ASQ2=$P(%,U,1) ; QUESTIONNAIRE (MONTH)
 S FC2=$P(%,U,4) ; FINE MOTOR CUTOFF
 S GC2=$P(%,U,3) ; GROSS MOTOR CUTOFF
 S CC2=$P(%,U,2) ; COMMUNICATION CUTOFF
 S SC2=$P(%,U,6) ; PERSONAL-SOCIAL CUTOFF
 S PC2=$P(%,U,5) ; PROBLEM SOLVING CUTOFF
 S DATE2=$$FMTE^XLFDT(VDT,"2D")
 S DIC="^VEN(7.15,",DIC(0)="L",DLAYGO=19707.15,X=""""_DFN_""""
 D ^DIC I Y=-1 G TGS
 S TODAY=+Y ; IEN IN THE ASQ TX FILE FOR TODAY'S NEW ENTRY
 S ^VEN(7.15,+Y,0)=DFN_U_DATE2_U_ASQ2_U_MOS2_U_EDOB_U_ADOB_U_GA_U_TVIEN
 S ^VEN(7.15,+Y,1)=U_FC2_U_"*"_U_GC2_U_"*"_U_CC2_U_"*"_U_SC2_U_"*"_U_PC2_U_"*"
TGS ; TABLE GEN STRING
 D ^XBFMK
 I '$G(LAST),'$G(TODAY) S OUT=0 Q
 S TABLE=3
 I '$G(LAST),$G(TODAY) S TABLE=2
 I $G(LAST),'$G(TODAY) S TABLE=1
 S TABLE=TABLE+1000
 S START=$O(^VEN(7.15,"B",DFN,0))
 S FIN=$O(^VEN(7.15,"B",DFN,999999999999),-1)
 S OUT="BMX ADO SS^VEN ASQ TX^^~"_START_"~"_FIN_"~"_TABLE ; GET TABLE GEN STRING FOR THIS ASQ MEASUREMENT
 Q
 ;
LSTG(DFN) ; EP - GET LAST ASQ VALUES
 ; DEAD CODE
 N LSTG,IDT,STOP,WCIEN,STG,PCE,V,C,X,Y,Z,%
 N F1,FC1,G1,GC1,C1,CC1,S1,SC1,P1,PS1,ASQIEN
 S LSTG=""
 S IDT=0,STOP=0
 F  S IDT=$O(^AUPNVWC("AA",DFN,IDT)) Q:'IDT  D  I STOP Q
 . S WCIEN=0
 . F  S WCIEN=$O(^AUPNVWC("AA",DFN,IDT,WCIEN)) Q:'WCIEN  D  I STOP Q
 .. S STG=$G(^AUPNVWC(WCIEN,2)) I '$P(STG,U,7) Q
 .. I STG?1."^" Q
 .. S VIEN=$P($G(^AUPNVWC(WCIEN,0)),U,3) I 'VIEN Q
 .. S FMDT=+^AUPNVSIT(VIEN,0)\1
 .. S DATE1=$$FMTE^XLFDT(FMDT,2) ; DATE OF LAST ASQ
 .. S ASQIEN=$P(STG,U,7) I 'ASQIEN Q  ; MUST HAVE A VALID ASQ IEN
 .. S ASQ1=+$G(^VEN(7.14,ASQIEN,0)) I 'ASQ1 Q  ; MUST HAVE A VALID QUESTIONNAIRE MONTH
 .. S MOS1=$$LASTAGE(DFN,FMDT)  ; GET AGE IN MOS AT LAST VISIT
 .. S STOP=1
 .. F PCE=1:1:5 D
 ... S %=$E("FGCSP",PCE)
 ... S V=%_1,C=%_"C1"
 ... S X=$P(STG,U,PCE),Y=+X,Z=+$P(X,"(",2)
 ... I $P(X," ")'="" S @V=Y
 ... I Z S @C=Z
 ... Q
 .. Q
 . Q
 S LSTG=$G(F1)_U_$G(FC1)_U_$G(G1)_U_$G(GC1)_U_$G(C1)_U_$G(CC1)_U_$G(S1)_U_$G(SC1)_U_$G(P1)_U_$G(PC1)
 Q LSTG
 ;
LASTAGE(DFN,DT) ; EP - GET LAST AGE IN MONTHS
 Q $$DOBAGE^VENPCCQ(DFN)
 ;
FLUSH(OUT,IN) ; EP - RPC: VEN ASQ FLUSH
 ; FLUSH ASQ DATA OUT OF TX FILE INTO V FILES
 N IEN,DIC,DIE,DA,DR,X,Y,Z,%,VIEN,VDT,XDT,TIEN,DFN,ASQ,F,FC,G,GC,C,CC,S,SC,P,PC,ASQIEN,NOW,PRVIEN
 N DATE,FMDT,%DT,PCE,STG,V1,V2,GBL,VSTG,VAL,ENT,PRVIEN,NOW
 S IEN(1)=+$P($G(IN),"~",2),IEN(2)=+$P($G(IN),"~",3),TIEN="",PRVIEN=+$P($G(IN),"~",5) ; ,NOW=$E($$NOW^XLFDT,1,12)
 S DFN=+$G(^VEN(7.15,IEN(2),0)) I 'DFN Q
 S VIEN=$P(^VEN(7.15,IEN(2),0),U,8) I 'VIEN Q
 S VDT=+$G(^AUPNVSIT(VIEN,0))\1 I 'VDT Q
 S DA=999999999,Z=0
 F  S DA=$O(^VEN(7.15,"B",DFN,DA),-1) Q:'DA  D
 . S ENT=$G(^VEN(7.15,DA,0)) I ENT="" Q
 . S X=$P(ENT,U,2) I X="" Q  ; EXTERNAL DATE
 . D ^%DT I 'Y Q
 . I Y>Z S Z=Y,TIEN=DA
 . Q
 I 'TIEN Q  ; NO CURRENT TRANSACTION FOUND, SO QUIT
FL1 S X=$G(^VEN(7.15,TIEN,0))
 S DFN=+X,ASQ=$P(X,U,3),DATE=$P(X,U,2),VIEN=$P(X,U,8)
 S ASQIEN=$O(^VEN(7.14,"B",+$G(ASQ),0))
 I 'ASQIEN D ASQCLEAN(VIEN) S OUT="OK" Q  ; ASQ FORM NOT DEFINED, SO DELETE ALL OF THE VISIT'S ASQ RESULTS AND QUIT
 ; S X=DATE D ^%DT S FMDT=Y
 S STG=$G(^VEN(7.15,TIEN,1)) I '$L(STG) Q
 F PCE=1:1:5 D
 . S %=$E("FGCSP",PCE)
 . S V1=%,V2=%_"C"
 . I PCE=1 S X=$P(STG,U,11),Y=$P(STG,U,2)
 . E  S Z=(PCE*2)-1,X=$P(STG,U,Z),Y=$P(STG,U,Z+1)
 . S Y=$TR(Y,"(",""),Y=$TR(Y,")","")
 . S @V1=X,@V2=Y
 . Q
 S VSTG=F_" ("_FC_")^"_G_" ("_GC_")^"_C_" ("_CC_")^"_S_" ("_SC_")^"_P_" ("_PC_")^^"_ASQIEN
 I $G(NOWC) G MEAS
WC ; V WELL CHILD ENTRY
 S DA=$$VWC(DFN,VIEN) I 'DA G MEAS ; GET V WELL CHILD RECORD NUMBER
 S GBL=$NA(^AUPNVWC(DA,2))
 S @GBL=VSTG
MEAS ; V MEASUREMENTS ENTRY
 I $P(VSTG,U,7) F PCE=7,1:1:5 S VAL=$P(VSTG,U,PCE) S:VAL="" VAL="@" D ASQVMSR(PCE,VAL,VIEN,VDT,PRVIEN) ; MAKE INDIVIDUAL V MEAS ENTRIES
DEL S DIK="^VEN(7.15,",DA=""
 F  S DA=$O(^VEN(7.15,"B",DFN,DA)) Q:'DA  I $D(^VEN(7.15,DA)) D ^DIK ; CLEAN UP THE TX FILE
 S OUT="OK"
 D ^XBFMK
 Q
 ;
ASQCLEAN(VIEN) ; IF THERE IS NO ASQ MONTH VALUE, CLEAN OUT ALL ASQ RESULTS FOR THIS VISIT.
 I VIEN
 E  Q
 N DIC,DIE,DA,DR,DIK,X,Y,Z,%,MVIEN,WREF
 S DA=0,WREF="^AUPNVWC",DIK="^AUPNVMSR("
 F  S DA=$O(^AUPNVWC("AD",VIEN,DA)) Q:'DA  K @WREF@(DA,2) ; CLEAN OUT THE ASQ NODE (2) IN THE V WELL CHILD FILE
 F  S DA=$O(^AUPNVMSR("AD",VIEN,DA)) Q:'DA  D
 . S X=+$G(^AUPNVMSR(DA,0)) I 'X Q
 . S Y=$P($G(^AUTTMSR(X,0)),U) I $E(Y,1,3)'="ASQ" Q
 . D ^DIK
 . Q
 D ^XBFMK
 Q
 ; 
EHRFLUSH(OUT,IN) ; EP  - VEN ASQ EHR FLUSH
 ; GIVEN THE PATIENT IEN, FLUSH TODAY'S ASQ RESULTS TO THE V MEASUREMENT FILE
 S OUT=""
 I $G(IN),$D(^AUPNVSIT(+IN,0))
 E  Q
 N DFN,VSTG,TIEN,X,Y,Z,%,VIEN,NOWC,ASQ,DATE,ASQIEN,STG,PCE,V1,V2,VSTG,VAL,DIK,DIC,DIE,DA,DR,VDT
 S VIEN=+IN
 S VDT=+$G(^AUPNVSIT(VIEN,0))\1 I 'VDT Q
 S DFN=$P($G(^AUPNVSIT(VIEN,0)),U,5) I 'DFN Q
 S TIEN=99999999
 F  S TIEN=$O(^VEN(7.15,"B",DFN,TIEN),-1) Q:'TIEN  I $P($G(^VEN(7.15,TIEN,0)),U,8)=VIEN Q  ; FIND THE ROW ASSOCIATED WITH TODAY'S VISIT
 I TIEN S NOWC=1 D FL1 S DA=TIEN,DIK="^VEN(7.15," D ^DIK,^XBFMK
 Q
 ; 
VWC(DFN,VIEN) ; EP - RETURN THE V WELL CHILD IEN - CREATE A NEW ONE IF NECESSARY
 I '$G(DFN)!('$G(VIEN)) Q ""
 N DIC,DIE,DR,DA,X,Y,VDT,GBL
 S VDT=+$G(^AUPNVSIT(VIEN,0))\1 I 'VDT Q ""
 S DA=$O(^AUPNVWC("AD",VIEN,999999999),-1) I DA Q DA ; A RECORD HAS ALREADY BEEN CREATED - GET LATEST V WC RECORD
 S DIC="^AUPNVWC(",DIC(0)="L",DLAYGO=9000010.46,X=""""_0_""""
 D ^DIC
 I Y=-1 Q ""
 S GBL="^AUPNVWC"
 S DA=+Y,DIE=DIC,DR=".02////^S X=DFN;.03////^S X=VIEN"
 L +^AUPNVWC(DA):1 I  D ^DIE L -^AUPNVWC(DA)
 S @GBL@("AC",DFN,DA)="",@GBL@("AD",VIEN,DA)="",@GBL@("AA",DFN,9999999-VDT,DA)=""
 Q DA
 ;
ASQVMSR(PCE,VAL,VIEN,VDT,PRVIEN) ; EP - FILE ASQ SCORES IN V MEASUREMNTS
 N DIE,DIC,DA,DR,X,Y
 S DIE="^AUPNVMSR("
 S X=$P("ASQF^ASQG^ASQL^ASQS^ASQP^^ASQM",U,PCE) I X="" Q
 S DA=$$VMSR(VIEN,X) I 'DA Q  ; FIND EXISTING V MEASUREMENT OR MAKE A NEW ONE
 I PCE=7 S VAL=$P($G(^VEN(7.14,+$G(VAL),0)),U) I 'VAL Q
 S DR=".04////^S X=VAL;1201////^S X=VDT;1204////^S X=PRVIEN"
 I $P($G(^AUPNVMSR(DA,0)),U,2)="" S DR=".02////^S X=DFN;.03////^S X=VIEN;"_DR
SETMSR L +^AUPNVMSR(DA):1 I  D ^DIE L -^AUPNVMSR(DA)
 Q
 ; 
VMSR(VIEN,TYPE) ; EP - FIND OR CREATE A V MEASUREMENT ENTRY
 N MIEN,VMIEN,DIC,X,Y
 S MIEN=$O(^AUTTMSR("B",TYPE,0)) I 'MIEN Q "" ; GET THE MEASUREMENT IEN
 S VMIEN=0
 F  S VMIEN=$O(^AUPNVMSR("AD",VIEN,VMIEN)) Q:'VMIEN  I +$G(^AUPNVMSR(VMIEN,0))=MIEN Q
 I VMIEN Q VMIEN ; A V MEAS ENTRY ALREADY EXISTS FOR THIS ASQ CATEGORY AND VISIT
 S DIC="^AUPNVMSR(",DIC(0)="L",DLAYGO=9000010.01
 S X=""""_TYPE_""""
 D ^DIC I Y=-1 Q "" ; MAKE A NEW V MEAS ENTRY
 Q +Y
 ; 
MLSTG(DFN,LSTGX,LSTG1) ; EP - GET LAST ASQ VALUES
 S LSTGX="",LSTG1=""
 N IDT,STOP,STG,PCE,V,C,X,Y,Z,%,ASQIEN,VIEN
 N ASQA,ASQF,ASQG,ASQL,ASQM,ASQP,ASQS,I,STG,TYPE,RIEN,VMIEN,MSR
 N F,FC,G,GC,L,LC,S,SC,P,PC,VAL,MIEN,ASQ1,MOS1,DATE1,FMDT
 S STG="AFGLMPS"
 F I=1:1:$L(STG) S X=$E(STG,I) S Y=$O(^AUTTMSR("B",("ASQ"_X),0)) Q:'Y  S @("ASQ"_X)=Y
 I '$G(ASQS) Q
 S IDT=0,STOP=0
 F  S IDT=$O(^AUPNVMSR("AA",DFN,ASQM,IDT)) Q:'IDT  D  I STOP Q  ; GET ALL ASQ MEASUREMENTS ON THIS DATE
 . S MIEN=$O(^AUPNVMSR("AA",DFN,ASQM,IDT,99999999999),-1) I 'MIEN Q  ; FIRST DEFINE THE BASELINE DATA FROM THE FORM ENTRY
 . S ASQ1=+$P($G(^AUPNVMSR(MIEN,0)),U,4) I '$O(^VEN(7.14,"B",ASQ1,0)) Q  ; GET/VALIDATE QUESTIONNAIRE MONTH
 . S VIEN=$P($G(^AUPNVMSR(MIEN,0)),U,3) I 'VIEN Q
 . S FMDT=+^AUPNVSIT(VIEN,0)\1 I 'FMDT Q
 . ; S %=$P($G(^AUPNVMSR(MIEN,12)),U) I % S FMDT=%\1 ; EVENT DATE REPLACES VISIT DATE (IF AVAILABLE)
 . S DATE1=$$FMTE^XLFDT(FMDT,2) ; DATE OF LAST ASQ
 . S MOS1=$$LASTAGE(DFN,FMDT)  ; GET AGE IN MOS AT LAST VISIT
 . F RIEN=ASQA,ASQF,ASQG,ASQL,ASQP,ASQS D  ; NOW GET ALL THE ASQ MEASUREMENTS FOR THAT VISIT
 .. S MSR=$E($G(^AUTTMSR(RIEN,0)),4) I MSR="" Q
 .. S VMIEN=$O(^AUPNVMSR("AA",DFN,RIEN,IDT,99999999999),-1) I 'VMIEN Q  ; V MEASUREMENT IEN
 .. S VAL=$P($G(^AUPNVMSR(VMIEN,0)),U,4)
 .. S @MSR=$P(VAL," "),@(MSR_"C")=$P(VAL," ",2)
 .. Q
 . I $L(LSTGX) S LSTGX=LSTGX_"|"
 . S LSTGX=LSTGX_DFN_U_$G(DATE1)_U_ASQ1_U_$G(MOS1)_U_$G(EDOB)_U_$G(ADOB)_U_$G(GA)_U_VIEN
 . I $L(LSTG1) S LSTG1=LSTG1_"|"
 . S LSTG1=LSTG1_$G(F)_U_$G(FC)_U_$G(G)_U_$G(GC)_U_$G(L)_U_$G(LC)_U_$G(S)_U_$G(SC)_U_$G(P)_U_$G(PC)
 . Q
 Q
 ;
MPOP(DFN,LSTG0,LSTG1,LAST) ; GIVEN A PATIENT IEN AND ASQ RESULTS STRING, POPULATE THE ASQ TRANSACTION TABLE RETURNING THE STARTING IENS AKA LAST
 S START="",LAST=""
 I $G(DFN),$G(LSTG0)'="",$G(LSTG1)'=""
 E  Q
 N X,Y,Z,DIC,DLAYGO,STG,PCE,STG1,STG0,MAX
 S DIC="^VEN(7.15,",DIC(0)="LO",DLAYGO=19707.15,MAX=$L(LSTG1,"|")
 F PCE=1:1:MAX D
 . S STG0=$P(LSTG0,"|",PCE) I STG0="" Q
 . S STG1=$P(LSTG1,"|",PCE) I STG1="" Q
 . S X=""""_DFN_""""
 . D ^DIC I Y=-1 Q
 . I 'START S START=+Y
 . S ^VEN(7.15,+Y,0)=STG0
 . S $P(STG1,U,11)=$P(STG1,U,1),$P(STG1,U,1)=""
 . S ^VEN(7.15,+Y,1)=STG1
 . I PCE=1 S LAST=+Y
 . Q
 Q
 ; 
