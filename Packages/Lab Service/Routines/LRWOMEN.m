LRWOMEN ;DALOI/CYM -  LINK TO WOMEN'S HEALTH PROGRAM ;2-19-99
 ;;5.2T9;LR;**1018**;Nov 17, 2004
 ;;5.2;LAB SERVICE;**231,248**;Sep 27, 1994
 ;
 ;Reference to CREATE^WVLRLINK supported by IA #2772
 ;Reference to DELETE^WVLRLINK supported by IA #2772
 ;Reference to MOVE^WVLRLINK supported by IA #2772
 ;
ADD ; From DD 63.08,.11 and 63.09,.11
 Q:$G(SEX)'["F"
 Q:+$G(LRDPF)'=2
 Q:'$D(LRSS)
 Q:$P(^LR(LRDFN,LRSS,LRI,0),U,11)']""
 Q:$T(CREATE^WVLRLINK)']""
 D CREATE^WVLRLINK(DFN,LRDFN,LRI,$G(LRA),LRSS)
 Q
DEL ; From LRAPM
 Q:$G(SEX)'["F"
 Q:+$G(LRDPF)'=2
 Q:'$D(LRSS)
 Q:$P(^LR(LRDFN,LRSS,LRI,0),U,11)]""
 Q:$T(DELETE^WVLRLINK)']""
 D DELETE^WVLRLINK(DFN,LRDFN,LRI,X,LRSS)
 Q
MOVE ; From LRAPMV
 N LRNODE,LROLD,OLDDFN,LRNEW
 S LRNODE=^LR(LRO,0)
 Q:$P(LRNODE,U,2)'=2
 S OLDDFN=$P(LRNODE,U,3)
 S LROLD=^LR(LRO,LRSS,LRI,0)
 I $P(LROLD,U,11)']"" Q  ; Do not send notification if not verified
 Q:$T(MOVE^WVLRLINK)']""
 D MOVE^WVLRLINK(OLDDFN,LRO,LRI,LROLD,LRSS) ; Old data moved
 S LRNEW=^LR(LRDFN,0)
 I $P(LRNEW,U,2)'=2 Q
 D CREATE^WVLRLINK(DFN,LRDFN,LRI,LROLD,LRSS) ; New data created
 Q
