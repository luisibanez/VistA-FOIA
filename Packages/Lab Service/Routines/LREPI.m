LREPI ;DALLAS/SED - EMERGING PATHOGENS SEARCH ; 5/1/98
 ;;5.2T9;LR;**1018**;Nov 17, 2004
 ;;5.2;LAB SERVICE;**132,175**;Sep 27, 1994
 ;
TEST S LRRPS=2980201,LRRPE=2980228,LRRTYPE=1
 S D0=0 F  S D0=$O(^LAB(69.5,D0)) Q:+D0'>0  D
 .Q:$P(^LAB(69.5,D0,0),U,2)="1"
 .Q:$P(^LAB(69.5,D0,0),U,7)=""
 .Q:'$D(^ORD(101,$P(^LAB(69.5,D0,0),U,7),0))
 .S LREPI(D0)=""
 S LRBEG=9999999-(LRRPE+.9),LREND=9999999-LRRPS+.999999
EN ;
 ;
INIT ;Set up search criteria
 S LRBEG=9999999-(LRRPE+.9),LREND=9999999-LRRPS+.999999
 K ^TMP($J),^TMP("HLS",$J)
 S D0=0 F  S D0=$O(LREPI(D0)) Q:+D0'>0  D
 .S ^TMP($J,$P(^LAB(69.5,D0,0),U,7))=""
 .S:$P(^LAB(69.5,D0,0),U,8)=1 ^TMP($J,"LREPI",D0)=""
 .S LRPROT=$P(^LAB(69.5,D0,0),U,7)
 .S D1=0 F  S D1=$O(^LAB(69.5,D0,1,D1)) Q:+D1'>0  D
 ..S TST=$P(^LAB(69.5,D0,1,D1,0),U)
 ..Q:'$D(^LAB(60,TST,0))
 ..Q:$P(^LAB(60,TST,0),U,4)=""
 ..I $P(^LAB(60,TST,0),U,4)="CH" D
 ...Q:$P(^LAB(60,TST,0),U,5)=""
 ...S ^TMP($J,"T",TST,D0)=""
 ...S ^TMP($J,"TPROT",TST,LRPROT)=""
 ...S LRIND=$P(^LAB(69.5,D0,1,D1,0),U,2,3)
 ...S ^TMP($J,$P(^LAB(60,TST,0),U,4),TST)=$P(^LAB(60,TST,0),U,5)_U_LRIND
 ..I $P(^LAB(60,TST,0),U,4)="CY" D
 ...S ^TMP($J,"T",TST,D0)=""
 ...S ^TMP($J,$P(^LAB(60,TST,0),U,4),TST)=""
 .S D1=0 F  S D1=$O(^LAB(69.5,D0,2,D1)) Q:+D1'>0  S ^TMP($J,"E",$P(^LAB(69.5,D0,2,D1,0),U),D0)=""
 .S D1=0 F  S D1=$O(^LAB(69.5,D0,9,D1)) Q:+D1'>0  S ^TMP($J,"SNO",$P(^LAB(69.5,D0,9,D1,0),U),D0)=""
 .S D1=0 F  S D1=$O(^LAB(69.5,D0,3,D1)) Q:+D1'>0  S ^TMP($J,"ICD",$P(^LAB(69.5,D0,3,D1,0),U),D0)=""
 K D0,D1,TST,LRIND
 I $D(^TMP($J,"LREPI")) D SEARCH^LREPI4
 I $D(^TMP($J,"ICD")) D PTF
LAB63 ;Search file 63 for lab data
 K LRIND
 S LRDFN=0 F  S LRDFN=$O(^LR(LRDFN)) Q:+LRDFN'>0  D
 .Q:'$D(^LR(LRDFN,0))
 .Q:$P(^LR(LRDFN,0),U,2)'=2
 .S LRPAT=$P(^LR(LRDFN,0),U,3)
 .I $D(^TMP($J,"CH")) D CH
 .I $D(^TMP($J,"CY")) D CYTST^LREPICY
 .I $D(^TMP($J,"E")) D MI
 .;I '$D(^TMP($J,"ICD"))&($D(^TMP($J,"SNO"))) D CY^LREPICY
 .I $D(^TMP($J,"SNO")) D CY^LREPICY
 D ^LREPI2
EXIT ;EXIT
 S D0=0
 I $G(LRRTYPE)=0 F  S D0=$O(LREPI(D0)) Q:+D0'>0  D
 .S $P(^LAB(69.5,D0,0),U,4)=DT
 K LREPI,DFN,CNT,DA,DIE,DR,DQ,HL,ENTRY,ENDT,ENC,FD,HLECH,HLFS,HLN,HLQ
 K DDER,D0,HLRST,HLSAN,LRBEG,LRCNT,LRCS,LRDATE,LRDFN,LREFG,LRENCDT
 K LREND,LRETND,LRHL7,LRINV,LRINVD,LRITN,LRND,LRNL,LRNLT,LRNTE,LROBR
 K LRPAT,LRPFG,LRPID,LRPROT,LRPV1,LRRPE,LRRPS,LRRTYPE,LRTND,LRTNM,MSG
 K MSGCNT,PTF,RR,SEG,SP,STDT,TST,UN,TSTNM,VAERR,X,XCNP,XMDUZ,XMZ,ZTSK
 K AF,D,DI,LRENT,LRIND,LRPATH,OV,LRENDT
 Q
PTF ;SEARCH DISCHARGE DATES; NEED ADDITIONAL LATER SPECS
 S STDT=(LRRPS-.0001),ENDT=(LRRPE+.9999)
 F  S STDT=$O(^DGPT("ADS",STDT)) Q:+STDT'>0!(STDT>ENDT)  D
 .S IFN=0 F  S IFN=$O(^DGPT("ADS",STDT,IFN)) Q:+IFN'>0  D
 ..Q:$P($G(^DGPT(IFN,0)),U,6)'=3
 ..I $P($G(^DGPT(IFN,300)),U,3)=1 D
 ...S ICD9=$O(^ICD9("BA","482.80 ",0)) D ICD9
 ..I $D(^DGPT(IFN,70)) F LRI=10,11,16:1:24 D
 ...S ICD9=$P(^DGPT(IFN,70),U,LRI) D ICD9
 ..;SEARCH SUB FIELDS
 ..S LRMV=0 F  S LRMV=$O(^DGPT(IFN,"M",LRMV)) Q:+LRMV'>0  D
 ...I $P($G(^DGPT(IFN,"M",LRMV,300)),U,3)=1 D
 ....S ICD9=$O(^ICD9("BA","482.80 ",0)) D ICD9
 ...I $D(^DGPT(IFN,"M",LRMV,0)) F LRI=5:1:9,11:1:15 D
 ....S ICD9=$P(^DGPT(IFN,"M",LRMV,0),U,LRI) D ICD9
 K IFN,LRMV,ICD9,LRI
 Q
ICD9 ;CHECK ICD9 CODE AND SAVE
 Q:+ICD9'>0
 Q:'$D(^TMP($J,"ICD",+ICD9))
 S LRPROT=$G(LRPROT,999999) S ^TMP($J,"ICDPROT",+ICD9,LRPROT)=""
 S DFN=$P(^DGPT(IFN,0),U,1),ADMDT=$P(^DGPT(IFN,0),U,2)
 S LRPATH=0 F  S LRPATH=$O(^TMP($J,"ICD",+ICD9,LRPATH)) Q:+LRPATH'>0  D SET
 Q
SET ;SET THE TMP GLOBAL
 S LRPROT=$P(^LAB(69.5,LRPATH,0),U,7)
 S LRCHK=0 D ADDCHK Q:LRCHK
 S:'$D(^TMP($J,LRPROT,DFN,ADMDT)) ^TMP($J,LRPROT,DFN,ADMDT)="I"_U_IFN
 S ^TMP($J,LRPROT,DFN,ADMDT,LRPATH,(9999999-ADMDT),"PTF")=IFN
 Q
ENCT ;SET THE ENCOUNTER FOR PV1
 S LRPROT=$P(^LAB(69.5,LRPATH,0),U,7)
 S LRCHK=0 D ADDCHK Q:LRCHK
 S LRDATE=9999999-LRINV
 K VAIN,DFN,VAINDT S DFN=LRPAT,VAINDT=LRDATE D INP^VADPT
 S LRENCDT=$S(VAIN(7)'="":$P(VAIN(7),U),1:LRDATE)
 I $P(^LAB(69.5,LRPATH,0),U,8)=1 D CHECK^LREPI4
 S:'$D(^TMP($J,LRPROT,LRPAT,LRENCDT)) ^TMP($J,LRPROT,LRPAT,LRENCDT)=$S(VAIN(7)'="":"I",1:"O")_U_$G(VAIN(10))
 S:'$D(^TMP($J,LRPROT,LRPAT,LRENCDT,LRPATH,LRINV,ND)) ^TMP($J,LRPROT,LRPAT,LRENCDT,LRPATH,LRINV,ND)=""
 Q
ADDCHK ;DO ADDITIONAL CHECKS HERE FOR AGE AND SEX SCREENING.
 ;
 I '$G(DFN) S DFN=$G(LRPAT)
 K VADM
 I $G(DFN) D DEM^VADPT
 ;
 I $P(^LAB(69.5,LRPATH,0),U,10)'="" D
 .S LRSEX=$P(^LAB(69.5,LRPATH,0),U,10)
 .I LRSEX="O"&$P(VADM(5),U,1)="M" S LRCHK=1 Q
 .I LRSEX="O"&$P(VADM(5),U,1)="F" S LRCHK=1 Q
 .I LRSEX'=$P(VADM(5),U,1) S LRCHK=1
 I $P(^LAB(69.5,LRPATH,0),U,11)'=""!$P(^LAB(69.5,LRPATH,0),U,12)'="" D
 .S LRBEF=$P(^LAB(69.5,LRPATH,0),U,11),LRAFT=$P(^LAB(69.5,LRPATH,0),U,12)
 .I LRBEF'=""&($P(VADM(3),U,1)>LRBEF) S LRCHK=1
 .I LRAFT'=""&($P(VADM(3),U,1)<LRAFT) S LRCHK=1
 K LRBEF,LRSEX,LRAFT,VADM
 Q
CH ;Check the 'CH' node
 S LRINV=LRBEG
 F  S LRINV=$O(^LR(LRDFN,"CH",LRINV)) Q:+LRINV'>0!(LRINV>LREND)  D
 .Q:$P(^LR(LRDFN,"CH",LRINV,0),U,3)=""
 .S LRCNT=1,LRTST=0 F  S LRTST=$O(^TMP($J,"CH",LRTST)) Q:+LRTST'>0  D
 ..S LRND=$P($P(^TMP($J,"CH",LRTST),";",2),U,1) Q:+LRND'>0
 ..S LRPC=$P($P(^TMP($J,"CH",LRTST),";",3),U,1) Q:+LRPC'>0
 ..S LRRES=$P($G(^LR(LRDFN,"CH",LRINV,LRND)),U,LRPC) Q:LRRES=""
 ..S ^TMP($J,"TST",LRTST)=+$G(^TMP($J,"TST",LRTST))+1
 ..S ^TMP($J,"TST",LRTST,LRDFN)=""
 ..S LRPATH=0 F  S LRPATH=$O(^TMP($J,"T",LRTST,LRPATH)) Q:+LRPATH'>0  D CHKIND
 K LRTST,LRND,LRPC,LRRES,LRNO
 Q
CHKIND ;Check the results
 I '$D(^LAB(69.5,LRPATH,1,"B",LRTST)) Q
 S LRITST=0,ND="CH",LRNO=0
 F  S LRITST=$O(^LAB(69.5,LRPATH,1,"B",LRTST,LRITST)) Q:+LRITST'>0  D  D:'LRNO ENCT
 .S LRNO=0
 .S LRIND=$P(^LAB(69.5,LRPATH,1,LRITST,0),U,2,3)
 .Q:$P(LRIND,U,1)=""
 .I $P(LRIND,U,1)=1 D  Q
 ..Q:'LRRES#2
 ..S LRSPEC=$P($G(^LR(LRDFN,"CH",LRINV,0)),U,5) Q:LRSPEC=""
 ..Q:'$D(^LAB(60,LRTST,1,LRSPEC,0))
 ..S LRLOW=$P(^LAB(60,LRTST,1,LRSPEC,0),U,2),LRHIG=$P(^(0),U,3)
 ..Q:'LRLOW#2!('LRHIG#2)
 ..I LRRES<LRLOW!(LRRES>LRHIG) Q
 ..S LRNO=1
 .I $P(LRIND,U,2)="" Q
 .S LRRES=$$UP^XLFSTR(LRRES),LRIND=$$UP^XLFSTR(LRIND)
 .I $P(LRIND,U,1)=2,(LRRES[$P(LRIND,U,2)) Q
 .I $P(LRIND,U,1)=3,(LRRES>$P(LRIND,U,2)) Q
 .I $P(LRIND,U,1)=4,(LRRES<$P(LRIND,U,2)) Q
 .I $P(LRIND,U,1)=5,(LRRES=$P(LRIND,U,2)) Q
 .S LRNO=1
 K LRITST,LRRES,LRLOW,LRHIG,LRSPEC
 Q
MI ;Check the 'MI' node
 S LRINV=LRBEG
 F  S LRINV=$O(^LR(LRDFN,"MI",LRINV)) Q:+LRINV'>0!(LRINV>LREND)  D
 .S LRCNT=1
 .F LRMIND=3,6,9,12,17 S LRETND=0 F  S LRETND=$O(^LR(LRDFN,"MI",LRINV,LRMIND,LRETND)) Q:+LRETND'>0  D
 ..I LRMIND=3,$P($G(^LR(LRDFN,"MI",LRINV,1)),U,2)'="F" Q
 ..I LRMIND'=3,$P($G(^LR(LRDFN,"MI",LRINV,(LRMIND-1))),U,2)'="F" Q
 ..S LRETI=$P($G(^LR(LRDFN,"MI",LRINV,LRMIND,LRETND,0)),U)
 ..Q:+LRETI'>0
 ..Q:'$D(^TMP($J,"E",LRETI))
 ..S ^TMP($J,"EPROT",LRETI)=""
 ..S ^TMP($J,"ETI",LRETI)=+$G(^TMP($J,"ETI",LRETI))+1
 ..S ^TMP($J,"ETI",LRETI,LRDFN)=""
 ..S LRPATH=0 F  S LRPATH=$O(^TMP($J,"E",LRETI,LRPATH)) Q:+LRPATH'>0  D
 ...S ND="MI"
 ...D TOP Q:LRTOP
 ...I LRMIND=3 D ANTI Q
 ...D ENCT
 K LRMIND,LRETI
 Q
TOP ;CHECK TO SEE IF SCREEN ON SITE
 S LRTOP=0
 S LRSITE=$P($G(^LR(LRDFN,"MI",LRINV,0)),U,5) Q:+LRSITE'>0
 I ($O(^LAB(69.5,LRPATH,5,0))="")&($O(^LAB(69.5,LRPATH,6,0))="") Q
 I ($O(^LAB(69.5,LRPATH,5,0))'="")&($O(^LAB(69.5,LRPATH,6,0))'="") Q
 I ($O(^LAB(69.5,LRPATH,5,0))'="")&($D(^LAB(69.5,LRPATH,5,"B",LRSITE))) Q
 I ($O(^LAB(69.5,LRPATH,6,0))'="")&('$D(^LAB(69.5,LRPATH,6,"B",LRSITE))) Q
 S LRTOP=1
 Q
ANTI ;LOOK FOR THE ANTIMICROBIAL SUS FOR ORGANISMS
 I $O(^LAB(69.5,LRPATH,4,0))="" D ENCT Q
 S LRANTI=0 F  S LRANTI=$O(^LAB(69.5,LRPATH,4,LRANTI)) Q:+LRANTI'>0  D
 .S LRANT=$G(^LAB(69.5,LRPATH,4,LRANTI,0)) Q:+LRANT'>0
 .S LRAND=$P($G(^LAB(62.06,LRANT,0)),U,2) Q:LRAND=""
 .Q:'$D(^LR(LRDFN,"MI",LRINV,LRMIND,LRETND,LRAND))
 .Q:$P(^LR(LRDFN,"MI",LRINV,LRMIND,LRETND,LRAND),U,2)=""
 .Q:$$UP^XLFSTR($E($P($G(^LR(LRDFN,"MI",LRINV,LRMIND,LRETND,LRAND)),U,2),1,1))="S"
 .D ENCT
 Q
 ;
