LREPI3 ;DALLAS/SED - EMERGING PATHOGENS HL7 SEGMENTS ; 5/21/98
 ;;5.2T9;LR;**1018**;Nov 17, 2004
 ;;5.2;LAB SERVICE;**132,175**;Sep 27, 1994
 ;
NTE ;TO BUILD THE NTE SEGMENT TO DEFINE THE EPI
 S LRCNT=LRCNT+1,^TMP("HLS",$J,LRCNT)="NTE"_HLFS_LRNTE_HLFS_$P(^LAB(69.5,LRPATH,0),U,9)_LRCS_$P(^LAB(69.5,LRPATH,0),U,1)
 S LRMSGSZ=LRMSGSZ+$L(^TMP("HLS",$J,LRCNT))
 S LRNTE=LRNTE+1
 Q
DG1 ;BUILD THE DG1 FOR ICD9 CODES
 K ^TMP($J,"DG1")
 S IFN=+$G(^TMP($J,LRPROT,DFN,LRENDT,LRPATH,LRINVD,LRND))
DG11 Q:+IFN'>0
 Q:'$D(^DGPT(IFN))
 ;SEARCH FOR LEGIONAIRS HERE
 I $P($G(^DGPT(IFN,300)),U,3)=1 D
 .S ICD9=$O(^ICD9("BA","482.80 ",0)) Q:+ICD9'>0
 .S ^TMP($J,"DG1",ICD9)=""
 I $D(^DGPT(IFN,70)) F LRI=10,11,16:1:24 D
 .S ICD9=$P(^DGPT(IFN,70),U,LRI) Q:+ICD9'>0
 .S ^TMP($J,"DG1",ICD9)=""
 ;SEARCH SUB FIELDS
 S LRMV=0 F  S LRMV=$O(^DGPT(IFN,"M",LRMV)) Q:+LRMV'>0  D
 .;SEARCH FOR LEGIONAIRS HERE IN SUB FILE
 .I $P($G(^DGPT(IFN,"M",LRMV,300)),U,3)=1 D
 ..S ICD9=$O(^ICD9("BA","482.80 ",0)) Q:+ICD9'>0
 ..S ^TMP($J,"DG1",ICD9)=""
 .I $D(^DGPT(IFN,"M",LRMV,0)) F LRI=5:1:9,11:1:15 D
 ..S ICD9=$P(^DGPT(IFN,"M",LRMV,0),U,LRI) Q:+ICD9'>0
 ..S ^TMP($J,"DG1",ICD9)=""
 Q:'$D(^TMP($J,"DG1"))
BLD S ICD9=0 F  S ICD9=$O(^TMP($J,"DG1",ICD9)) Q:+ICD9'>0  D
 .S:'$D(DGCNT) DGCNT=1
 .K LRDATA
 .S LRDATA="DG1"_HLFS_DGCNT_HLFS_HLFS_$P(^ICD9(ICD9,0),U,1)
 .S LRDATA=LRDATA_LRCS_$P(^ICD9(ICD9,0),U,3)_LRCS_"I9"
 .S ^TMP("HL7",$J,DGCNT)=LRDATA,DGCNT=DGCNT+1
 K ^TMP($J,"DG1"),LRDATA,DGCNT,ICD9,LRMV
 Q
PID ;TO BUILD PID SEGMENT
 K MSG
 S FLDS="1,2,3,5,7,8,10,19" S MSG=$$EN^VAFHLPID(DFN,FLDS,LRPID)
 ;ADDITIONAL DATA ADDED HERE HOMELESSNES
 S:$$HOMELESS^SOWKHIRM(DFN) $P(MSG,HLFS,12)="HOMELESS"
 ;NOW GET PERIOD OF SERVICE
 K VAEL D ELIG^VADPT
 S:$G(VAEL(2))'="" $P(MSG,HLFS,28)=$P($G(^DIC(21,+VAEL(2),0)),U,3)
 K VAEL
 ;GET ZIP IF THERE
 K VAPA D ADD^VADPT S $P(MSG,HLFS,12)=$P(MSG,HLFS,12)_LRCS_$G(VAPA(6))
 K VAPA
 S LRPID=LRPID+1,LRCNT=LRCNT+1,^TMP("HLS",$J,LRCNT)=MSG
 S LRMSGSZ=LRMSGSZ+$L(MSG)
 K FLDS,VAEL
 Q
PV1 ;TO BUILD PV1 SEGMENT
 K PTF,Y,C,LRDATA
 S $P(LRDATA,HLFS,1)=LRPV1
 S $P(LRDATA,HLFS,2)=$P(^TMP($J,LRPROT,DFN,LRENDT),U)
 S:$P(^TMP($J,LRPROT,DFN,LRENDT),U,3)="UPDT" $P(LRDATA,HLFS,2)="U"
 S $P(LRDATA,HLFS,44)=$$HLDATE^HLFNC(LRENDT)
 S PTF=$P(^TMP($J,LRPROT,DFN,LRENDT),U,2) I +PTF>0 D
 .Q:'$D(^DGPT(PTF,0))
 .Q:$P(^DGPT(PTF,0),U,6)'=3
 .Q:'$D(^DGPT(PTF,70))
 .S:+$P(^DGPT(PTF,70),U)>0 $P(LRDATA,HLFS,45)=$$HLDATE^HLFNC($P(^DGPT(PTF,70),U))
 .S (Y,LRDTY)=$P(^DGPT(PTF,70),U,3)
 .Q:+Y'>0
 .S C=$P(^DD(45,72,0),U,2) D Y^DIQ
 .S $P(LRDATA,HLFS,36)=LRDTY_LRCS_Y_LRCS_"VA45"
 S LRCNT=LRCNT+1,^TMP("HLS",$J,LRCNT)="PV1"_HLFS_LRDATA,LRPV1=LRPV1+1
 S LRMSGSZ=LRMSGSZ+$L(LRDATA)
 Q:+$G(PTF)'>0
 Q:'$D(^DGPT(PTF,0))
 Q:$P(^DGPT(PTF,0),U,6)'=3
 S IFN=PTF D DG11
 D MOVE^LREPI2
 K PTF,Y,C,LRDATA,LRDTY,Y,IFN
 Q
 ;
