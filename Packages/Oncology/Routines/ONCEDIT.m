ONCEDIT ;Hines OIFO/GWB - ONCOLOGY INTERFIELD EDITS ;09/29/00
 ;;2.11;ONCOLOGY;**27,28,34,36,39,42,43,45**;Mar 07, 1995
 ;
 K MSG
 S RHSP=$$GET1^DIQ(165.5,PRM,.03,"I")  ;REPORTING HOSPITAL
 S COCI=$$GET1^DIQ(165.5,PRM,.04,"I")  ;CLASS OF CASE
 S COCE=$$GET1^DIQ(165.5,PRM,.04,"E")
 S STAT=$$GET1^DIQ(165.5,PRM,.091)     ;STATUS
 S TRSI=$$GET1^DIQ(165.5,PRM,1.2,"I")  ;TYPE OF REPORTING SOURCE
 S TRSE=$$GET1^DIQ(165.5,PRM,1.2,"E")
 S RFAC=$$GET1^DIQ(165.5,PRM,6,"I")    ;FACILITY REFERRED FROM
 S TFAC=$$GET1^DIQ(165.5,PRM,7,"I")    ;FACILITY REFERRED TO
 S TOPI=$$GET1^DIQ(165.5,PRM,20,"I")   ;PRIMARY SITE
 S TOPE=$$GET1^DIQ(165.5,PRM,20,"E")
 S HSTI=$$HIST^ONCFUNC(PRM)            ;HISTOLOGY
 S HSTE=$$GET1^DIQ(165.5,PRM,HSTFLD,"E")
 S BCOD=$E(HSTI,5)                     ;BEHAVIOR CODE
 S SSTI=$$GET1^DIQ(165.5,PRM,35,"I")   ;SUMMARY STAGE
 S SSTE=$$GET1^DIQ(165.5,PRM,35,"E")
 ;
IF02 S SDX=""
 S SDXI=$$GET1^DIQ(165.5,PRM,16,"I")   ;STATE AT DX
 S SDXE=$$GET1^DIQ(165.5,PRM,16,"E")
 I SDXI'="" S SDX=$P(^ONCO(160.15,SDXI,0),U,1)
 S PCDX=$$GET1^DIQ(165.5,PRM,9,"I")    ;POSTAL CODE AT DX
 I SDX="YY",PCDX'=888888888 D  D ERRMSG
 .S MSG(1)="STATE AT DX = YY ("_SDXE_")"
 .S MSG(2)="POSTAL CODE AT DX must be 888888888"
 I SDX="ZZ",PCDX'=999999999 D  D ERRMSG
 .S MSG(1)="STATE AT DX = ZZ ("_SDXE_")"
 .S MSG(2)="POSTAL CODE AT DX must be 999999999"
 K MSG,PCDX,SDX,SDXI,SDXE
 ;
IF03 I RHSP=RFAC D  D ERRMSG
 .S MSG(1)="REPORTING HOSPITAL = FACILITY REFERRED FROM"
 I RHSP=TFAC D  D ERRMSG
 .S MSG(1)="REPORTING HOSPITAL = FACILITY REFERRED TO"
 ;
IF10 I ((COCI=2)!(COCI=3)),RFAC="" D  D ERRMSG
 .S MSG(1)="CLASS OF CASE = "_COCI_" ("_COCE_")"
 .S MSG(2)="FACILITY REFERRED FROM may not be blank"
 K MSG,RHSP,RFAC,TFAC
 ;
IF06 S DIAI=$$GET1^DIQ(165.5,PRM,155,"I")  ;DATE OF FIRST CONTACT
 S DIAE=$$GET1^DIQ(165.5,PRM,155,"E")
 S DDXI=$$GET1^DIQ(165.5,PRM,3,"I")    ;DATE DX
 S DDXE=$$GET1^DIQ(165.5,PRM,3,"E")
 S MSG(1)="CLASS OF CASE = "_COCI_" ("_COCE_")"
 S MSG(2)="DATE OF FIRST CONTACT..: "_DIAE
 S MSG(3)=" later than"
 I COCI=0 D
 .S SPSI=$$GET1^DIQ(165.5,PRM,50,"I")  ;MOST DEFINITIVE SURG DATE
 .S SPSE=$$GET1^DIQ(165.5,PRM,50,"E")
 .S RADI=$$GET1^DIQ(165.5,PRM,51,"I")  ;RADIATION DATE
 .S RADE=$$GET1^DIQ(165.5,PRM,51,"E")
 .S CNSI=$$GET1^DIQ(165.5,PRM,52,"I")  ;RADIATION THERAPY TO CNS DATE
 .S CNSE=$$GET1^DIQ(165.5,PRM,52,"E")
 .S CHMI=$$GET1^DIQ(165.5,PRM,53,"I")  ;CHEMOTHERAPY DATE
 .S CHME=$$GET1^DIQ(165.5,PRM,53,"E")
 .S HORI=$$GET1^DIQ(165.5,PRM,54,"I")  ;HORMONE THERAPY DATE
 .S HORE=$$GET1^DIQ(165.5,PRM,54,"E")
 .S BRMI=$$GET1^DIQ(165.5,PRM,55,"I")  ;IMMUNOTHERAPY DATE
 .S BRME=$$GET1^DIQ(165.5,PRM,55,"E")
 .S OTHI=$$GET1^DIQ(165.5,PRM,57,"I")  ;OTHER TREATMENT DATE
 .S OTHE=$$GET1^DIQ(165.5,PRM,57,"E")
 .I SPSI'="0000000",DIAI>SPSI D  D ERRMSG
 ..S MSG(4)="MOST DEFINITIVE SURG DATE....: "_SPSE
 .I RADI'="0000000",DIAI>RADI D  D ERRMSG
 ..S MSG(4)="RADIATION DATE...............: "_RADE
 .I DDXI<2960000,CNSI'="0000000",DIAI>CNSI D  D ERRMSG
 ..S MSG(4)="RADIATION THERAPY TO CNS DATE: "_CNSE
 .I CHMI'="0000000",DIAI>CHMI D  D ERRMSG
 ..S MSG(4)="CHEMOTHERAPY DATE............: "_CHME
 .I HORI'="0000000",DIAI>HORI D  D ERRMSG
 ..S MSG(4)="HORMONE THERAPY DATE.........: "_HORE
 .I BRMI'="0000000",DIAI>BRMI D  D ERRMSG
 ..S MSG(4)="IMMUNOTHERAPY DATE...........: "_BRME
 .I OTHI'="0000000",DIAI>OTHI D  D ERRMSG
 ..S MSG(4)="OTHER TREATMENT DATE.........: "_OTHE
 K MSG,DIAI,DIAE
 K SPSI,SPSE,RADI,RADE,CNSI,CNSE,CHMI,CHME,HORI,HORE,BRMI,BRME,OTHI,OTHE
 ;
IF0708 S DXCF=$$GET1^DIQ(165.5,PRM,26,"I")   ;DIAGNOSTIC CONFIRMATION
 S SEQ=$$GET1^DIQ(165.5,PRM,.06,"I")   ;SEQUENCE NUMBER
 S SEQN=+$$GET1^DIQ(165.5,PRM,.06,"I")
 S PRIM=$$GET1^DIQ(165.5,PRM,20,"I")   ;PRIMARY SITE
 S OVER=$$GET1^DIQ(165.5,PRM,223,"I")  ;OVERRIDE HOSPSEQ/DXCONF
 I TRSI=6,COCI'=5 D  D ERRMSG
 .S MSG(1)="TYPE OF REPORTING SOURCE = 6 ("_TRSE_")"
 .S MSG(2)="CLASS OF CASE must be 5 (Dx at autopsy)"
 I COCI=5,TRSI'=6 D  D ERRMSG
 .S MSG(1)="CLASS OF CASE = 5 ("_COCE_")"
 .S MSG(2)="TYPE OF REPORTING SOURCE must be 6 (Autopsy only)"
 I TRSI=7,DXCF'=9 D  D ERRMSG
 .S MSG(1)="TYPE OF REPORTING SOURCE = 7 ("_TRSE_")"
 .S MSG(2)="DIAGNOSTIC CONFIRMATION must be 9 (Unk if microscopically confirmed)"
 I TRSI=6,((DXCF'=1)&(DXCF'=6)) D  D ERRMSG
 .S MSG(1)="TYPE OF REPORTING SOURCE = 6 ("_TRSE_")"
 .S MSG(2)="DIAGNOSTIC CONFIRMATION must be 1 (Pos histology) or"
 .S MSG(3)="                                6 (Direct visualization)"
 I ($E(PRIM,3,4)=76)!(PRIM=67809) G IF11
 I DXCF>5,((SEQN>0)&(SEQN<60))!(SEQN>88),OVER="" D  D ERRMSG
 .S MSG(1)="DIAGNOSTIC CONFIRMATION > 5 ("_DXCF_")"
 .S MSG(2)="SEQUENCE NUMBER > 00 ("_SEQ_")"
 .S MSG(3)="OVERRIDE HOSPSEQ/DXCONF is required"
 K MSG,DXCF,SEQN,PRIM,OVER
 ;
IF11 G:TOPI="" IF11EX
 S PORG=$$GET1^DIQ(164,TOPI,.07,"I")   ;PAIRED ORGAN
 S LTRL=$$GET1^DIQ(165.5,PRM,28,"I")   ;LATERALITY
 I PORG=1,LTRL=0,DDXI>3031231 D  D ERRMSG
 .S MSG(1)=TOPE_" is a paired site"
 .S MSG(2)="LATERALITY must be provided for specified paired organs/sites"
 .S MSG(3)=""
 .S:TOPI=67300 MSG(4)="NOTE: If NASAL CARTILAGE or NASAL SEPTUM, override this warning."
 .S:TOPI=67340 MSG(4)="NOTE: If CARINA, override this warning."
 .S:TOPI=67413 MSG(4)="NOTE: If STERNUM, override this warning."
 .S:TOPI=67414 MSG(4)="NOTE: If SACRUM, COCCYX or SYMPHYSIS PUBIS, override this warning."
 I PORG=1,LTRL="" D  D ERRMSG
 .S MSG(1)=TOPE_" is a paired site"
 .S MSG(2)="LATERALITY must be provided for specified paired organs/sites"
 I PORG="",LTRL'=0 D  D ERRMSG
 .S MSG(1)=TOPE_" is an unpaired site"
 .S MSG(2)="LATERALITY must be 0 (Not a paired site)"
IF11EX K MSG,PORG,LTRL
 ;
 S RADI=$$GET1^DIQ(165.5,PRM,51.2,"I")   ;RADIATION
 S RADE=$$GET1^DIQ(165.5,PRM,51.2,"E")
 S LRTI=$$GET1^DIQ(165.5,PRM,126,"I")    ;LOCATION OF RADIATION TX
 I RADI'=0,LRTI=0 D  D ERRMSG
 .S MSG(1)="RADIATION = "_RADI_" ("_RADE_")"
 .S MSG(2)="LOCATION OF RADIATION TX cannot be 0 (No radiation tx)"
 K MSG,RADI,RADE,LRTI
 ;
 G ^ONCEDIT2
 ;
ERRMSG ;Error message
 S CMPLT=0
 W !," WARNING: "
 S MSGSUB=0 F  S MSGSUB=$O(MSG(MSGSUB)) Q:MSGSUB'>0  W ?10,MSG(MSGSUB),!
 R Z:10
 Q
