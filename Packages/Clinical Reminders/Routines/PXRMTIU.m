PXRMTIU ;SLC/RMS,PKR - Clinical Reminder TIU routines. ; 06/16/2004
 ;;2.0;CLINICAL REMINDERS;;Feb 04, 2005
 ;==========================================================
NOTE(DFN,NGET,BDT,EDT,NFOUND,TEST,DATE,DATA,TEXT) ;Computed finding
 ;for note title. TIU access supported by DBIA #2937.
 S NFOUND=0
 Q:(TEST="")!(NGET=0)
 N AUTH,EDTT,INVDATE,NGETABS,PIEN,SDIR,STATUS,TIEN,TITLE
 S TITLE=$P(TEST,U),STATUS=$P(TEST,U,2)
 I STATUS="" S STATUS=7  ;COMPLETED IS THE DEFAULT
 S EDTT=$S(EDT[".":EDT+.0000001,1:EDT+.240001)
 ;Invert and switch beginning and ending dates because the TIU index
 ;uses inverse dates.
 S INVDATE=BDT,BDT=9999999-EDTT,EDTT=9999999-INVDATE
 S SDIR=$S(NGET>0:1,1:-1)
 S INVDATE=$S(SDIR=+1:BDT-.000001,1:EDTT)
 S NGETABS=$S(NGET<0:-NGET,1:NGET)
 S TIEN=$O(^TIU(8925.1,"B",TITLE,0)) Q:'+TIEN
 F  S INVDATE=$O(^TIU(8925,"APT",DFN,TIEN,STATUS,INVDATE),SDIR)  Q:$S(INVDATE=0:1,NFOUND=NGETABS:1,INVDATE<BDT:1,INVDATE>EDTT:1,1:0)  D
 . S PIEN=$O(^TIU(8925,"APT",DFN,TIEN,STATUS,INVDATE,0)) Q:'+PIEN
 . S NFOUND=NFOUND+1
 . S TEST(NFOUND)=1
 . S DATE(NFOUND)=$P(^TIU(8925,PIEN,13),U)
 . S DATA(NFOUND,"VALUE")=TITLE
 . S AUTH=+$P($G(^TIU(8925,PIEN,12)),U,2)
 . S AUTH=$S(AUTH>0:$$GET1^DIQ(200,AUTH,.01),1:"MISSING")
 . S DATA(NFOUND,"AUTH")=AUTH
 . S TEXT(NFOUND)="Author: "_AUTH
 Q
 ;
