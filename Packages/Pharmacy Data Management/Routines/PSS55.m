PSS55 ;BHM/DB - API FOR PHARMACY PATIENT FILE ; 15 JUN 05
 ;;1.0;PHARMACY DATA MANAGEMENT;**101**;9/30/97
PSS431(DFN,PO,PSDATE,PEDATE,LIST) ;
  ;DFN: IEN of Patient [REQUIRED]
         ;PO: Order # [optional]
         ;PSDATE: Start Date [optional]
         ;PEDATE: End Date [optional]
         ;If a start date is sent, an end date must also be sent
         ;LIST: Subscript name used in ^TMP global [REQUIRED]
         N PSSPO,PSSIEN,DA,DR,DIC
 I $G(LIST)="" Q
 K ^TMP($J,LIST)
 I $G(DFN)="" S ^TMP($J,LIST,0)=-1_"^"_"NO DATA FOUND" Q
 S PSSIEN=$G(DFN),PSSPO=$G(PO) S ^TMP($J,LIST,0)=0
 I $G(PSSPO)>0,$G(PSSIEN)>0 S DA=PSSIEN,(IEN,DA(55.06))=PSSPO G DIQ431
 I $G(PSSPO)="",$G(PSDATE)'="",$G(PEDATE)'="" S PSDATE=$S('$P(PSDATE,".",2):PSDATE_.000001,1:PSDATE),PEDATE=$S('$P($G(PEDATE),".",2):PEDATE_.999999,1:$G(PEDATE)) N PSS56 G DT431
 I $G(PSSPO)="" N PSSPO1 G LOOP431
 Q
LOOP431 S (PSSPO1,PSSPO)=0 F  S PSSPO1=$O(^PS(55,DFN,5,"B",PSSPO1)) Q:PSSPO1'>0  F  S PSSPO=$O(^PS(55,DFN,5,"B",PSSPO1,PSSPO)) Q:PSSPO'>0  S PO=PSSPO D DIQ431
 Q
DIQ431 ;
 I '$D(^PS(55,DFN,5,PO,0)) Q
 N PSS,CNT1,X
 S PSSIEN=PO_","_DFN_"," K DIQ
 D GETS^DIQ(55.06,PSSIEN,".01;.5;1;2*;3;4;5;6;7;11;12;27;27.1;28","IE","^TMP(""PSS5506"",$J)")
 F X=.01,.5,1,3,4,5,6,7,11,12,27,27.1,28 S ^TMP($J,LIST,+PSSPO,X)=$G(^TMP("PSS5506",$J,55.06,PSSIEN,X,"I"))
 F X=.5,1,3,4,5,6,7,27,27.1,28 S ^TMP($J,LIST,+PSSPO,X)=$S($G(^TMP("PSS5506",$J,55.06,PSSIEN,X,"E"))'="":^TMP($J,LIST,+PSSPO,X)_"^"_$G(^TMP("PSS5506",$J,55.06,PSSIEN,X,"E")),1:"")
 S (PSS(1),CNT1)=0 F  S PSS(1)=$O(^TMP("PSS5506",$J,55.07,PSS(1))) Q:'PSS(1)  D
 .S ^TMP($J,LIST,+PSSPO,"DDRUG",+PSS(1),.11)=$G(^TMP("PSS5506",$J,55.07,PSS(1),.11,"I"))
 .S ^TMP($J,LIST,+PSSPO,"DDRUG",+PSS(1),.12)=$G(^TMP("PSS5506",$J,55.07,PSS(1),.12,"I"))
 .S ^TMP($J,LIST,+PSSPO,"DDRUG",+PSS(1),.01)=$S($G(^TMP("PSS5506",$J,55.07,PSS(1),.01,"E"))'="":$G(^TMP("PSS5506",$J,55.07,PSS(1),.01,"I"))_"^"_$G(^TMP("PSS5506",$J,55.07,PSS(1),.01,"E")),1:"")
 .S ^TMP($J,LIST,+PSSPO,"DDRUG",+PSS(1),.02)=$G(^TMP("PSS5506",$J,55.07,PSS(1),.02,"I"))
 .S ^TMP($J,LIST,+PSSPO,"DDRUG",+PSS(1),.03)=$S($G(^TMP("PSS5506",$J,55.07,PSS(1),.03,"E"))'="":$G(^TMP("PSS5506",$J,55.07,PSS(1),.03,"I"))_"^"_$G(^TMP("PSS5506",$J,55.07,PSS(1),.03,"E")),1:"")
 .S ^TMP($J,LIST,+PSSPO,"DDRUG",+PSS(1),.04)=$G(^TMP("PSS5506",$J,55.07,PSS(1),.04,"I"))
 .S ^TMP($J,LIST,+PSSPO,"DDRUG",+PSS(1),.05)=$G(^TMP("PSS5506",$J,55.07,PSS(1),.05,"I"))
 .S ^TMP($J,LIST,+PSSPO,"DDRUG",+PSS(1),.06)=$G(^TMP("PSS5506",$J,55.07,PSS(1),.06,"I"))
 .S ^TMP($J,LIST,+PSSPO,"DDRUG",+PSS(1),.07)=$G(^TMP("PSS5506",$J,55.07,PSS(1),.07,"I"))
 .S ^TMP($J,LIST,+PSSPO,"DDRUG",+PSS(1),.08)=$G(^TMP("PSS5506",$J,55.07,PSS(1),.08,"I"))
 .S ^TMP($J,LIST,+PSSPO,"DDRUG",+PSS(1),.09)=$G(^TMP("PSS5506",$J,55.07,PSS(1),.09,"I"))
 .S ^TMP($J,LIST,+PSSPO,"DDRUG",+PSS(1),.1)=$G(^TMP("PSS5506",$J,55.07,PSS(1),.1,"I"))
 .S CNT1=CNT1+1
 K ^TMP("PSS5506",$J),PSS(1) S ^TMP($J,LIST,"B",+PSSPO)=""
 S ^TMP($J,LIST,0)=^TMP($J,LIST,0)+1
 S ^TMP($J,LIST,+PSSPO,"DDRUG",0)=$S(CNT1=0:"-1^NO DATA FOUND",1:CNT1)
 S ^TMP($J,LIST,0)=$S(^TMP($J,LIST,0)=0:"-1^NO DATA FOUND",1:^TMP($J,LIST,0))
 Q
DT431 F  S PSDATE=$O(^PS(55,DFN,5,"AUS",PSDATE)) Q:((+$G(PEDATE)>0)&(PSDATE>$G(PEDATE)))  Q:PSDATE'>0  D
 .S PSS56=0 F  S PSS56=$O(^PS(55,DFN,5,"AUS",PSDATE,PSS56)) Q:PSS56'>0  S (PSSPO,PO)=PSS56 D DIQ431
 S ^TMP($J,LIST,0)=$S(^TMP($J,LIST,0)=0:"-1^NO DATA FOUND",1:^TMP($J,LIST,0))
 Q
PSS432(DFN,PO,LIST) ;SRS 3.2.43.2
 ;DFN: IEN of Patient [REQUIRED]
 ;PO: Order # [optional] If left blank, all active orders will be returned
 ;LIST: Subscript name used in ^TMP global [REQUIRED]
 Q:$G(LIST)=""  K ^TMP($J,LIST)
 I +$G(DFN)'>0 S ^TMP($J,LIST,0)="-1^NO DATA FOUND" Q
 S ^TMP($J,LIST,0)=0
 I '$D(^PS(55,DFN,0)) S ^TMP($J,LIST,0)="-1^NO DATA FOUND" Q
 I $G(PO)'="" G AUS2
 S PSSDT=0
AUS ;Loop through stop date/time xref
 F  S PSSDT=$O(^PS(55,DFN,5,"AUS",PSSDT)) Q:PSSDT'>0  S PSSIEN=0 D
 .F  S PSSIEN=$O(^PS(55,DFN,5,"AUS",PSSDT,PSSIEN)) Q:PSSIEN'>0  D
 ..S (IEN,DA(55.06))=PSSIEN,PSSDATA=$G(^PS(55,DFN,5,PSSIEN,0)) I $P(PSSDATA,"^",9)'="A" Q
 ..S PO=PSSIEN D AUSDIQ
 S ^TMP($J,LIST,0)=$S(^TMP($J,LIST,0)=0:"-1^NO DATA FOUND",1:^TMP($J,LIST,0))
 Q
AUSDIQ K ^UTILITY("DIQ1",$J),DIQ S PSSPO=PO
 S DA=DFN,DIC=55,DR=62,DR(55.06)=".01;.5;1;3;4;5;6;7;11;12;27;27.1;28",DIQ(0)="IE" D EN^DIQ1
 F X=.01,.5,1,3,4,5,6,7,11,12,27,27.1,28 S ^TMP($J,LIST,+PSSPO,X)=$G(^UTILITY("DIQ1",$J,55.06,IEN,X,"I"))
 F X=.5,1,3,4,5,6,7,27,27.1,28 S ^TMP($J,LIST,+PSSPO,X)=$S($G(^UTILITY("DIQ1",$J,55.06,IEN,X,"E"))'="":^TMP($J,LIST,PSSPO,X)_"^"_$G(^UTILITY("DIQ1",$J,55.06,IEN,X,"E")),1:"")
 K ^UTILITY("DIQ1",$J)
 S ^TMP($J,LIST,"B",+PSSPO)="",^TMP($J,LIST,0)=$G(^TMP($J,LIST,0))+1
 Q
AUS2 ;one PO
 S (PSSIEN,IEN,DA(55.06))=PO,PSSQ=1 D AUSDIQ
 S ^TMP($J,LIST,0)=$S(^TMP($J,LIST,0)=0:"-1^NO DATA FOUND",1:^TMP($J,LIST,0))
 I ^TMP($J,LIST,+PO,.01)="" K ^TMP($J,LIST) S ^TMP($J,LIST,0)="-1^NO DATA FOUND"
AUSQ K PSSDT,PSSIEN,PSSDATA,PSSPO,PSSQ,DA,DR,DIC Q
 ;
PSS433(DFN,LIST) ;SRS 3.2.43.3 - Passed all tests 8/22
 ;DFN: IEN of Patient [REQUIRED]
 ;LIST: Subscript name used in ^TMP global [REQUIRED]
 Q:$G(LIST)=""  K ^TMP($J,LIST)
 I $G(DFN)'>0 S ^TMP($J,LIST,0)="-1^NO DATA FOUND" Q
 I '$D(^PS(55,DFN)) G NODATA
 S PSSIEN=0,^TMP($J,LIST,0)=0
BGN433 S PSSIEN=$O(^PS(55,DFN,5,PSSIEN)) G Q433:PSSIEN'>0 S PSSPO=$P($G(^PS(55,DFN,5,PSSIEN,0)),"^")
 S (IEN,DA(55.06))=PSSIEN,DIC=55,DA=DFN,DR=62,DR(55.06)=".5;9;25;26;34;41;42;70",DIQ(0)="IE" D EN^DIQ1
 F X=.5,9,25,26,34,41,42,70 S ^TMP($J,LIST,+PSSIEN,X)=$G(^UTILITY("DIQ1",$J,55.06,IEN,X,"I"))
 F X=.5,9,25,34,70 S ^TMP($J,LIST,+PSSIEN,X)=$S($G(^UTILITY("DIQ1",$J,55.06,IEN,X,"E"))'="":^TMP($J,LIST,+PSSIEN,X)_"^"_$G(^UTILITY("DIQ1",$J,55.06,IEN,X,"E")),1:"")
 S ^TMP($J,LIST,0)=$G(^TMP($J,LIST,0))+1
 S ^TMP($J,LIST,"B",+PSSIEN)=""
 G BGN433
Q433 K ^UTILITY("DIQ1",$J) Q
PSS435(DFN,PO,LIST) ;SRS 3.2.43.5
 ;DFN:  IEN of Patient [REQUIRED]
 ;PO: Order # [optional] If left blank, all active orders will be returned. 
 ;LIST: Subscript name used in ^TMP global [REQUIRED]
 ;Active hyperal orders utilizing "AIT" cross reference
 Q:$G(LIST)=""  K ^TMP($J,LIST)
 I $G(DFN)'>0  S ^TMP($J,LIST,0)="-1^NO DATA FOUND" Q
 I '$D(^PS(55,DFN,"IV","AIT","H")) S ^TMP($J,LIST,0)="-1^NO DATA FOUND" Q
 S PSSDT=0,^TMP($J,LIST,0)=0
AIT ;loop trough AIT xref
 S PSSDT=$O(^PS(55,DFN,"IV","AIT","H",PSSDT)) G AITQ:PSSDT'>0 S PSSIEN=0
AIT1 S PSSIEN=$O(^PS(55,DFN,"IV","AIT","H",PSSDT,PSSIEN)) G AIT:PSSIEN'>0
 S PSSDATA=$G(^PS(55,DFN,"IV",PSSIEN,0)),PSSSTAT=$P($G(PSSDATA),"^",17) I PSSSTAT'="A",$G(PO)'>0 G AIT1
 I +$G(PO)>0 G AIT1:PSSIEN'=PO
 S PSSPO=$P(PSSDATA,"^",1),^TMP($J,LIST,"B",+PSSIEN)=""
AITDIQ K ^UTILITY("DIQ1",$J) S DA=DFN,(IEN,DA(55.01))=PSSIEN,DIC=55,DR=100,DIQ(0)="IE",DR(55.01)=".01;.02;.03;.04;.06;.08;.09;.12;.17;.24;100;104;106;108;110;112;120;121" D EN^DIQ1
 F X=.01,.02,.03,.04,.06,.08,.09,.12,.17,.24,100,104,106,108,110,112,120,121 S ^TMP($J,LIST,PSSPO,X)=$G(^UTILITY("DIQ1",$J,55.01,IEN,X,"I"))
 F X=.02,.03,.04,.06,100,106,108,112,120,121 S ^TMP($J,LIST,PSSPO,X)=$S($G(^UTILITY("DIQ1",$J,55.01,IEN,X,"E"))'="":^TMP($J,LIST,PSSPO,X)_"^"_$G(^UTILITY("DIQ1",$J,55.01,IEN,X,"E")),1:"")
 K ^UTILITY("DIQ1",$J)
 S ^TMP($J,LIST,0)=$G(^TMP($J,LIST,0))+1
 G AIT1
AITQ I $G(^TMP($J,LIST,0))=0 K ^TMP($J,LIST) S ^TMP($J,LIST,0)="-1^NO DATA FOUND"
 Q
 ;
PSS436(DFN,ORDER,LIST) ;SRS 3.2.43.6
 ;DFN: IEN of Patient [REQUIRED]
 ;ORDER:  ORDER NUMBER [REQUIRED]
 ;LIST: Subscript name used in ^TMP global [REQUIRED]
 ;Active IV AD nodes
 K PSSLOOP Q:$G(LIST)=""  K ^TMP($J,LIST) I $G(DFN)'>0  S ^TMP($J,LIST,0)="-1^NO DATA FOUND" Q
 I '$D(^PS(55,DFN)) S ^TMP($J,LIST,0)="-1^NO DATA FOUND" Q
 K ^TMP($J,LIST) I $G(ORDER)="" S PSSLOOP=1 S ORDER=0 G LOOP436
 I $G(ORDER)'="" S PSSPO=$O(^PS(55,DFN,"IV","B",ORDER,0)) G PSS436Q:$G(PSSPO)'>0 G DIQ436
LOOP436 S ORDER=$O(^PS(55,DFN,"IV","B",ORDER)) G PSS436Q:ORDER'>0  S PSSPO=$O(^PS(55,DFN,"IV","B",ORDER,0))
DIQ436 K DA,DR S DA=DFN,(IEN,DA(55.01))=PSSPO,DIC=55,DR=100
 S DR(55.01)=".01;.02;.03;.04;.06;.08;.09;.12;.17;.24;100;104;106;108;110;112;120;121;147"
 S DIQ(0)="IE" D EN^DIQ1 I '$D(^UTILITY("DIQ1",$J)) G NODATA
 F X=.01,.02,.03,.04,.06,.08,.09,.12,.17,.24,100,104,106,108,110,112,120,121,147 S ^TMP($J,LIST,PSSPO,X)=$G(^UTILITY("DIQ1",$J,55.01,IEN,X,"I"))
 F X=.02,.03,.04,.06,100,106,108,112,120,121,147 S ^TMP($J,LIST,PSSPO,X)=$S($G(^UTILITY("DIQ1",$J,55.01,IEN,X,"E"))'="":^TMP($J,LIST,PSSPO,X)_"^"_$G(^UTILITY("DIQ1",$J,55.01,IEN,X,"E")),1:"")
 S ^TMP($J,LIST,"B",PSSPO)="",PSSADD=0,^TMP($J,LIST,0)=$G(^TMP($J,LIST,0))+1
 S ^TMP($J,LIST,PSSPO,"ADD",0)=0
PSSADD S PSSADD=$O(^PS(55,DFN,"IV",PSSPO,"AD",PSSADD)) I PSSADD'>0 S PSSSOL=0 S ^TMP($J,LIST,PSSPO,"SOL",0)=0 G PSSSOL
 S PSSDATA=$G(^PS(55,DFN,"IV",PSSPO,"AD",PSSADD,0)),X1=$P(PSSDATA,"^"),X2=$P(PSSDATA,"^",2),X3=$P(PSSDATA,"^",3)
 S ^TMP($J,LIST,PSSPO,"ADD",PSSADD,.01)=X1_"^"_$P($G(^PS(52.6,X1,0)),"^")
 S ^TMP($J,LIST,PSSPO,"ADD",PSSADD,.02)=X2
 S ^TMP($J,LIST,PSSPO,"ADD",PSSADD,.03)=X3
 S ^TMP($J,LIST,PSSPO,"ADD",0)=$G(^TMP($J,LIST,PSSPO,"ADD",0))+1
 G PSSADD
PSSSOL I ^TMP($J,LIST,PSSPO,"ADD",0)'>0 S ^TMP($J,LIST,PSSPO,"ADD",0)="-1^NO DATA FOUND"
 S PSSSOL=$O(^PS(55,DFN,"IV",PSSPO,"SOL",PSSSOL)) I PSSSOL'>0,$G(PSSLOOP)'=1 D  G PSS436Q
 .I ^TMP($J,LIST,PSSPO,"SOL",0)=0 S ^TMP($J,LIST,PSSPO,"SOL",0)="-1^NO DATA FOUND"
 I PSSSOL'>0 D  G LOOP436
 .I ^TMP($J,LIST,PSSPO,"SOL",0)=0 S ^TMP($J,LIST,PSSPO,"SOL",0)="-1^NO DATA FOUND"
 S PSSDATA=$G(^PS(55,DFN,"IV",PSSPO,"SOL",PSSSOL,0)),X1=$P(PSSDATA,"^"),X2=$P(PSSDATA,"^",2)
 S ^TMP($J,LIST,PSSPO,"SOL",PSSSOL,.01)=X1_"^"_$P($G(^PS(52.7,X1,0)),"^")
 S ^TMP($J,LIST,PSSPO,"SOL",PSSSOL,1)=X2
 S ^TMP($J,LIST,PSSPO,"SOL",0)=$G(^TMP($J,LIST,PSSPO,"SOL",0))+1
 G PSSSOL
PSS436Q K ^UTILITY("DIQ1",$J),DIQ I '$D(^TMP($J,LIST,"B")) S ^TMP($J,LIST,0)="-1^NO DATA FOUND"
 Q
NODATA S ^TMP($J,LIST,0)="-1^NO DATA FOUND"
Q Q
