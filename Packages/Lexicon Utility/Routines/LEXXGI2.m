LEXXGI2 ; ISL/KER - Global Import (Update Change File w/^LEXM)  ; 04/07/2004
 ;;2.0;LEXICON UTILITY;**25,26,28,29**;Sep 23, 1996
 ;
 ; External References
 ;   DBIA  10103  $$NOW^XLFDT
 ;   DBIA  10103  $$DT^XLFDT
 ;   DBIA   2052  $$GET1^DID
 ;   DBIA  10006  ^DIC  (file #757.903)
 ;   DBIA  10009  FILE^DICN  (file #757.903)
 ;   DBIA  10013  IX1^DIK
 ;   DBIA  10018  ^DIE
 ;
 Q
BEG ; Begin Update
 Q:'$L($G(LEXBLD))  Q:'$L($G(LEXDAT))  Q:+($G(LEXDAT))'>0
 Q:'$L($G(LEXFI))  Q:+($G(LEXFI))'>0  Q:+($G(LEXBEG))'>0
 Q:'$L($$GET1^DID(+($G(LEXFI)),.01,,"LABEL"))
 N LEXIGI,LEXIGD,LEXIGT,LEXIGL,LEXIGG,LEXIGB,LEXEXP
 S LEXIGI=$G(LEXBLD),LEXIGD=$G(LEXDAT),LEXIGT=LEXBEG,LEXEXP=+($P($G(LEXEDT),".",1))
 S LEXEXP=$S((+LEXEXP>0&($L(LEXEXP)=7)):LEXEXP,1:0) S:LEXEXP>0 LEXIGD=LEXEXP
 S LEXIGL=$$LOG(LEXIGD,LEXIGI) Q:+LEXIGL'>0  S LEXIGB=$G(LEXFB)
 S LEXIGF=LEXFI D STRT(LEXIGL,LEXIGI,LEXIGF,LEXIGT,LEXIGB)
 Q
END ; End Update
 Q:'$L($G(LEXBLD))  Q:'$L($G(LEXDAT))  Q:+($G(LEXDAT))'>0
 Q:'$L($G(LEXFI))  Q:+($G(LEXFI))'>0
 Q:'$L($$GET1^DID(+($G(LEXFI)),.01,,"LABEL"))
 Q:+($G(LEXBEG))'>0  Q:+($G(LEXEND))'>0  Q:'$L($G(LEXELP))
 N LEXIGI,LEXIGD,LEXIGT,LEXIGL,LEXIGG,LEXDA0,LEXDA1,LEXDA2,LEXEXP
 S LEXEXP=+($P($G(LEXEDT),".",1)) S LEXEXP=$S((+LEXEXP>0&($L(LEXEXP)=7)):LEXEXP,1:0)
 S LEXDA2=$O(^LEXC(757.9,"B",LEXDAT,0))
 S:+LEXDA2'>0 LEXDA2=$O(^LEXC(757.9,"C",LEXBLD,0)) Q:+LEXDA2'>0
 S LEXDA2=$O(^LEXC(757.903,"B",LEXDA2,0)) Q:+LEXDA2'>0
 S LEXDA1=$O(^LEXC(757.903,LEXDA2,1,"B",LEXFI,0)) Q:+LEXDA1'>0
 S LEXDA0=$O(^LEXC(757.903,LEXDA2,1,LEXDA1,1,"B",LEXBEG,0)) Q:+LEXDA0'>0
 D COMP(LEXDA2,LEXDA1,LEXDA0,LEXEND,LEXELP)
 Q
STRT(LEXIGL,LEXIGI,LEXIGF,LEXIGT,LEXIGB) ; Start Date/Time
 Q:+($G(LEXIGL))'>0  Q:'$D(^LEXC(757.9,+($G(LEXIGL)),0))
 Q:'$L($G(LEXIGI))  Q:+($G(LEXIGF))'>0  Q:'$L($$GET1^DID(+($G(LEXIGF)),.01,,"LABEL"))  Q:+($G(LEXIGT))'>0
 N DIE,DIC,DIK,DTOUT,DUOUT,DIROUT,DIRUT,DLAYGO,DA,DR,X,Y,LEXDA,LEXDA1,LEXDA2
 S LEXDA2=$$IG(LEXIGL,LEXIGI) Q:+LEXDA2'>0  Q:'$D(^LEXC(757.903,+LEXDA2,0))
 S LEXDA1=$$IGF(LEXDA2,LEXIGF,$G(LEXIGB)) Q:+LEXDA1'>0  Q:'$D(^LEXC(757.903,+LEXDA2,1,+LEXDA1,0))
 S DA(2)=LEXDA2,DA(1)=LEXDA1,X=LEXIGT,DIC="^LEXC(757.903,"_DA(2)_",1,"_DA(1)_",1,"
 S DLAYGO=757.90311,DIC("P")="757.90311D",DIC(0)="ML" S DIC("DR")=".01////^S X=LEXIGT"
 D ^DIC S LEXDA=+($G(Y)) S DA=LEXDA2,DIK="^LEXC(757.903," D IX1^DIK
 Q
COMP(LEXDA2,LEXDA1,LEXDA0,LEXEND,LEXELP) ; Completion Date/Time
 Q:+($G(LEXDA2))'>0  Q:'$D(^LEXC(757.903,+($G(LEXDA2)),0))
 Q:+($G(LEXDA1))'>0  Q:'$D(^LEXC(757.903,+($G(LEXDA2)),1,+($G(LEXDA1)),0))
 Q:+($G(LEXDA0))'>0  Q:'$D(^LEXC(757.903,+($G(LEXDA2)),1,+($G(LEXDA1)),1,+($G(LEXDA0)),0))
 Q:+($G(LEXEND))'>0  Q:'$L($G(LEXELP))  Q:$L($G(LEXELP))>10
 N DIE,DIC,DIK,DTOUT,DUOUT,DIROUT,DIRUT,DLAYGO,DA,DR,X,Y,LEXL
 S DA(2)=LEXDA2,DA(1)=LEXDA1,DA=LEXDA0,DIE="^LEXC(757.903,"_LEXDA2_",1,"_LEXDA1_",1,",DR=".02////^S X=$G(LEXEND);.03////^S X=$G(LEXELP)"
C2 L +^LEXC(757.903,LEXDA2):0 S LEXL=+($T)
 I LEXL D ^DIE L -^LEXC(757.903,LEXDA2) Q
 I 'LEXL G C2
 Q
IG(LEXIGL,LEXIGI) ; Import Global
 Q:+($G(LEXIGL))'>0 -1  Q:'$D(^LEXC(757.9,+($G(LEXIGL)),0)) -1  Q:'$L($G(LEXIGI)) -1
 N DIE,DIC,DTOUT,DUOUT,DIROUT,DIRUT,DLAYGO,DA,DR,LEXI1,LEXI2,LEXDA,LEXDA1,LEXDA2
 S LEXI1=$O(^LEXC(757.903,"B",LEXIGL,0)),LEXI2=$O(^LEXC(757.903,"C",LEXIGI,0))
 Q:+LEXI1>0&(LEXI1=LEXI2) LEXI1  Q:LEXI2>0&($D(^LEXC(757.903,"B",LEXIGL,+LEXI2))) LEXI2  Q:LEXI1>0&($D(^LEXC(757.903,"C",LEXIGI,+LEXI1))) LEXI1
 S DIC="^LEXC(757.903,",DLAYGO=757.903,DIC(0)="ML",X=$P($G(^LEXC(757.9,+($G(LEXIGL)),0)),"^",1) Q:'$L(X) -1
 S DIC("DR")=".01////^S X=LEXIGL;.02////^S X=LEXIGI" K DO,D0 D FILE^DICN S X=+Y
 Q X
IGF(LEXDA2,LEXIGF,LEXIGB) ; Import Global File
 Q:+($G(LEXDA2))'>0 -1  Q:'$D(^LEXC(757.903,+($G(LEXDA2)),0)) -1
 Q:+($G(LEXIGF))'>0 -1  Q:'$L($$GET1^DID(+($G(LEXIGF)),.01,,"LABEL")) -1
 N DIE,DIC,DTOUT,DUOUT,DIROUT,DIRUT,DLAYGO,X,Y,DA
 S DIC="^LEXC(757.903,"_LEXDA2_",1,",DA(1)=LEXDA2,DLAYGO=757.9031,DIC("P")="757.9031P",DIC(0)="ML",X=LEXIGF
 S DIC("DR")=".01////^S X=LEXIGF" S:$L($G(LEXIGB)) DIC("DR")=".01////^S X=LEXIGF;.02////^S X=LEXIGB"
 D ^DIC S X=+Y
 Q X
LOG(LEXDT,LEXBD) ; Get Change Log IEN
 Q:+($G(LEXDT))'>0 -1  Q:'$L($G(LEXBD)) -1
 N DIE,DIC,DTOUT,DUOUT,DIROUT,DIRUT,DLAYGO,DA,DR,X,Y
 N LEXLOG,LEXID,LEXIT,LEXIA,LEXIV,LEXIR S LEXLOG=0
 S LEXID=$G(^LEXM(0,"CHANGE DATE")) S:'$L(LEXID) LEXID=LEXDT
 S LEXIT=$$TYPE Q:+LEXIT'>0 -1  S LEXIA=$$SDO Q:+LEXIA'>0 -1
 S LEXIV=$G(^LEXM(0,"CHANGE VERSION")) S:'$L(LEXIV) LEXIV=LEXBD
 S LEXIR=$$REA Q:'$L(LEXIR) -1
 S LEXLOG=$O(^LEXC(757.9,"B",LEXID,0))
 S:+LEXLOG'>0 LEXLOG=$O(^LEXC(757.9,"C",LEXIV,0))
 I +LEXLOG>0 D  Q +LEXLOG
 . N DIE,DR,DA,DIC S (DIE,DIC)="^LEXC(757.9,",DR=""
 . S:+LEXID>0 DR=".01////^S X=LEXID" S:+LEXIT>0 DR=DR_";.02////^S X=LEXIT"
 . S:+LEXIA>0 DR=DR_";.03////^S X=LEXIA" S:$L(LEXIV) DR=DR_";.04////^S X=LEXIV"
 . S:LEXIR?1U&("^B^A^P^U^M^"[("^"_LEXIR_"^")) DR=DR_";.05////^S X=LEXIR"
 . F  Q:$E(DR,1)'=";"  S DR=$E(DR,2,$L(DR))
 . S DA=+LEXLOG D ^DIE
 S LEXTY=$$TYPE Q:+LEXTY'>0 -1  S LEXSD=$$SDO Q:+LEXSD'>0 -1
 S DIC="^LEXC(757.9,",DLAYGO=757.9,DIC(0)="ML",X=LEXID
 S DIC("DR")=".01////^S X=LEXID;.02////^S X=LEXIT;.03////^S X=LEXIA;.04////^S X=LEXIV;.05////^S X=LEXIR"
 D ^DIC S X=+($G(Y))
 Q X
TYPE(X) ; Get Change Type
 N DIE,DIC,DTOUT,DUOUT,DIROUT,DIRUT,DLAYGO,Y,LEXS,LEXI,LEXN
 S LEXN=$G(^LEXM(0,"CHANGE TYPE")) I $L(LEXN) D  Q:+X>0 X
 . K DLAYGO S DIC="^LEXC(757.901,",DIC(0)="M",X=LEXN D ^DIC S X=+($G(Y))
 S LEXN="Routine Maintenance",LEXS="Maintenance"
 S LEXI=$O(^LEXC(757.901,"C",$$UP(LEXN),0)) Q:+LEXI>0 +LEXI
 S DIC="^LEXC(757.901,",DLAYGO=757.901,DIC(0)="ML",X=LEXN
 S DIC("DR")=".01////^S X=$E(LEXN,1,30);.02////^S X=$E(LEXS,1,13)"
 D ^DIC S X=+Y
 Q X
SDO(X) ; Get Reason
 N DIE,DIC,DTOUT,DUOUT,DIROUT,DIRUT,DLAYGO,Y,LEXS,LEXI,LEXN
 S LEXN=$G(^LEXM(0,"CHANGE AGENCY")) I $L(LEXN) D  Q:+X>0 X
 . K DLAYGO S DIC="^LEXC(757.902,",DIC(0)="M",X=LEXN D ^DIC S X=+($G(Y))
 S LEXN="VistA Lexicon Utility",LEXS="Vista"
 S LEXI=$O(^LEXC(757.902,"C",$$UP(LEXN),0)) Q:+LEXI>0 +LEXI
 S DIC="^LEXC(757.902,",DLAYGO=757.902,DIC(0)="ML",X=LEXN
 S DIC("DR")=".01////^S X=$E(LEXN,1,30);.02////^S X=$E(LEXS,1,13)" D ^DIC S X=+Y
 Q X
REA(X) ; Get Standards Organization
 N DIE,DIC,DTOUT,DUOUT,DIROUT,DIRUT,DLAYGO,Y,LEXS,LEXI,LEXN
 S LEXN=$G(^LEXM(0,"CHANGE REASON")) S:'$L(LEXN) LEXN="Maintenance"
 S X=$S(LEXN="Baseline":"B",LEXN="Annual Update":"A",LEXN="Periodic Update":"P",LEXN="Unscheduled Update":"U",1:"M")
 Q X
UP(X) ; Uppercase
 Q $TR(X,"abcdefghijklmnopqrstuvwxyz","ABCDEFGHIJKLMNOPQRSTUVWXYZ")
