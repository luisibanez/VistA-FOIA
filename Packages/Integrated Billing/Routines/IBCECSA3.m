IBCECSA3 ;ALB/CXW - CLAIMS STATUS AWAITING RESOLUTION REPORT ;23-JUL-99
 ;;2.0;INTEGRATED BILLING;**137**;21-MAR-94
 Q
EN ; Report of claims status awaiting resolution
 D FULL^VALM1
 W !!,"You will need a 132 column printer for this report!",!
 ;
 N %ZIS,ZTSAVE,ZTRTN,ZTDESC
 S %ZIS="QM" D ^%ZIS Q:POP
 I $D(IO("Q")) K IO("Q") D  Q
 . S ZTRTN="LIST^IBCECSA3"
 . S ZTSAVE("IBSORT1")=""
 . S ZTSAVE("IBSORT2")=""
 . S ZTSAVE("^TMP(""IBCECSB"",$J,")=""
 . S ZTDESC="IB -Claims Status Awaiting Resolution Report" D ^%ZTLOAD K ZTSK D HOME^%ZIS
 U IO
LIST ; display
 N IBSTOP,X,IBPAGE,IBX,IBDIV,IBDA,IBPAY,IB,IBZ,IBZFT,IBFST,IBX2
 W:$E(IOST,1,2)["C-" @IOF ;Only initial form feed for print to screen
 S (IBSTOP,IBPAGE,IBFST,IBDIV)=0
 I IBSORT1="D" S IBDIV=1
 I '$D(^TMP("IBCECSB",$J)) D  G LISTQ
 . D HDR1 W !,"No entries found for this report"
 S IBX="" F  S IBX=$O(^TMP("IBCECSB",$J,IBX)) Q:IBX=""!(IBSTOP)  S IBX2="" F  S IBX2=$O(^TMP("IBCECSB",$J,IBX,IBX2)) Q:IBX2=""!(IBSTOP)  D
 . I 'IBFST S IBPAY=$$IBPAY(IBX,IBX2) D HDR1 S:'IBDIV IBFST=1 Q:IBSTOP
 . S IBDA=0 F  S IBDA=$O(^TMP("IBCECSB",$J,IBX,IBX2,IBDA)) Q:'IBDA!(IBSTOP)  S IB=$G(^TMP("IBCECSB",$J,IBX,IBX2,IBDA)) D
 .. I ($Y+5)>IOSL D HDR1 Q:IBSTOP
 .. W $$BN1^PRCAFN(+IB),?13,$E($P(IB,U,2),1,25),?40,$E($P(IB,U,3),1,30),?72,$P($P(IB,U,4),"~"),?78,$$DAT1^IBOUTL($P(IB,U,5)),?88,$E($P(IB,U,7),1,10),?100,"$"_$J($P(IB,U,6),0,2),?110,$P(IB,U,10),?122,$P(IB,U,11),!
 .. W " FORM TYPE: "_$P($G(^IBE(353,$P($G(^DGCR(399,+IB,0)),U,19),0)),U),!
 .. I 'IBDIV S X=" DIVISION: "_$P(IB,U,8) W X,$J(" ",40-$L(X))_"AUTHORIZING BILLER: "_$P(IB,U,9),!
 .. W " MESSAGE TEXT: " S IBZFT=0
 .. S IBZ=0 F  S IBZ=$O(^IBM(361,IBDA,1,IBZ)) Q:'IBZ  D  Q:IBSTOP
 ... W:'IBZFT ?15 S X=$G(^IBM(361,IBDA,1,IBZ,0))
 ... F I=1:131:$L(X) W " "_$E(X,I,I+130),!
 ... S IBZFT=1
 ... I ($Y+5)>IOSL D HDR1 Q:IBSTOP
 .. W !
 G:IBSTOP LISTQ
 I $E(IOST,1,2)["C-" K DIR S DIR(0)="E" D ^DIR K DIR
LISTQ I $D(ZTQUEUED) S ZTREQ="@" Q
 W ! D ^%ZISC
 Q
IBPAY(IBX,IBX2) ;
 N X
 S X=$O(^TMP("IBCECSB",$J,IBX,IBX2,0))
 S X=$G(^TMP("IBCECSB",$J,IBX,IBX2,X))
 Q $P(X,U,2)
HDR1 ;
 N DIR,Y
 I IBPAGE D  Q:IBSTOP
 . I $E(IOST,1,2)["C-" K DIR S DIR(0)="E" D ^DIR K DIR S IBSTOP=('Y) Q:IBSTOP
 . W @IOF
 S IBPAGE=IBPAGE+1
 W !,?48,"CLAIMS STATUS AWAITING RESOLUTION REPORT",?115,"PAGE: ",IBPAGE,!,?55,"RUN DATE: ",$$HTE^XLFDT($H,"2"),!
 W ?55,"FIRST LEVEL SORT BY: "_$S(IBSORT1="A":"AUTHORIZING BILLER",IBSORT1="B":"BILL NUMBER",IBSORT1="D":"DIVISION",IBSORT1="N":"NUMBER OF DAYS PENDING",IBSORT1="E":"ERROR CODE",1:"PAYER")
 W !,?55,"SECOND LEVEL SORT BY: "_$S(IBSORT2="A":"AUTHORIZING BILLER",IBSORT2="B":"BILL NUMBER",IBSORT2="D":"DIVISION",IBSORT2="N":"NUMBER OF DAYS PENDING",IBSORT2="E":"ERROR CODE",1:"PAYER")
 I IBDIV W !,"DIVISION: "_$S($G(IBX)=0:"",1:$G(IBX)),!,"AUTHORIZING BILLER: "_$G(IBPAY)
 W !!,?72,"LAST",?78,"DATE OF",?88,"LOCATION",?100,"BILLED",?110,"SOURCE OF",?122,"DAYS MSG"
 W !,"BILL #",?13,"PAYER NAME",?40,"PATIENT NAME",?72,"4 SSN",?78,"SERVICE",?88,"OF SERVICE",?100,"AMOUNT",?110,"MESSAGE",?122,"PENDING"
 W !,$TR($J("",132)," ","-"),!
 Q
 ;
