IBCEP7A ;ALB/TMP - Functions for fac level PROVIDER ID MAINT ;11-07-00
 ;;2.0;INTEGRATED BILLING;**232**;21-MAR-94
 ;
IDNUM(IBIEN) ; Find site-default id # for id type
 ; IBIEN = ien of prov ID type (file 355.97)
 N IBID,Z0,Z1
 S IBID=""
 S Z0=$G(^IBE(355.97,IBIEN,0)),Z1=$G(^(1))
 I $P(Z1,U,9) G IDNUMQ
 I $P(Z0,U,4)'="" S IBID=$P(Z0,U,4) G IDNUMQ
 I $P(Z1,U,4) S IBID=$P($G(^IBE(350.9,1,1)),U,5)
 ;
IDNUMQ Q IBID
 ;
ADDFAC(IBINS) ; Add a new fac id for an ins co
 N IB,IBDIV,IBY,IBOK,IBRBLD,IBITYP,IBFORM,DIC,DIR,X,Y,DTOUT,DUOUT,DLAYGO,DO,DD,Z,Z0,DIE,DIK,DA
 S IBRBLD=0,IBY=-1
 S IBOK=$$FACFLDS^IBCEP7("",IBINS,.IBITYP,.IBFORM,.IBDIV,"A")
 I 'IBOK G ADDFQ
 ;
 S X=IBINS,DIC(0)="L",DIC="^IBA(355.92,",DIC("DR")=".04////"_IBFORM_$S($G(IBDIV):";.05////"_IBDIV,1:"")_";.06////"_IBITYP_";.07R",DLAYGO=355.92 D FILE^DICN K DIC,DLAYGO,DO,DD
 S IBY=+Y
ADDFQ I IBY>0,$P($G(^IBA(355.92,IBY,0)),U,7)="" S DIK="^IBA(355.92,",DA=IBY D ^DIK S IBY=-1
 I IBY'>0 S DIR("A",+$O(DIR("A"," "),-1)+1)="A NEW ID WAS NOT ADDED",IBRBLD=0
 I IBY>0 S DIR("A",1)="A NEW ID WAS ADDED SUCCESSFULLY",IBRBLD=1
 S DIR(0)="EA",DIR("A")="PRESS RETURN TO CONTINUE: " W ! D ^DIR K DIR
 Q IBRBLD
 ;
