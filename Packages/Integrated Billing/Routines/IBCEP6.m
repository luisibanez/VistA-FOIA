IBCEP6 ;ALB/TMP - PROVIDER ID MAINT menu and INS CO EDIT hook ;11-02-00
 ;;2.0;INTEGRATED BILLING;**137,232**;21-MAR-94
 ;
EN ; -- main entry point for IBCE PRV INS PARAMS
 D FULL^VALM1
 D EN^VALM("IBCE PRVMAINT")
ENQ Q
 ;
HDR ; -- header code
 K VALMHDR
 Q
 ;
INIT ; Initialization
 N IBLCT,IBCT,Z,Z0
 S (IBLCT,IBCT)=0,XQORM("B")="Select"
 K ^TMP("IBCE_PRVMAINT_MENU",$J)
 F Z=1:1:2 S Z0=$J("",10) D SET1(.IBLCT,Z0,1)
 S Z0=$J("",17)_"-- PROVIDER ID EDITS --" D SET1(.IBLCT,Z0,1),CNTRL^VALM10(IBLCT,18,23,IORVON,IORVOFF)
 S Z0=$J("",10)_"1 > PROVIDER SPECIFIC IDS" D SET1(.IBLCT,Z0,1)
 S Z0=$J("",14)_"o PROVIDER'S OWN IDS" D SET1(.IBLCT,Z0,1)
 S Z0=$J("",14)_"o PROVIDER IDS FURNISHED BY INSURANCE CO" D SET1(.IBLCT,Z0,1)
 S Z0=$J("",10)_"2 > INSURANCE CO IDS" D SET1(.IBLCT,Z0,2)
 S Z0=$J("",10)_"3 > FACILITY IDS" D SET1(.IBLCT,Z0,3)
 S Z0=$J("",10)_"4 > CARE UNIT MAINTENANCE" D SET1(.IBLCT,Z0,4)
 S Z0=$J("",10) D SET1(.IBLCT,Z0,5)
 S Z0=$J("",10)_"5 > INS CO BATCH ID ENTRY" D SET1(.IBLCT,Z0,5)
 F Z=1:1:2 S Z0=$J("",10) D SET1(.IBLCT,Z0,6)
 S Z0=$J("",14)_"-- NON/OTHER VA ENTITY EDITS --" D SET1(.IBLCT,Z0,6),CNTRL^VALM10(IBLCT,15,31,IORVON,IORVOFF)
 S Z0=$J("",10)_"6 > NON/OTHER VA PROVIDER ID INFORMATION" D SET1(.IBLCT,Z0,6)
 K VALMBG,VALMCNT
 S VALMBG=1,VALMCNT=IBLCT
 Q
 ;
SET1(IBLCT,Z0,IBCT) ;
 S IBLCT=IBLCT+1 D SET^VALM10(IBLCT,Z0,$G(IBCT))
 Q
 ;
EXPND ;
 Q
 ;
HELP ;
 Q
 ;
EXIT ;
 K ^TMP("IBCE_PRVMAINT_MENU",$J)
 D CLEAN^VALM10
 Q
 ;
SEL ;
 N Z,Z1,DIR
 D FULL^VALM1
 D EN^VALM2($G(XQORNOD(0)),"OS")
 S Z=+$O(VALMY(0))
 I Z,Z<6,'$D(^XUSEC("IB PROVIDER EDIT",DUZ)) S DIR(0)="EA",DIR("A",1)="YOU ARE NOT AUTHORIZED TO EDIT PROVIDER IDS",DIR("A")="Press ENTER to continue" W ! D ^DIR K DIR W ! G SELQ
 I Z S Z1=$P($T(ACT+Z),U,2,3) I Z1'="" D @Z1
SELQ K VALMBCK,XQORM("B")
 S VALMBCK="R",XQORM("B")="Quit"
 Q
 ;
EN1 ; Provider maintenance from the billing screen 8
 N DIR,X,Y,IBEDIT
 ;S IBEDIT=1
 W !
 ;S DIR(0)="YA",DIR("B")="NO",DIR("A",1)="WANT TO ATTEMPT TO RESET ALL PROVIDER IDS TO THE CALCULATED",DIR("A")="DEFAULTS FOR THIS BILL?: " D ^DIR K DIR
 ;Q:$D(DTOUT)!$D(DUOUT)
 ;I Y=1 S IBEDIT=0 D RECALCA^IBCEP2A(IBIFN) W !
 ;
 I '$D(^XUSEC("IB PROVIDER EDIT",DUZ)) S DIR(0)="EA",DIR("A")="Press ENTER to continue: ",DIR("A",1)="YOU LACK THE SECURITY KEY FOR THIS ACTION" D ^DIR K DIR Q
 ;I 'IBEDIT D
 ;. S DIR(0)="YA",DIR("A")="WANT TO CONTINUE WITH GENERAL PROVIDER ID MAINTENANCE?: ",DIR("B")="NO" D ^DIR K DIR
 ;. I $D(DTOUT)!$D(DUOUT)!'Y Q
 ;. S IBEDIT=1
 D EN
 Q
 ;
ACT ; Actions available
 ;;PROVIDER LEVEL ID EDIT^EN^IBCEP5
 ;;INS CO LEVEL ID EDIT^EN^IBCEP0
 ;;SITE LEVEL ID EDIT^EN^IBCEP7
 ;;CARE UNIT EDIT^EN^IBCEP4
 ;;BATCH ID ENTRY BY INS CO^EN^IBCEP9
 ;;NON-VA PROVIDER EDIT^EN^IBCEP8
 ;
