IBCECSA1 ;ALB/CXW - IB STATUS AWAITING RESOLUTION SCREEN ;28-JUL-99
 ;;2.0;INTEGRATED BILLING;**137,283,288**;21-MAR-94
 ; DBIA for $$ORI^PRCAFN() = DBIA380
 ; DBIA for $$BN1^PRCAFN() = 
 ;
BLD ; Build list entrypoint
 N IBDA,IBREV,IBIFN,IBPAY,IBSSN,IBSER,IB399,IBLOC,IBDIV,IBUER,IBMSG,IBERR,IBPEN,SEVERITY
 K ^TMP("IBCECSA",$J),^TMP("IBCECSB",$J),^TMP("IBCECSD",$J)
 S IBSEV=$G(IBSEV,"R")
 S VALMCNT=0,IB364=""
 S SEVERITY=""
 F  S SEVERITY=$O(^IBM(361,"ACSA",SEVERITY)) Q:SEVERITY=""  I SEVERITY="R"!(IBSEV="B") S IBREV="" F  S IBREV=$O(^IBM(361,"ACSA",SEVERITY,IBREV)) Q:IBREV=""  I IBREV<2 S IBDA=0 F  S IBDA=$O(^IBM(361,"ACSA",SEVERITY,IBREV,IBDA)) Q:'IBDA  D
 . S IB=$G(^IBM(361,IBDA,0)),IBIFN=+IB
 . S IBPEN=$$FMDIFF^XLFDT(DT,$P(IB,U,2),1)
 . ;quit if not pending for at least the minimum # of days requested
 . Q:IBDAYS>IBPEN
 . S IB399=$G(^DGCR(399,IBIFN,0))
 . ;
 . ; no cancelled claims allowed on the CSA screen
 . ; if we find one, then update the appropriate EDI files
 . I $P(IB399,U,13)=7 D UPDEDI^IBCEM(+$P(IB,U,11),"C") Q
 . ;
 . S IBDIV=+$P(IB399,U,22)
 . S IBUER=+$P($G(^DGCR(399,IBIFN,"S")),U,11)
 . ;
 . ; If Request MRA bill, pull the MRA Requestor user instead
 . I 'IBUER,$P(IB399,U,13)=2 S IBUER=+$P($G(^DGCR(399,IBIFN,"S")),U,8)
 . I $D(^TMP("IBBIL",$J)),'$D(^TMP("IBBIL",$J,IBUER)) Q  ; User not selected
 . I $D(^TMP("IBDIV",$J)),'$D(^TMP("IBDIV",$J,IBDIV)) Q  ; Div not selected
 . ;
 . S IBPAY=$P($G(^DIC(36,+$P($G(^DGCR(399,IBIFN,"MP")),U),0)),U)
 . I IBPAY="" S IBPAY=$P($G(^DIC(36,+$$CURR^IBCEF2(IBIFN),0)),U)
 . I IBPAY="" S IBPAY="UNKNOWN PAYER"
 . S IBPAT=$G(^DPT(+$P(IB399,U,2),0))
 . S IBSSN=$E($P(IBPAT,U,9),6,9)
 . S IBPAT=$P(IBPAT,U)
 . S IBSER=$P($G(^DGCR(399,IBIFN,"U")),U)
 . S IBLOC=$P(IB399,U,4)
 . S IBLOC=$S(IBLOC=1:"HOSPITAL",IBLOC=2:"SKILLED NURSING",1:"CLINIC")
 . I IBDIV S IBDIV=$P($G(^DG(40.8,IBDIV,0)),U) S:IBDIV=""&(IBSORT1="D"!(IBSORT2="D")) IBDIV="UNSPECIFIED"
 . S IBMSG=$S($P(IB,U,8):"PAYER",1:"NON-PAYER")
 . S IBUER=$S(IBUER:$P($G(^VA(200,IBUER,0)),U),1:"UNKNOWN")_"~"_IBUER
 . S IB364=$P(IB,U,11)
 . S IBOAM=$$ORI^PRCAFN(IBIFN)  ;original amt
 . ;
 . ;error code
 . S IBERR="-"
 . I IBSORT1="E"!(IBSORT2="E") D
 .. N Z
 .. I '$O(^IBM(361,IBDA,1,0)) Q
 .. S IB=0 F  S IB=$O(^IBM(361,IBDA,1,IB)) Q:'IB  S Z=^(IB,0) I $TR(Z,"error","ERROR")["ERROR" S IBERR=$E(Z,1,30) Q
 . S IB=$$BN1^PRCAFN(IBIFN)
 . S A=IBIFN_U_IBPAY_U_IBPAT_U_IBSSN_U_IBSER_U_IBOAM_U_IBLOC_U_IBDIV_U_IBUER_U_IBMSG_U_IBPEN_U_$S(IBREV:"*",1:"")_U_IB364_U_IB
 . S ^TMP("IBCECSB",$J,$S(IBSORT1="D":IBDIV,IBSORT1="A":IBUER,IBSORT1="P":IBPAY,IBSORT1="N":-IBPEN,IBSORT1="B":IB,1:IBERR),$S(IBSORT2="D":IBDIV,IBSORT2="A":IBUER,IBSORT2="P":IBPAY,IBSORT2="N":-IBPEN,IBSORT2="B":IB,1:IBERR),IBDA)=A
 ;
 I '$D(^TMP("IBCECSB",$J)) D NMAT Q
 D SCRN
 Q
 ;
NMAT ;No CSA list
 S VALMCNT=2,IBCNT=2
 S ^TMP("IBCECSA",$J,1,0)=" "
 S ^TMP("IBCECSA",$J,2,0)="No Messages Matching Selection Criteria Found"
 Q
 ;
SCRN ;
 N IBSRT1,IBSRT2,IBS1,IBS2,IBSS1,IBSS2,IBX,IBCNT,IBIFN,IBDA,IB,INFX,DAT,X
 S IBCNT=0
 S IBS1=$S(IBSORT1="A":"AUTHORIZING BILLER",IBSORT1="B":"BILL NUMBER",IBSORT1="D":"DIVISION",IBSORT1="P":"PAYER",IBSORT1="N":"NUMBER OF DAYS PENDING",1:"ERROR CODE")
 S IBS2=$S(IBSORT2="A":"AUTHORIZING BILLER",IBSORT2="B":"BILL NUMBER",IBSORT2="D":"DIVISION",IBSORT2="P":"PAYER",IBSORT2="N":"NUMBER OF DAYS PENDING",1:"ERROR CODE")
 S IBSRT1="" F  S IBSRT1=$O(^TMP("IBCECSB",$J,IBSRT1)) Q:IBSRT1=""  D
 . S IBSS1=1
 . S IBSRT2="" F  S IBSRT2=$O(^TMP("IBCECSB",$J,IBSRT1,IBSRT2)) Q:IBSRT2=""  S IBSS2=1,IBDA=0 F  S IBDA=$O(^TMP("IBCECSB",$J,IBSRT1,IBSRT2,IBDA)) Q:'IBDA  D
 .. S IB=$G(^TMP("IBCECSB",$J,IBSRT1,IBSRT2,IBDA))
 .. S IBIFN=+IB
 .. S IB364=$P(IB,U,13)
 .. S DAT=IBIFN_U_IBDA_U_IBSRT1_U_IBSRT2_U_IB364
 .. I IBSS1 D:IBCNT SET(" ",IBCNT,"") S IBSS1=0 D:$S("BPE"[IBSORT1&("BPE"[IBSORT2):0,1:1) SET(IBS1_": "_$S(IBSRT1="-":"NONE",1:$P(IBSRT1,"~")),$S(IBCNT:IBCNT,1:1),"")
 .. I IBSS2 S IBSS2=0 D:$S("BPE"[IBSORT2:0,1:1) SET("  "_IBS2_": "_$S(IBS2="-":"NONE",1:$P(IBSRT2,"~")),$S(IBCNT:IBCNT,1:1),"")
 .. S IBCNT=IBCNT+1
 .. S X=$$SETFLD^VALM1(IBCNT,"","NUMBER")
 .. D SETL1(IB,.X)
 .. D SET(X,IBCNT,DAT)
 .. S IBX=$O(^IBM(361,IBDA,1,0)) I IBX D  D SET(X,IBCNT,DAT)
 ... S X=IBX,(IBERR,INFX,IBX)=0
 ... F  S IBX=$O(^IBM(361,IBDA,1,IBX)) Q:'IBX  S IB=$G(^(IBX,0)),IBERR=$S(IB["Error":IBX,1:0) Q:IBERR  I IB["Informational",'INFX S INFX=IBX
 ... I IBERR S X=$$SETSTR^VALM1($G(^IBM(361,IBDA,1,IBERR,0)),"",4,200) Q
 ... I INFX S X=$$SETSTR^VALM1($G(^IBM(361,IBDA,1,INFX,0)),"",4,200) Q
 ... S X=$$SETSTR^VALM1($G(^IBM(361,IBDA,1,X,0)),"",4,200)
 Q
 ;
SET(X,CNT,DAT) ;set up list manager screen array
 S VALMCNT=VALMCNT+1
 S ^TMP("IBCECSA",$J,VALMCNT,0)=X
 S ^TMP("IBCECSA",$J,"IDX",VALMCNT,CNT)=""
 I DAT'="" S ^TMP("IBCECSA",$J,CNT)=VALMCNT_U_DAT
 Q
 ;
SETL1(IB,X) ;
 S X=$$SETFLD^VALM1($P(IB,U,14)_$P(IB,U,12),X,"BILL")
 S X=$$SETFLD^VALM1($P(IB,U,2),X,"PNAME")
 S X=$$SETFLD^VALM1($E($P(IB,U,3),1,20)_"/"_$P(IB,U,4),X,"PANAME")
 S X=$$SETFLD^VALM1($$DAT1^IBOUTL($P(IB,U,5)),X,"SERVICE")
 S X=$$SETFLD^VALM1("$"_$J($P(IB,U,6),0,2),X,"ABILL")
 Q
 ;
